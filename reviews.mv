<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "tgreviews">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Product Reviews">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "All the Reviews!">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, system, vis_system, json, clientside, component, fields_prod, fulfill, scheduledtask, vis_product, export, import">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Create Settings table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
							(
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) $	',
								value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) $ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0001', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_Settings. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Insert Defaults to TGReviews_Settings
	|		required
	|		mail_after
	|		merchant_email
	|		customer_email
	|		form_template
	|		reviews_template
	|		reviews_per_page
	|		reviews_default_sorting
	|		auto_approve
	|		recaptcha
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.defaults:required:email" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:location" VALUE = 0 />
	<MvASSIGN NAME = "l.defaults:required:name" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:rating" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:summary" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:title" VALUE = 1 />

	<MvASSIGN NAME = "l.required" VALUE = "{ miva_array_serialize( l.defaults:required ) }">
	<MvASSIGN NAME = "l.required_name" VALUE = "required" />

	<MvASSIGN NAME = "l.defaults:mail_after:notify" VALUE = 0 />
	<MvASSIGN NAME = "l.defaults:mail_after:days" VALUE = 7 />
	<MvASSIGN NAME = "l.defaults:mail_after:after" VALUE = "order" />
	<MvASSIGN NAME = "l.defaults:mail_after:from" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:mail_after:cc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:mail_after:bcc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:mail_after:subject" VALUE = "{ 'Please Review Your Recent Purchase on ' $ g.Store:name }" />

	<MvASSIGN NAME = "l.mail_after" VALUE = "{ miva_array_serialize( l.defaults:mail_after ) }">
	<MvASSIGN NAME = "l.mail_after_name" VALUE = "mail_after" />


	<MvASSIGN NAME = "l.defaults:merchant_email:notify" VALUE = 0 />
	<MvASSIGN NAME = "l.defaults:merchant_email:to" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:merchant_email:from" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:merchant_email:cc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:merchant_email:bcc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:merchant_email:subject" VALUE = "{ 'You have Received a Product Review on ' $ g.Store:name }" />

	<MvASSIGN NAME = "l.merchant_email" VALUE = "{ miva_array_serialize( l.defaults:merchant_email ) }">
	<MvASSIGN NAME = "l.merchant_email_name" VALUE = "merchant_email" />


	<MvASSIGN NAME = "l.defaults:customer_email:from" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:customer_email:cc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:customer_email:bcc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:customer_email:subject" VALUE = "{ 'Your Recent Product Review on ' $ g.Store:name }" />

	<MvASSIGN NAME = "l.customer_email" VALUE = "{ miva_array_serialize( l.defaults:customer_email ) }">
	<MvASSIGN NAME = "l.customer_email_name" VALUE = "customer_email" />

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tgreviews',  l.module:code ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0002:', 'An error occured while creating the item tgreviews.' ) }">
	</MvIF>

	<MvCAPTURE VARIABLE = "l.form_template:code">
		<MvINCLUDE FILE = "templates/form_template.mvt" INTERPRET = "OFF">
	</MvCAPTURE>
	<MvASSIGN NAME = "l.form_template:id" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.form_template:code, NULL, 'tgr_form.mvc' ) }">
	<MvASSIGN NAME = "l.form_template:name" VALUE = "form_template">

	<MvCAPTURE VARIABLE = "l.reviews_template:code">
		<MvINCLUDE FILE = "templates/reviews_template.mvt" INTERPRET = "OFF">
	</MvCAPTURE>
	<MvASSIGN NAME = "l.reviews_template:id" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.reviews_template:code, NULL, 'tgr_reviews.mvc' ) }">
	<MvASSIGN NAME = "l.reviews_template:name" VALUE = "reviews_template">

	<MvASSIGN NAME = "l.reviews_per_page" VALUE = "5" />
	<MvASSIGN NAME = "l.reviews_per_page_name" VALUE = "reviews_per_page" />

	<MvASSIGN NAME = "l.reviews_default_sorting" VALUE = "-id" />
	<MvASSIGN NAME = "l.reviews_default_sorting_name" VALUE = "reviews_default_sorting" />

	<MvASSIGN NAME = "l.auto_approve" VALUE = "0" />
	<MvASSIGN NAME = "l.auto_approve_name" VALUE = "auto_approve" />

	<MvASSIGN NAME = "l.recaptcha" VALUE = "" />
	<MvASSIGN NAME = "l.recaptcha_name" VALUE = "recaptcha" />

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? ),
								( ?, ? )' }"
				FIELDS	=	"	l.required_name, l.required,
								l.mail_after_name, l.mail_after,
								l.merchant_email_name, l.merchant_email,
								l.customer_email_name, l.customer_email,
								l.form_template:name, l.form_template:id,
								l.reviews_template:name, l.reviews_template:id,
								l.reviews_per_page_name, l.reviews_per_page,
								l.reviews_default_sorting_name, l.reviews_default_sorting,
								l.auto_approve_name, l.auto_approve,
								l.recaptcha_name, l.recaptcha">

	<MvASSIGN NAME = "l.merchant_email_page:admin"		VALUE = "0">
	<MvASSIGN NAME = "l.merchant_email_page:code"		VALUE = "TGR_Merchant_Email">
	<MvASSIGN NAME = "l.merchant_email_page:name"		VALUE = "Reviews Email Template: Merchant Notification">
	<MvASSIGN NAME = "l.merchant_email_page:ui_id"		VALUE = "0">
	<MvASSIGN NAME = "l.merchant_email_page:settings"	VALUE = "">
	<MvCAPTURE VARIABLE = "l.merchant_email_page:data">
		<MvINCLUDE FILE = "templates/merchant_email.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.merchant_email_page, l.merchant_email_page:data ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0003:', 'An error occured while creating the page TGR_Merchant_Email' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.merchant_email_page, 'store' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.merchant_email_page, 'urls' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.merchant_email_page, 'sitemap_exclude' ) }">

	<MvASSIGN NAME = "l.customer_email_page:admin"		VALUE = "0">
	<MvASSIGN NAME = "l.customer_email_page:code"		VALUE = "TGR_Customer_Email">
	<MvASSIGN NAME = "l.customer_email_page:name"		VALUE = "Reviews Email Template: Customer Notification">
	<MvASSIGN NAME = "l.customer_email_page:ui_id"		VALUE = "0">
	<MvASSIGN NAME = "l.customer_email_page:settings"	VALUE = "">
	<MvCAPTURE VARIABLE = "l.customer_email_page:data">
		<MvINCLUDE FILE = "templates/customer_email.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.customer_email_page, l.customer_email_page:data ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0004:', 'An error occured while creating the page TGR_Customer_Email' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.customer_email_page, 'store' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.customer_email_page, 'urls' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.customer_email_page, 'sitemap_exclude' ) }">

	<MvASSIGN NAME = "l.mail_after_page:admin"		VALUE = "0">
	<MvASSIGN NAME = "l.mail_after_page:code"		VALUE = "TGR_MailAfter_Email">
	<MvASSIGN NAME = "l.mail_after_page:name"		VALUE = "Reviews Email Template: Mail After Email">
	<MvASSIGN NAME = "l.mail_after_page:ui_id"		VALUE = "0">
	<MvASSIGN NAME = "l.mail_after_page:settings"	VALUE = "">
	<MvCAPTURE VARIABLE = "l.mail_after_page:data">
		<MvINCLUDE FILE = "templates/mailafter_email.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.mail_after_page, l.mail_after_page:data ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0005:', 'An error occured while creating the page TGR_Merchant_Email' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.mail_after_page, 'store' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.mail_after_page, 'urls' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.mail_after_page, 'sitemap_exclude' ) }">

	<MvCOMMENT>
	|
	|	Create Product Reviews table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Products
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								created		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cust_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								order_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								approved	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								rating		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								email		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								location	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								notify		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()	 		$ ',
								title		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								summary		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								store_reply	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								notified	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								verified	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0006', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_Products. Please make sure this table was not already created.' ) }">
	</MvIF>


	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviews_Products ON ' $ g.Store_Table_Prefix $ 'TGReviews_Products ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create Additional Fields table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0008', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields ON ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create Additional Field Values table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalValues
							(
								field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								rating_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0010', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalValues. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create TGReviews_Orders Table (for order followup email)
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Orders
							(
								order_id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) $ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0011', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_Orders. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviews_Orders ON ' $ g.Store_Table_Prefix $ 'TGReviews_Orders ( order_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create Store Keys.
	|		TGRProduct, TGRAdditional
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGRProduct', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0013', 'An error occured while creating the store key, TGRProduct. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGRAdditional', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0014', 'An error occured while creating the store key, TGRAdditional. Please make sure this does not already exsist.' ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ Module_Settings( l.mod_settings ) }">

	<MvIF EXPR = "{ [ g.Module_Library_DB ].Module_Load_Code( l.module:code, l.module ) }">
		<MvASSIGN NAME = "l.item:code"		VALUE = "{ 'tgreviews' }">
		<MvASSIGN NAME = "l.item:module_id"	VALUE = "{ l.module:id }">
		<MvASSIGN NAME = "l.void" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Item( l.item ) }">
	</MvIF>	

	<MvASSIGN NAME = "l.void"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.mod_settings:form_template ) }">
	<MvASSIGN NAME = "l.void"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.mod_settings:reviews_template ) }">

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_Merchant_Email', l.merchant_email_page ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.merchant_email_page ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_Customer_Email', l.merchant_email_page ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.merchant_email_page ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_MailAfter_Email', l.mail_after_page ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.mail_after_page ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Settings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Products'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalValues'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Orders' }">

	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGRProduct' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGRAdditional' ) }">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	SYSTEM
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.action EQ 'TGRADD' }">
		<MvASSIGN NAME = "l.void" VALUE = "{ FrontEnd_Add_Review() }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	VIS_SYSTEM
| ====================================================================================
</MvCOMMENT>


<MvFUNCTION NAME = "Module_System_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "TGR_SETT:Reviews: Settings,TGR_SETT/TGR_FIELDS:General Settings,TGR_SETT/TGR_EMAIL:Email Notifications,TGR_SETT/TGR_TEMPL:Templates & Items,TGR_ADDL:Reviews: Additional Fields,TGR_PROD:Reviews: Product Reviews">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ( l.tab NE 'TGR_SETT' ) AND ( l.tab NE 'TGR_PROD' ) AND ( l.tab NE 'TGR_ADDL' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_PROD' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_ProductLookup.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_Functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_Batchlist.js' }"></script>
		<script language="JavaScript">
			<MvASSIGN NAME = "l.void" VALUE = "{ Load_Setting( 'auto_approve', l.auto_approve ) }">
			<MvIF EXPR = "{ l.auto_approve EQ 1 }">var TGR_Auto_Approve = 1;<MvELSE>var TGR_Auto_Approve = 0;</MvIF>
			var TGR_Product_ID = 0;
			var TGR_Product_Code = '';
			MMScreen_LoadFinished( function() { new Reviews_Batchlist(); } );
		</script>
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TGR_ADDL' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=AdditionalFields_Functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=AdditionalFields_Batchlist.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new AdditionalFields_Batchlist(); } );
		</script>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ( l.tab NE 'TGR_SETT' ) AND ( l.tab NE 'TGR_PROD' ) AND ( l.tab NE 'TGR_ADDL') }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_SETT' ) }">
		<MvFUNCTIONRETURN VALUE = "{ Settings_UI( l.module ) }">
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_PROD' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsReviews_Batchlist"></div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_HTML() }">
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_ADDL' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsAdditionalFields_Batchlist"></div>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( g.Review_Tab EQ 'TGR_SETT' )  }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Review_Settings_Update( g.Required, g.Mail_After, g.Merchant_Email, g.Customer_Email, g.Reviews_Per_Page, g.Reviews_Default_Sorting, g.Auto_Approve, g.Recaptcha) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Reviews_Templates_Update( g.Reviews_Template ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	JSON
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT g.Admin_Open_Store }"><MvFUNCTIONRETURN></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Load_Query'			}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Load_Query( l.module )		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Update'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Update( l.module )			}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Insert'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Insert( l.module )			}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Load_Additional_Fields'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Load_Additional_Fields( l.module )			}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Delete' 				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Delete( l.module ) 		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Review_Approved_Update' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Review_Approved_Update( l.module ) 		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Review_Notify_Update' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Review_Notify_Update( l.module ) 			}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Review_Verified_Update'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Review_Verified_Update( l.module ) 		}"></MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Load_Query' 	}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Load_Query( l.module ) 	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Update' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Update( l.module ) 		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Insert' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Insert( l.module ) 		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Delete' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Delete( l.module ) 		}"></MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	CLIENTSIDE
| ====================================================================================
</MvCOMMENT>
<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Filename EQ 'Reviews_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/reviews_batchlist.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Reviews_Functions.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/reviews_functions.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Reviews_ProductLookup.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/reviews_productlookup.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'AdditionalFields_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/additionalfields_batchlist.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'AdditionalFields_Functions.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/additionalfields_functions.js">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	COMPONENT
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	|	Parameters & Function check
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.function_name EQ 'form_template' )				OR
					( l.function_name EQ 'reviews_template' )			OR
					( l.function_name EQ 'date' )						OR
					( l.function_name EQ 'product_rating' )				OR
					( l.function_name EQ 'product_review_breakdowns' )	OR
					( l.function_name EQ 'product_review_count' )		OR
					( l.function_name EQ 'test_mailafter_email' )
				}">
		<MvASSIGN NAME = "l.new_param" VALUE = "{ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	</MvIF>
	
	<MvCOMMENT>
	|
	|	tagerror
	|	22, 3 = display
	|	22, 0 = no display
	|
	|	FOR DEBUGGING, leave as 22, 3
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.new_param }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 0
			do_function
			pop
			tagerror	22, 0
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:
	</MvASSEMBLY>

</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	FIELDS_PROD
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">

	<MvASSIGN NAME = "l.fields" INDEX = "1" MEMBER = "code" VALUE = "tgr_rating">
	<MvASSIGN NAME = "l.fields" INDEX = "1" MEMBER = "name" VALUE = "Product Rating">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'tgr_rating' }">
		<MvFUNCTIONRETURN VALUE = "Product Rating">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Value" PARAMETERS = "module var, product_id, code" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ Product_Rating( l.module, l.param, l.all_settings, l.product_id, l.rating ) }">
	<MvFUNCTIONRETURN VALUE = "{ l.rating }" />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Set_Field" PARAMETERS = "module var, product_id, code, value" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	FULFILL
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "FulfillmentModule_ProcessOrder" PARAMETERS = "module var, order var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Send order:id to Reviews Order table.
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.void" VALUE = "{ Load_Setting( 'mail_after', l.mail_after ) }">
	<MvASSIGN NAME = "l.mail_after" VALUE = "{ miva_array_deserialize( l.mail_after ) }">

	<MvIF EXPR = "{ l.mail_after:notify NE 1 }">
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>

	<MvASSIGN NAME = "l.order_source"		VALUE = "{ tolower( l.order:source ) }">
	<MvIF EXPR = "{ [ g.Module_Library_Utilities ].Value_In_List( l.order_source, 'subscription,marketplaces_ebay,marketplaces_amazon,marketplaces_etsy' ) }">
		<MvFUNCTIONRETURN VALUE = 1 />
	</MvIF>

	<MvASSIGN NAME = "l.void" VALUE = "{ Reviews_Add_Order( l.order:id ) }">

	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
| SCHEDULEDTASK
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ScheduledTaskModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:provision_settings" VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision" VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Operations" PARAMETERS = "module var, operations var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.operations" INDEX = "1" MEMBER = "code"		VALUE = "tgr_order_emails">
	<MvASSIGN NAME = "l.operations" INDEX = "1" MEMBER = "descrip"	VALUE = "Trigger 'Mail After' Emails for Reviews">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Fields" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Validate" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Update" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Delete" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Execute" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.task:operation EQ 'tgr_order_emails' }">
		<MvFUNCTIONRETURN VALUE = "{ Trigger_MailAfter_Email() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-MOD-1000', 'Unsupported operation' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Provision_Settings" PARAMETERS = "module var, task var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
| VIS_PRODUCT
| ====================================================================================
</MvCOMMENT>


<MvFUNCTION NAME = "Module_Product_Tabs" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = "TGR_PROD:Product Reviews">
</MvFUNCTION>


<MvFUNCTION NAME = "Module_Product_Head" PARAMETERS = "module var, tab, product var" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ l.tab NE 'TGR_PROD' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
	<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_Functions.js' }"></script>
	<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_Batchlist.js' }"></script>
	<script language="JavaScript">
		var TGR_Product_ID = <MvEVAL EXPR = "{ l.product:id }">;
		var TGR_Product_Code = "<MvEVAL EXPR = "{ encodejavascriptstring( l.product:code ) }">";
		MMScreen_LoadFinished( function() { new Reviews_Batchlist(); } );
	</script>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvFUNCTION NAME = "Module_Product_Content" PARAMETERS = "module var, tab, load_fields, product var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
<MvIF EXPR = "{ l.tab NE 'TGR_PROD'}">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
	<div id="jsReviews_Batchlist"></div>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "Module_Product_Update" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Insert" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Delete" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	EXPORT
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Starting at 250 each iteration.
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "g.Exp_Count" VALUE = "250" />

	<MvIF EXPR = "{ NOT g.Review_Export_Continue }">
		<MvIF EXPR = "{ fexists( '/' $ g.Reviews_Export_File ) }">
			<MvASSIGN NAME = "l.success" VALUE = "{ fdelete( '/' $ g.Reviews_Export_File ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.Exp_Review_Count }">
		<MvASSIGN NAME = "l.csv_header" VALUE = "{ Export_Header_Template() }">
		<MvASSIGN NAME = "l.success" VALUE = "{ file_create( '/' $ g.Reviews_Export_File, 'data', l.csv_header ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'Review Export Progress', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">

	<MvIF EXPR = "{ g.Total_Review_Count }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Exp_Review_Count, g.Total_Review_Count ) }">
	</MvIF>

	<MvEVAL EXPR = "{ Export_Reviews( l.reviews ) }">
	
	<MvASSIGN NAME = "l.output" VALUE = "" />
	<MvFOREACH ITERATOR = "l.review" ARRAY = "l.reviews">
		<MvASSIGN NAME = "l.output" VALUE = "{ l.output $ CSV_Add( l.review, g.Exp_Addl_Fields ) }">

		<MvASSIGN NAME = "g.Exp_Review_Count" VALUE = "{ g.Exp_Review_Count + 1 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.Exp_Review_Count, g.Total_Review_Count ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ fexists( '/' $ g.Reviews_Export_File ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ file_append( '/' $ g.Reviews_Export_File, 'data', l.output ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.success" VALUE = "{ file_create( '/' $ g.Reviews_Export_File, 'data', l.output ) }">
	</MvIF>

	<MvIF EXPR = "{ g.Exp_Review_Count GE g.Total_Review_Count }">

		<MvIF EXPR = "{ g.ReviewsExport_SendEmail }">
			<MvDO FILE = "{ g.Module_Library_Utilities }" NAME = "l.null" VALUE = "{ Send_Email_Attachment( g.ReviewsExport_Email, g.ReviewsExport_Email, '', 'Reviews Export', g.Reviews_Export_File, '/', 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'TGReviews-SUCCESS-1', 'Review Export Email Sent' ) }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( 'Exported Reviews.' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'TGReviews-SUCCESS-2', 'Reviews Exported to ' $ g.Reviews_Export_File  ) }">

		<MvASSIGN NAME = "g.Review_Export_Continue" VALUE = "0">
		<MvASSIGN NAME = "g.Exp_Review_Count" VALUE = "0">
	<MvELSE>
		<MvASSIGN NAME = "g.Review_Export_Continue" VALUE = "1">
	</MvIF>

	<MvHIDE FIELDS = "	g.Exp_Offset,
						g.Review_Export_Continue,
						g.Module_Code,
						g.Exp_Review_Count,
						g.Reviews_Export_File,
						g.ReviewsExport_SendEmail,
						g.ReviewsExport_Email,
						g.Total_Review_Count,
						g.Exp_Addl_Fields">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.Reviews_Export_File"	VALUE = "{ trim( g.Reviews_Export_File ) }">

	<MvIF EXPR = "{ len( g.Reviews_Export_File ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Reviews_Export_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.Reviews_Export_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'Reviews_Export_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "g.ReviewsExport_Email" 	VALUE = "{ trim( g.ReviewsExport_Email ) }">

	<MvIF EXPR = "{ g.ReviewsExport_SendEmail }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.ReviewsExport_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'ReviewsExport_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ISNULL g.Reviews_Export_File }">
		<MvASSIGN NAME = "g.Reviews_Export_File" VALUE = "reviews.csv">
	</MvIF>

	<MvIF EXPR = "{ g.Review_Export_Continue EQ 1 }">
		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export Reviews to CSV', 'EXPT', '' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting Reviews...', 'EXPT', 'EXPT' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export Reviews to CSV', 'EXPT', '' ) }">
		<MvDO FILE = "{ g.Module_Admin }" NAME = "l.ok" VALUE = "{ BeginContent() }">

		<div style="font-family: sans-serif;">
			<MvHIDE FIELDS = "g.Module_Code">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Export Settings', 150 ) }">
				<table cellspacing="5">
					<tr>
						<td valign="top"><strong>Export Product Reviews to File:</strong></td>
						<td width="100%">
							<input type="text" name="Reviews_Export_File" size="40" value="{ encodeentities( g.Reviews_Export_File ) }"><br />
							<em>This is located in the private directory. This will be deleted if it already exists.</em>
						</td>
					</tr>
					<tr>
						<td valign="top">Email File To:</td>
						<td width="100%">
							<MvIF EXPR = "{ NOT g.ReviewsExport_SendEmail }">
								<label><input type="radio" name="ReviewsExport_SendEmail" value="0" checked>Do Not Email<br></label>
								<input type="radio" name="ReviewsExport_SendEmail" value="1">
							<MvELSE>
								<label><input type="radio" name="ReviewsExport_SendEmail" value="0">Do Not Email<br></label>
								<input type="radio" name="ReviewsExport_SendEmail" value="1" checked>
							</MvIF>
							<MvIF EXPR = "{ ISNULL g.ReviewsExport_Email }"><MvASSIGN NAME = "g.ReviewsExport_Email" VALUE = "{ g.Store:email }"></MvIF>
							<input type="text" size="40" name="ReviewsExport_Email" value="{ encodeentities( g.ReviewsExport_Email ) }">
						</td>
					</tr>
				</table>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	IMPORT
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ImportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:screen"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:persistent"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:format"				VALUE = "delimited">
	<MvASSIGN NAME = "l.capabilities:persistent_provision"	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Columns" PARAMETERS = "module var, import var, columns var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>											field					name						header				required	default			validation		</MvCOMMENT>
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'created',				'Created',					'CREATED',				0,			'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'product_code',			'Product Code',				'PRODUCT_CODE',			1,			'',			'code' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'order_id',				'Order ID',					'ORDER_ID',				0,			'',			'integer_nonneg' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'approved',				'Approved',					'APPROVED',				1,			'',			'bool' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'rating',				'Rating',					'RATING',				1,			'',			'integer_nonneg' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'name',					'Name',						'NAME',					0,			0,			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'email',				'Email',					'EMAIL',				0,			0,			'email' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'location',				'Location',					'LOCATION',				0,			0,			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'notify',				'Notify',					'NOTIFY',				0,			0,			'bool' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'title',				'Title',					'TITLE',				1,			0,			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'summary',				'Summary',					'SUMMARY',				1,			0,			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'store_reply',			'Store Reply',				'STORE_REPLY',			0,			0,			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'notified',				'Notified',					'NOTIFIED',				0,			0,			'integer_nonneg' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'verified',				'Verified',					'VERIFIED',				0,			0,			'bool' ) }">

	<MvFOREACH ITERATOR = "l.field" ARRAY = "g.Exp_Addl_Fields" COUNT = "{ Load_Additional_Fields( g.Exp_Addl_Fields ) }">
		<MvEVAL EXPR = "{ Add_Standard_Column( l.columns, 'field_' $ l.field:code, 'Field: ' $ l.field:name, 'FIELD:'$ toupper( l.field:code ), 0, 0, 'string' ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ miva_array_max( l.columns ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_StatusFields" PARAMETERS = "module var, import var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields"	VALUE = "">

	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'records_processed',	'Records Processed:',	0 ) }">
	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'records_skipped',	'Records Skipped:',		0 ) }">
	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'record_problems',	'Record Problems:',		0 ) }">

	<MvFUNCTIONRETURN VALUE = "{ miva_array_max( l.fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_Begin" PARAMETERS = "module var, import var, session var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Create_Columns_Present( l.import, l.run_data:columns_present ) }">

	<MvIF EXPR = "{ l.import:automap EQ 1 }">
		<MvIF EXPR = "{ NOT Validate_Columns( l.import, l.run_data:columns_present ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

    <MvASSIGN NAME = "l.list_pos"				VALUE = 0>
    <MvFOREACH ITERATOR = "l.column" ARRAY = "l.run_data:columns_all" INDEX = "l.index" COUNT = "{ ImportModule_Delimited_Columns( l.module, l.import, l.run_data:columns_all ) }">
        <MvIF EXPR = "{ miva_variable_value( 'l.run_data:columns_present:' $ l.column:field ) }">
            <MvASSIGN NAME = "l.column:present"	VALUE = 1>

            <MvREFERENCE NAME = "l.run_data:columns_list"		INDEX = "{ ++l.list_pos }"		VARIABLE = "{ 'l.run_data:columns_all[' $ l.index $ ']' }">
            <MvREFERENCE NAME = "l.run_data:columns_present"	MEMBER = "{ l.column:field }"	VARIABLE = "{ 'l.run_data:columns_all[' $ l.index $ ']' }">
        </MvIF>
    </MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_Record" PARAMETERS = "module var, import var, session var, record var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_IMP_UT ].Import_Session_StatusField_Increment( l.session, 'records_processed', 1 ) }">

	<MvIF EXPR = "{ ISNULL l.record:product_code OR ISNULL l.record:rating OR ISNULL l.record:title OR ISNULL l.record:summary }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Product Code, Rating, Title & Summary are required.' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Reviews_Import( l.import, l.session, l.record, l.run_data ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_End" PARAMETERS = "module var, import var, session var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Fields" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Validate" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.import:automap EQ 0 }">
		<MvEVAL EXPR = "{ Create_Columns_Present( l.import, l.columns_present ) }">

		<MvIF EXPR = "{ NOT Validate_Columns( l.import, l.columns_present ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Update" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Provision" PARAMETERS = "module var, import var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvINCLUDE FILE = "functions.mv">
<MvINCLUDE FILE = "settings.mv">
<MvINCLUDE FILE = "component_helpers.mv">
<MvINCLUDE FILE = "export.mv">
<MvINCLUDE FILE = "import.mv">