<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "tgreviews">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Reviews! With things that work!">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "All the Reviews!">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, system, vis_system, json, clientside, component">
	<MvCOMMENT><MvASSIGN NAME = "l.module:features"	VALUE = "vis_product, import, export"></MvCOMMENT>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Create Settings table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
							(
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) $	',
								value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) $ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0001', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_Settings. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Insert Defaults to TGReviews_Settings
	|		required
	|		mail_after
	|		merchant_email
	|		customer_email
	|		form_template
	|		reviews_template
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.defaults:required:email" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:location" VALUE = 0 />
	<MvASSIGN NAME = "l.defaults:required:name" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:rating" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:summary" VALUE = 1 />
	<MvASSIGN NAME = "l.defaults:required:title" VALUE = 1 />

	<MvASSIGN NAME = "l.required" VALUE = "{ miva_array_serialize( l.defaults:required ) }">
	<MvASSIGN NAME = "l.required_name" VALUE = "required" />

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? )' }"
				FIELDS	=	"l.required_name, l.required">

	<MvASSIGN NAME = "l.defaults:mail_after:notify" VALUE = 0 />
	<MvASSIGN NAME = "l.defaults:mail_after:days" VALUE = 7 />
	<MvASSIGN NAME = "l.defaults:mail_after:after" VALUE = "order" />
	<MvASSIGN NAME = "l.defaults:mail_after:from" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:mail_after:cc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:mail_after:bcc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:mail_after:subject" VALUE = "{ 'Please Review Your Recent Purchase on ' $ g.Store:name }" />

	<MvASSIGN NAME = "l.mail_after" VALUE = "{ miva_array_serialize( l.defaults:mail_after ) }">
	<MvASSIGN NAME = "l.mail_after_name" VALUE = "mail_after" />

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? )' }"
				FIELDS	=	"l.mail_after_name, l.mail_after">

	<MvASSIGN NAME = "l.defaults:merchant_email:notify" VALUE = 0 />
	<MvASSIGN NAME = "l.defaults:merchant_email:to" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:merchant_email:from" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:merchant_email:cc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:merchant_email:bcc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:merchant_email:subject" VALUE = "{ 'You have Received a Product Review on ' $ g.Store:name }" />

	<MvASSIGN NAME = "l.merchant_email" VALUE = "{ miva_array_serialize( l.defaults:merchant_email ) }">
	<MvASSIGN NAME = "l.merchant_email_name" VALUE = "merchant_email" />

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? )' }"
				FIELDS	=	"l.merchant_email_name, l.merchant_email">

	<MvASSIGN NAME = "l.defaults:customer_email:from" VALUE = "{ g.Store:email }" />
	<MvASSIGN NAME = "l.defaults:customer_email:cc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:customer_email:bcc" VALUE = "" />
	<MvASSIGN NAME = "l.defaults:customer_email:subject" VALUE = "{ 'Your Recent Product Review on ' $ g.Store:name }" />

	<MvASSIGN NAME = "l.customer_email" VALUE = "{ miva_array_serialize( l.defaults:customer_email ) }">
	<MvASSIGN NAME = "l.customer_email_name" VALUE = "customer_email" />

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? )' }"
				FIELDS	=	"l.customer_email_name, l.customer_email">

	<MvASSIGN NAME = "l.form_template:code" VALUE = "tbd" />
	<MvASSIGN NAME = "l.form_template:id" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.form_template:code, NULL, 'tgr_form.mvc' ) }">
	<MvASSIGN NAME = "l.form_template:name" VALUE = "form_template">

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? )' }"
				FIELDS	=	"l.form_template:name, l.form_template:id">

	<MvASSIGN NAME = "l.reviews_template:code" VALUE = "tbd" />
	<MvASSIGN NAME = "l.reviews_template:id" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.reviews_template:code, NULL, 'tgr_reviews.mvc' ) }">
	<MvASSIGN NAME = "l.reviews_template:name" VALUE = "reviews_template">

	<MvQUERY	NAME	=	"Merchant"
				QUERY	=	"{	'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviews_Settings
								( name, value )
								VALUES
								( ?, ? )' }"
				FIELDS	=	"l.reviews_template:name, l.reviews_template:id">

	<MvCOMMENT>
	|
	|	Create Product Reviews table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Products
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								created		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cust_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								order_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								approved	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								rating		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								email		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								location	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								notify		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()	 		$ ',
								title		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								summary		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0002', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_Products. Please make sure this table was not already created.' ) }">
	</MvIF>


	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviews_Products ON ' $ g.Store_Table_Prefix $ 'TGReviews_Products ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create Additional Fields table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0006', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields ON ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create Additional Field Values table.
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalValues
							(
								field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								rating_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0008', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalValues. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	Create Store Keys.
	|		TGRProduct, TGRAdditional
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGRProduct', 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0010', 'An error occured while creating the store key, TGRProduct. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGRAdditional', 1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGReviews-INSTALL-0012', 'An error occured while creating the store key, TGRAdditional. Please make sure this does not already exsist.' ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.void" VALUE = "{ Module_Settings( l.mod_settings ) }">

	<MvASSIGN NAME = "l.void"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.mod_settings:form_template ) }">
	<MvASSIGN NAME = "l.void"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.mod_settings:reviews_template ) }">

	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGReviews_Settings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGReviews_Products'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGReviews_AdditionalFields'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $  g.Store_Table_Prefix $ 'TGReviews_AdditionalValues'}">

	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGRProduct' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGRAdditional' ) }">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvCOMMENT>
| 
|	system
| 
</MvCOMMENT>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.action EQ 'TGRADD' }">
		<MvASSIGN NAME = "l.void" VALUE = "{ FrontEnd_Add_Review() }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	vis_system
|
</MvCOMMENT>


<MvFUNCTION NAME = "Module_System_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "TGR_SETT:Reviews: Settings,TGR_SETT/TGR_FIELDS:Field Settings,TGR_SETT/TGR_EMAIL:Email Notifications,TGR_SETT/TGR_TEMPL:Templates,TGR_ADDL:Reviews: Additional Fields,TGR_PROD:Reviews: Product Reviews">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ( l.tab NE 'TGR_SETT' ) AND ( l.tab NE 'TGR_PROD' ) AND ( l.tab NE 'TGR_ADDL' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_PROD' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_ProductLookup.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_Functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=Reviews_Batchlist.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new Reviews_Batchlist(); } );
		</script>
	</MvIF>
	<MvIF EXPR = "{ l.tab EQ 'TGR_ADDL' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=AdditionalFields_Functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=AdditionalFields_Batchlist.js' }"></script>
		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new AdditionalFields_Batchlist(); } );
		</script>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ( l.tab NE 'TGR_SETT' ) AND ( l.tab NE 'TGR_PROD' ) AND ( l.tab NE 'TGR_ADDL') }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_SETT' ) }">
		<MvFUNCTIONRETURN VALUE = "{ Settings_UI( l.module ) }">
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_PROD' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsReviews_Batchlist"></div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_HTML() }">
	</MvIF>
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_ADDL' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">
		<div id="jsAdditionalFields_Batchlist"></div>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( g.Review_Tab EQ 'TGR_SETT' )  }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Review_Settings_Update( g.Required, g.Mail_After, g.Merchant_Email, g.Customer_Email) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Reviews_Templates_Update( g.Reviews_Template ) }">
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	json
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Load_Query'			}"><MvFUNCTIONRETURN VALUE = "{ Product_Reviews_Load_Query( l.module )		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Update'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Update( l.module )		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Insert'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Insert( l.module )		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Load_Additional_Fields'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Load_Additional_Fields( l.module )		}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Reviews_Delete' 				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Delete( l.module ) 	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Review_Approved_Update' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Review_Approved_Update( l.module ) 	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Review_Notify_Update' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Review_Notify_Update( l.module ) 		}"></MvIF>

	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Load_Query' 	}"><MvFUNCTIONRETURN VALUE = "{ AdditionalFields_Load_Query( l.module ) 	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Update' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Update( l.module ) 	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Insert' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Insert( l.module ) 	}"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Delete' 		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Delete( l.module ) 	}"></MvIF>
</MvFUNCTION>

<MvCOMMENT>
|
|	clientside
|
</MvCOMMENT>
<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Filename EQ 'Reviews_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/reviews_batchlist.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Reviews_Functions.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/reviews_functions.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'Reviews_ProductLookup.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/reviews_productlookup.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'AdditionalFields_Batchlist.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/additionalfields_batchlist.js">
	</MvIF>
	<MvIF EXPR = "{ g.Filename EQ 'AdditionalFields_Functions.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/additionalfields_functions.js">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ============================
|	COMPONENT
| ============================
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	|	Parameters & Function check
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ( l.function_name EQ 'form_template' )		OR
					( l.function_name EQ 'reviews_template' )
				}">
		<MvASSIGN NAME = "l.new_param" VALUE = "{ l.function_name $ '( l.module, l.param, ' $ l.parameters $ ' )' }">
	</MvIF>
	
	<MvCOMMENT>
	|
	|	tagerror
	|	22, 3 = display
	|	22, 0 = no display
	|
	|	FOR DEBUGGING, leave as 22, 3
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.new_param }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 3
			do_function
			pop
			tagerror	22, 3
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:
	</MvASSEMBLY>

</MvFUNCTION>

<MvINCLUDE FILE = "functions.mv">
<MvINCLUDE FILE = "settings.mv">

<MvCOMMENT>
|
|	Component Helpers
|		Parse_Function_Parameters( string, function_name var, parameters var, parameter_count var )
|		Parameter_Error( param, message )
|		Is_Variable( variable var )
|		Pretty_Code( function_name, paramaters )
|
</MvCOMMENT>

<MvFUNCTION NAME = "Parse_Function_Parameters" PARAMETERS = "string, function_name var, parameters var, parameter_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.parameter_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.loop_counter" 		VALUE = 0>
	<MvASSIGN NAME = "l.string" 			VALUE = "{ trim( l.string ) }">
	<MvASSIGN NAME = "l.starting_pos" 		VALUE = "{ indexof( '(', l.string, 1 ) + 1 }">
	<MvASSIGN NAME = "l.function_name" 		VALUE = "{ tolower( substring_var( l.string, 1, l.starting_pos - 2 ) ) }">

	<MvIF EXPR = "{ l.starting_pos EQ 1 }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Expected \'(\' to start the function call' ) }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.function_name }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Missing function name' ) }">
	</MvIF>

	<MvIF EXPR = "{ substring_var( l.string, len_var( l.string ), 1 ) NE ')' }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Expected \')\' to end the function call' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ 1 }">
		<MvASSIGN NAME = "l.quote_pos" 		VALUE = "{ indexof( '\'', l.string, l.starting_pos ) }">
		<MvASSIGN NAME = "l.end_quote_pos" 	VALUE = "{ indexof( '\'', l.string, l.quote_pos + 1 ) }">
		<MvASSIGN NAME = "l.comma_pos" 		VALUE = "{ indexof( ',', l.string, l.starting_pos ) }">

		<MvIF EXPR = "{ l.quote_pos AND NOT l.end_quote_pos }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Missing single quote' ) }">
		</MvIF>

		<MvWHILE EXPR = "{ ( l.comma_pos LT l.end_quote_pos ) AND ( l.comma_pos GT l.quote_pos ) }"> <MvCOMMENT> comma in a quoted string </MvCOMMENT>
			<MvASSIGN NAME = "l.comma_pos" VALUE = "{ indexof( ',', l.string, l.comma_pos + 1 ) }">
		</MvWHILE>

		<MvIF EXPR = "{ l.comma_pos EQ 0 }"> <MvCOMMENT> on the last parameter or we only have 1 parameter </MvCOMMENT>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ len_var( l.string ) - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ len_var( l.string ) }">
			<MvASSIGN NAME = "l.quote_pos" 		VALUE = 0>
		<MvELSE> <MvCOMMENT> if there are more commas to come, grab from the start to the comma</MvCOMMENT>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ l.comma_pos - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ l.comma_pos + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.param" VALUE = "{ trim( substring_var( l.string, l.start, l.end ) ) }">

		<MvIF EXPR = "{ ISNULL l.param }">
			<MvIF EXPR = "{ l.comma_pos EQ 0 AND l.parameter_count EQ 0 }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Parameter number ' $ ( ++l.parameter_count ) $ ' cannot be empty' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.parameters" INDEX = "{ ++l.parameter_count }" VALUE = "{ l.param }">

		<MvIF EXPR = "{ l.quote_pos EQ 0 AND l.comma_pos EQ 0 }">
			<MvWHILESTOP>
		</MvIF>

		<MvIF EXPR = "{ l.loop_counter GT 1000 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Error parsing parameters' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.loop_counter" VALUE = "{ l.loop_counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Parameter_Error" PARAMETERS = "param, message" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.error_count"														VALUE = "{ miva_array_elements( g.Session:wailist ) }">
	<MvASSIGN NAME = "g.Session:dev_util" INDEX = "{ l.error_count + 1 }" MEMBER = "param"	VALUE = "{ l.param }">
	<MvASSIGN NAME = "g.Session:dev_util" INDEX = "{ l.error_count + 1 }" MEMBER = "message"	VALUE = "{ l.message }">

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Is_Variable" PARAMETERS = "variable var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.valid_chars" 		VALUE = "_.: ">
	<MvASSIGN NAME = "l.square_bracket_pos" VALUE = 0>
	<MvASSIGN NAME = "l.bracket_count" 		VALUE = 0>

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.variable ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.variable, l.pos, 1 ) }">

		<MvIF EXPR = "{ l.pos EQ 1 }">
			<MvIF EXPR = "{ ( NOT isalpha( l.char ) ) AND
							( l.char NE '_' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFORCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.char EQ '[' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count + 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos + 1 ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ ']' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count - 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos + 1 ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ ( NOT isdigit( l.char ) ) AND
							( NOT isalpha( l.char ) ) AND
							( NOT ( l.char IN l.valid_chars ) ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOR>

	<MvIF EXPR = "{ l.bracket_count NE 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Pretty_Code" PARAMETERS = "function_name, parameters" STANDARDOUTPUTLEVEL = "html, text">
<div style="background: #ffffff; overflow:auto;width:auto;padding:.2em 0;">
<pre style="margin: 0; line-height: 125%;"><span style="color: #446fbd">&lt;mvt:item</span> <span style="color: #6d8600">name=</span><span style="color: #535353">&quot;tgreviews&quot;</span> <span style="color: #6d8600">param=</span><span style="color: #535353">&quot;<span style="color: #446fbd"><MvEVAL EXPR = "{ l.function_name }"></span>(<MvEVAL EXPR = "{ l.parameters }">)&quot;</span> <span style="color: #446fbd">/&gt;</span></pre>
</div>
</MvFUNCTION>