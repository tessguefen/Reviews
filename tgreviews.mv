<MIVA STANDARDOUTPUTLEVEL = "">

<MvCOMMENT>
|
| Prefix         : MOD-SYS-TGR-
| Next Error Code: 156
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "tgreviews">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Product Reviews">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "2.010">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.12">
	<MvASSIGN NAME = "l.module:description"	VALUE = "Product Reviews module allows your customers to easily write reviews for products. This module also allows for importing and exporting product reviews.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, system, vis_system, json, clientside, component, fields_prod, not_prod, fulfill, scheduledtask, vis_product, export, import, json_api, prod_facet">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature: data_store
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	| sNN_TGReviewsGeneralSettings
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsGeneralSettings
							(
								fld_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								fld_email	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								fld_loc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								fld_summry	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								fld_title	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								per_page	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								sort		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								auto_apprv	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								rc_enabled	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								rc_pub_key	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								rc_prv_key	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00001', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.general_settings:fld_name"		VALUE = 1>
	<MvASSIGN NAME = "l.general_settings:fld_email"		VALUE = 1>
	<MvASSIGN NAME = "l.general_settings:fld_loc"		VALUE = 0>
	<MvASSIGN NAME = "l.general_settings:fld_summry"	VALUE = 1>
	<MvASSIGN NAME = "l.general_settings:fld_title"		VALUE = 1>
	<MvASSIGN NAME = "l.general_settings:per_page"		VALUE = 5>
	<MvASSIGN NAME = "l.general_settings:sort"			VALUE = "-id">
	<MvASSIGN NAME = "l.general_settings:auto_apprv"	VALUE = 0>
	<MvASSIGN NAME = "l.general_settings:rc_enabled"	VALUE = 0>
	<MvASSIGN NAME = "l.general_settings:rc_pub_key"	VALUE = "">
	<MvASSIGN NAME = "l.general_settings:rc_prv_key"	VALUE = "">

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Insert( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_TGReviewsNotificationSettings
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsNotificationSettings
							(
								ma_enable	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								ma_days		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								ma_after	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
								ma_from		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								ma_cc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								ma_bcc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								ma_subject	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								mn_enable	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								mn_to		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								mn_from		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								mn_cc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								mn_bcc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								mn_subject	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								cn_from		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								cn_cc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								cn_bcc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
								cn_subject	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00002', g.MvQUERY_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.email_settings:ma_enable"	VALUE = 0>
	<MvASSIGN NAME = "l.email_settings:ma_days"		VALUE = 7>
	<MvASSIGN NAME = "l.email_settings:ma_after"	VALUE = "order">
	<MvASSIGN NAME = "l.email_settings:ma_from"		VALUE = "{ g.Store:email }">
	<MvASSIGN NAME = "l.email_settings:ma_cc"		VALUE = "">
	<MvASSIGN NAME = "l.email_settings:ma_bcc"		VALUE = "">
	<MvASSIGN NAME = "l.email_settings:ma_subject"	VALUE = "{ 'Please Review Your Recent Purchase on ' $ g.Store:name }">
	<MvASSIGN NAME = "l.email_settings:mn_enable"	VALUE = 0>
	<MvASSIGN NAME = "l.email_settings:mn_to"		VALUE = "{ g.Store:email }">
	<MvASSIGN NAME = "l.email_settings:mn_from"		VALUE = "{ g.Store:email }">
	<MvASSIGN NAME = "l.email_settings:mn_cc"		VALUE = "">
	<MvASSIGN NAME = "l.email_settings:mn_bcc"		VALUE = "">
	<MvASSIGN NAME = "l.email_settings:mn_subject"	VALUE = "{ 'You have Received a Product Review on ' $ g.Store:name }">
	<MvASSIGN NAME = "l.email_settings:cn_from"		VALUE = "{ g.Store:email }">
	<MvASSIGN NAME = "l.email_settings:cn_cc"		VALUE = "">
	<MvASSIGN NAME = "l.email_settings:cn_bcc"		VALUE = "">
	<MvASSIGN NAME = "l.email_settings:cn_subject"	VALUE = "{ 'Your Recent Product Review on ' $ g.Store:name }">

	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Insert( l.email_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_TGReviewsTemplates
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsTemplates
							(
								ma_tmpl_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								mn_tmpl_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cn_tmpl_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								form_tmpl	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								rvws_tmpl	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								prlog_tmpl	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00003', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tgreviews', l.module:code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00004', g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tgreviews_orderitems', l.module:code ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00154', g.Error_Message ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| ma_tmpl_id: TGR_MailAfter_Email
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.ma_template:admin"		VALUE = 1>
	<MvASSIGN NAME = "l.ma_template:code"		VALUE = "TGR_MailAfter_Email">
	<MvASSIGN NAME = "l.ma_template:name"		VALUE = "Reviews Email Template: Mail After">
	<MvASSIGN NAME = "l.ma_template:ui_id"		VALUE = "{ l.module:id }">
	<MvASSIGN NAME = "l.ma_template:settings"	VALUE = "">

	<MvCAPTURE VARIABLE = "l.ma_template_data">
		<MvINCLUDE FILE = "templates/TGR_MailAfter_Email.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.ma_template, l.ma_template_data ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.ma_template, 'store' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.ma_template, 'urls' ) }">

	<MvCOMMENT>
	|
	| Miva auto-adds `order`. Need to remove it as it causes issues.
	|
	</MvCOMMENT>
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Unassign_Item( l.ma_template, 'order' ) }">

	<MvCOMMENT>
	|
	| mn_tmpl_id: TGR_Merchant_Email
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.mn_template:admin"		VALUE = 1>
	<MvASSIGN NAME = "l.mn_template:code"		VALUE = "TGR_Merchant_Email">
	<MvASSIGN NAME = "l.mn_template:name"		VALUE = "Reviews Email Template: Merchant Notification">
	<MvASSIGN NAME = "l.mn_template:ui_id"		VALUE = "{ l.module:id }">
	<MvASSIGN NAME = "l.mn_template:settings"	VALUE = "">

	<MvCAPTURE VARIABLE = "l.mn_template_data">
		<MvINCLUDE FILE = "templates/TGR_Merchant_Email.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.mn_template, l.mn_template_data ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.mn_template, 'store' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.mn_template, 'urls' ) }">

	<MvCOMMENT>
	|
	| mc_tmpl_id: TGR_Merchant_Email
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.cn_template:admin"		VALUE = 1>
	<MvASSIGN NAME = "l.cn_template:code"		VALUE = "TGR_Customer_Email">
	<MvASSIGN NAME = "l.cn_template:name"		VALUE = "Reviews Email Template: Customer Notification">
	<MvASSIGN NAME = "l.cn_template:ui_id"		VALUE = "{ l.module:id }">
	<MvASSIGN NAME = "l.cn_template:settings"	VALUE = "">

	<MvCAPTURE VARIABLE = "l.cn_template_data">
		<MvINCLUDE FILE = "templates/TGR_Customer_Email.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Page_LowLevel( l.cn_template, l.cn_template_data ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.cn_template, 'store' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.cn_template, 'urls' ) }">

	<MvCOMMENT>
	|
	| form_tmpl: Form Template
	|
	</MvCOMMENT>

	<MvCAPTURE VARIABLE = "l.form_template_code">
		<MvINCLUDE FILE = "templates/tgr_form.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvASSIGN NAME = "l.form_settings"			VALUE = "">
	<MvASSIGN NAME = "l.templates:form_tmpl"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.form_template_code, l.form_settings, 'tgr_form.mvc' ) }">

	<MvIF EXPR = "{ NOT l.templates:form_tmpl }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| rvws_tmpl: Reviews Template
	|
	</MvCOMMENT>

	<MvCAPTURE VARIABLE = "l.reviews_template_code">
		<MvINCLUDE FILE = "templates/tgr_reviews.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvASSIGN NAME = "l.review_settings"		VALUE = "">
	<MvASSIGN NAME = "l.templates:rvws_tmpl"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.reviews_template_code, l.review_settings, 'tgr_reviews.mvc' ) }">

	<MvIF EXPR = "{ NOT l.templates:rvws_tmpl }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| prlog_tmpl: Pre-Submission Logic
	|
	</MvCOMMENT>

	<MvCAPTURE VARIABLE = "l.prelogic_template_code">
		<MvINCLUDE FILE = "templates/tgr_prelogic.mvt" INTERPRET = "OFF">
	</MvCAPTURE>

	<MvASSIGN NAME = "l.prelogic_settings"		VALUE = "">
	<MvASSIGN NAME = "l.templates:prlog_tmpl"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.prelogic_template_code, l.prelogic_settings, 'tgr_prelogic.mvc' ) }">

	<MvIF EXPR = "{ NOT l.templates:prlog_tmpl }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.templates:ma_tmpl_id"	VALUE = "{ l.ma_template:id }">
	<MvASSIGN NAME = "l.templates:mn_tmpl_id"	VALUE = "{ l.mn_template:id }">
	<MvASSIGN NAME = "l.templates:cn_tmpl_id"	VALUE = "{ l.cn_template:id }">

	<MvIF EXPR = "{ NOT TGReviewsTemplates_Insert( l.templates ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_TGReviewsProducts
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsProducts
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								created		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								cust_id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								order_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								approved	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
								rating		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								email		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								location	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								notify		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()	 		$ ',
								title		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) 		$ ',
								summary		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								store_rply	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ ',
								notified	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								verified	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00005', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsProducts_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsProducts ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00006', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant"
			 QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsProducts_2 ON ' $ g.Store_Table_Prefix $ 'TGReviewsProducts ( product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00007', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_TGReviewsProductsAverages
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages
							(
								product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								rating		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
								rtng_count	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00008', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages ( product_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00009', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_TGReviewsAdditionalFields
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00010', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00011', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields_2 ON ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00012', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| sNN_TGReviewsAdditionalValues
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues
							(
								field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								review_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO()			$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00013', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues ( field_id, review_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00014', g.MvQUERY_Error ) }">
	</MvIF>


	<MvCOMMENT>
	|
	| sNN_TGReviewsOrders
	|
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsOrders
							(
								order_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) $ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00015', g.MvQUERY_Error ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsOrders_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsOrders ( order_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00016', g.MvQUERY_Error ) }">
	</MvIF>

	<MvCOMMENT>
	|
	| StoreKeys
	| TGRProduct, TGRAdditional
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGRProduct',		1 ) OR
					NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGRAdditional',	1 ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.version LT 1.002 }">
		<MvCOMMENT>
		|
		| Change `store_reply` to `store_rply` for MivaSQL
		|
		</MvCOMMENT>

		<MvQUERY NAME	= "Merchant"
				 QUERY	= "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Products CHANGE store_reply store_rply ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO() }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00017', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 2.000 }">
		<MvCOMMENT>
		|
		| Upgrade path for 2.000
		|
		</MvCOMMENT>

		<MvCOMMENT>
		|
		| sNN_TGReviewsGeneralSettings
		|
		</MvCOMMENT>

		<MvQUERY	NAME = "Merchant"
					QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsGeneralSettings
								(
									fld_name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									fld_email	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									fld_loc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									fld_summry	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									fld_title	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									per_page	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									sort		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									auto_apprv	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									rc_enabled	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									rc_pub_key	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									rc_prv_key	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
								)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00018', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| sNN_TGReviewsNotificationSettings
		|
		</MvCOMMENT>

		<MvQUERY	NAME = "Merchant"
					QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsNotificationSettings
								(
									ma_enable	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									ma_days		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									ma_after	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 50 )		$ ',
									ma_from		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									ma_cc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									ma_bcc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									ma_subject	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									mn_enable	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()			$ ',
									mn_to		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									mn_from		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									mn_cc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									mn_bcc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									mn_subject	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									cn_from		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									cn_cc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									cn_bcc		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ ',
									cn_subject	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 254 )		$ '
								)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00019', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| sNN_TGReviewsTemplates
		|
		</MvCOMMENT>

		<MvQUERY	NAME = "Merchant"
					QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsTemplates
								(
									ma_tmpl_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									mn_tmpl_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									cn_tmpl_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									form_tmpl	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									rvws_tmpl	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									prlog_tmpl	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
								)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00020', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.previous_settings"	VALUE = "">

		<MvOPENVIEW NAME	= "Merchant"
					VIEW 	= "TGReviews_Settings"
					QUERY 	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviews_Settings' }">
		<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00021', g.MvOPENVIEW_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.count" VALUE = 0>

		<MvWHILE EXPR = "{ NOT TGReviews_Settings.d.EOF }">
			<MvASSIGN NAME = "l.previous_settings" MEMBER = "{ TGReviews_Settings.d.name }" VALUE = "{ TGReviews_Settings.d.value }">

			<MvSKIP NAME = "Merchant" VIEW = "TGReviews_Settings" ROWS = 1>
		</MvWHILE>

		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviews_Settings">

		<MvASSIGN NAME = "l.previous_settings:required"			VALUE = "{ miva_array_deserialize( l.previous_settings:required ) }">
		<MvASSIGN NAME = "l.previous_settings:recaptcha"		VALUE = "{ miva_array_deserialize( l.previous_settings:recaptcha ) }">
		<MvASSIGN NAME = "l.previous_settings:mail_after"		VALUE = "{ miva_array_deserialize( l.previous_settings:mail_after ) }">
		<MvASSIGN NAME = "l.previous_settings:merchant_email"	VALUE = "{ miva_array_deserialize( l.previous_settings:merchant_email ) }">
		<MvASSIGN NAME = "l.previous_settings:customer_email"	VALUE = "{ miva_array_deserialize( l.previous_settings:customer_email ) }">

		<MvCOMMENT>
		|
		| Copy over General Settings
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.general_settings:fld_name"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:required:name ) }">
		<MvASSIGN NAME = "l.general_settings:fld_email"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:required:email ) }">
		<MvASSIGN NAME = "l.general_settings:fld_loc"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:required:location ) }">
		<MvASSIGN NAME = "l.general_settings:fld_summry"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:required:summary ) }">
		<MvASSIGN NAME = "l.general_settings:fld_title"		VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:required:title ) }">
		<MvASSIGN NAME = "l.general_settings:per_page"		VALUE = "{ int( l.previous_settings:reviews_per_page ) }">
		<MvASSIGN NAME = "l.general_settings:sort"			VALUE = "{ l.previous_settings:reviews_default_sorting }">
		<MvASSIGN NAME = "l.general_settings:auto_apprv"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:auto_approve ) }">
		<MvASSIGN NAME = "l.general_settings:rc_enabled"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:recaptcha:enable ) }">
		<MvASSIGN NAME = "l.general_settings:rc_pub_key"	VALUE = "{ l.previous_settings:recaptcha:public_key }">
		<MvASSIGN NAME = "l.general_settings:rc_prv_key"	VALUE = "{ l.previous_settings:recaptcha:private_key }">

		<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Insert( l.general_settings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| Copy over Email Settings
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.email_settings:ma_enable"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:mail_after:notify ) }">
		<MvASSIGN NAME = "l.email_settings:ma_days"		VALUE = "{ int( l.previous_settings:mail_after:days ) }">
		<MvASSIGN NAME = "l.email_settings:ma_after"	VALUE = "{ l.previous_settings:mail_after:after }">
		<MvASSIGN NAME = "l.email_settings:ma_from"		VALUE = "{ l.previous_settings:mail_after:from }">
		<MvASSIGN NAME = "l.email_settings:ma_cc"		VALUE = "{ l.previous_settings:mail_after:cc }">
		<MvASSIGN NAME = "l.email_settings:ma_bcc"		VALUE = "{ l.previous_settings:mail_after:bcc }">
		<MvASSIGN NAME = "l.email_settings:ma_subject"	VALUE = "{ l.previous_settings:mail_after:subject }">
		<MvASSIGN NAME = "l.email_settings:mn_enable"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.previous_settings:merchant_email:notify ) }">
		<MvASSIGN NAME = "l.email_settings:mn_to"		VALUE = "{ l.previous_settings:merchant_email:to }">
		<MvASSIGN NAME = "l.email_settings:mn_from"		VALUE = "{ l.previous_settings:merchant_email:from }">
		<MvASSIGN NAME = "l.email_settings:mn_cc"		VALUE = "{ l.previous_settings:merchant_email:cc }">
		<MvASSIGN NAME = "l.email_settings:mn_bcc"		VALUE = "{ l.previous_settings:merchant_email:bcc }">
		<MvASSIGN NAME = "l.email_settings:mn_subject"	VALUE = "{ l.previous_settings:merchant_email:subject }">
		<MvASSIGN NAME = "l.email_settings:cn_from"		VALUE = "{ l.previous_settings:customer_email:from }">
		<MvASSIGN NAME = "l.email_settings:cn_cc"		VALUE = "{ l.previous_settings:customer_email:cc }">
		<MvASSIGN NAME = "l.email_settings:cn_bcc"		VALUE = "{ l.previous_settings:customer_email:bcc }">
		<MvASSIGN NAME = "l.email_settings:cn_subject"	VALUE = "{ l.previous_settings:customer_email:subject }">

		<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Insert( l.email_settings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvCOMMENT>
		|
		| prlog_tmpl: Pre-Submission Logic
		|
		</MvCOMMENT>

		<MvCAPTURE VARIABLE = "l.prelogic_template_code">
			<MvINCLUDE FILE = "templates/tgr_prelogic.mvt" INTERPRET = "OFF">
		</MvCAPTURE>

		<MvASSIGN NAME = "l.prelogic_settings"		VALUE = "">
		<MvASSIGN NAME = "l.templates:prlog_tmpl"	VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate( l.prelogic_template_code, l.prelogic_settings, 'tgr_prelogic.mvc' ) }">

		<MvIF EXPR = "{ NOT l.templates:prlog_tmpl }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_MailAfter_Email', l.mail_after_page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.mail_after_page:admin"	VALUE = 1>
		<MvASSIGN NAME = "l.mail_after_page:ui_id"	VALUE = "{ l.module:id }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Update( l.mail_after_page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_Merchant_Email', l.merchant_notification_page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.merchant_notification_page:admin"	VALUE = 1>
		<MvASSIGN NAME = "l.merchant_notification_page:ui_id"	VALUE = "{ l.module:id }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Update( l.merchant_notification_page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_Customer_Email', l.customer_notification_page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.customer_notification_page:admin"	VALUE = 1>
		<MvASSIGN NAME = "l.customer_notification_page:ui_id"	VALUE = "{ l.module:id }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Update( l.customer_notification_page ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.templates:form_tmpl"	VALUE = "{ l.previous_settings:form_template }">
		<MvASSIGN NAME = "l.templates:rvws_tmpl"	VALUE = "{ l.previous_settings:reviews_template }">
		<MvASSIGN NAME = "l.templates:mn_tmpl_id"	VALUE = "{ l.merchant_notification_page:templ_id }">
		<MvASSIGN NAME = "l.templates:ma_tmpl_id"	VALUE = "{ l.mail_after_page:templ_id }">
		<MvASSIGN NAME = "l.templates:cn_tmpl_id"	VALUE = "{ l.customer_notification_page:templ_id }">

		<MvIF EXPR = "{ NOT TGReviewsTemplates_Insert( l.templates ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Settings' }">

		<MvCOMMENT>
		|
		| Rename sNN_ to sNN_TGReviewsProducts
		| Update Indexes
		|
		</MvCOMMENT>

		<MvQUERY	NAME = "Merchant"
					QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Products TO ' $ g.Store_Table_Prefix $ 'TGReviewsProducts' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00022', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Native_DBAPI ].DB_Drop_Index( g.Store_Table_Prefix $ 'TGReviewsProducts', g.Store_Table_Prefix $ '' ) }">

		<MvQUERY	NAME	= "Merchant"
					QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsProducts_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsProducts ( id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00023', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY	NAME = "Merchant"
					QUERY = "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsProducts_2 ON ' $ g.Store_Table_Prefix $ 'TGReviewsProducts ( product_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00024', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| sNN_TGReviewsProductsAverages
		|
		</MvCOMMENT>

		<MvQUERY	NAME = "Merchant"
					QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages
								(
									product_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
									rating		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 10, 2 )	$ ',
									rtng_count	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ '
								)' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00025', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY	NAME	= "Merchant"
					QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages ( product_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00026', g.MvQUERY_Error ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT TGReviewsProductsAverages_UpdateAll() }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvCOMMENT>
		|
		| Rename sNN_TGReviews_AdditionalFields to sNN_TGReviewsAdditionalFields
		| Update Indexes
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalFields TO ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00027', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Native_DBAPI ].DB_Drop_Index( g.Store_Table_Prefix $ 'TGReviewsAdditionalFields', g.Store_Table_Prefix $ 'TGReviews_AdditionalFields' ) }">

		<MvQUERY	NAME	= "Merchant"
					QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields ( id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00028', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY	NAME	= "Merchant"
					QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields_2 ON ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields ( code )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00029', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| Rename sNN_TGReviews_AdditionalValues to sNN_TGReviewsAdditionalValues
		| Rename rating_id to review_id
		| Update Indexes
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_AdditionalValues TO ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00030', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues CHANGE COLUMN rating_id review_id ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 ) }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00031', g.MvQUERY_Error ) }">
		</MvIF>

		<MvQUERY	NAME	= "Merchant"
					QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues ( field_id, review_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00032', g.MvQUERY_Error ) }">
		</MvIF>

		<MvCOMMENT>
		|
		| Rename sNN_TGReviews_Orders to sNN_TGReviewsOrders
		| Update Indexes
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'RENAME TABLE ' $ g.Store_Table_Prefix $ 'TGReviews_Orders TO ' $ g.Store_Table_Prefix $ 'TGReviewsOrders' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00033', g.MvQUERY_Error ) }">
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Native_DBAPI ].DB_Drop_Index( g.Store_Table_Prefix $ 'TGReviewsOrders', g.Store_Table_Prefix $ 'TGReviews_Orders' ) }">

		<MvQUERY	NAME	= "Merchant"
					QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGReviewsOrders_1 ON ' $ g.Store_Table_Prefix $ 'TGReviewsOrders ( order_id )' }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00034', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 2.001 }">

		<MvCOMMENT>
		|
		| Alter sNN_TGReviewsAdditionalValues values
		|
		</MvCOMMENT>

		<MvQUERY NAME = "Merchant"
				 QUERY = "{ 'ALTER TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues MODIFY COLUMN value ' $ [ g.Module_Library_Native_DBAPI ].DB_Type_MEMO() }">
		<MvIF EXPR = "{ g.MvQUERY_Error }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00146', g.MvQUERY_Error ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.version LT 2.004 }">

		<MvCOMMENT>
		|
		| Create item `tgreviews_orderitems`
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_Item( 'tgreviews_orderitems', l.module:code ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00155', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ TGReviewsTemplates_Load( l.templates ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.templates:form_tmpl ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.templates:rvws_tmpl ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.templates:prlog_tmpl ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_Merchant_Email', l.merchant_email_page ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.merchant_email_page ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_MailAfter_Email', l.mail_after_page ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.mail_after_page ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_TUI_DB ].Page_Load_Code( 'TGR_Customer_Email', l.customer_notification_page ) }">
		<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_Page( l.customer_notification_page ) }">
	</MvIF>

	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsGeneralSettings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsNotificationSettings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsTemplates' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsProducts'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGReviewsOrders' }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGRProduct' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGRAdditional' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| System Extensions Feature (system)
|
</MvCOMMENT>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| System Extension Settings Screen Feature (vis_system)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_System_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "TGR_SETT:Product Reviews: Settings,TGR_SETT/FIELDS:Field Settings,TGR_SETT/REVIEW:Review Settings,TGR_SETT/RECAPTCHA:reCAPTCHA Settings,TGR_SETT/MAIL_AFTER:Mail After Notification,TGR_SETT/MERCHANT:Merchant Notification Email,TGR_SETT/CUSTOMER:Customer Notification Email,TGR_SETT/PRERENDER_LOGIC_TEMPLATE:Pre-Submission Logic Template,TGR_SETT/FORM_TEMPLATE:Review Form Template,TGR_SETT/REVIEWS_TEMPLATE:Reviews Template,TGR_SETT/FUNCTIONS:Item Functions,TGR_PROD:Product Reviews: Reviews,TGR_ADDL:Reviews: Additional Fields">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_PROD' ) }">
		<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_CSS() }">

		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=productlookupcolumn.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=productreviewlist.js' }"></script>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScript_Set_A_Variable_Boolean( 'TGReviewsGeneralSettings_auto_apprv', l.general_settings:auto_apprv ) }">

		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new ProductReview_List( null, TGReviewsGeneralSettings_auto_apprv); } );
		</script>
	</MvIF>

	<MvIF EXPR = "{ ( l.tab EQ 'TGR_ADDL' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">

		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=functions.js' }"></script>
		<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=additionalfieldlist.js' }"></script>

		<script language="JavaScript">
			MMScreen_LoadFinished( function() { new AdditionalField_List(); } );
		</script>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "html, text, compresswhitespace">
	<MvIF EXPR = "{ ( l.tab EQ 'TGR_PROD' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons_Suppress( '[UPDATE]' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

		<div id="batchlist_productreviews"></div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_HTML() }">
	</MvIF>

	<MvIF EXPR = "{ ( l.tab EQ 'TGR_ADDL' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons_Suppress( '[UPDATE]' ) }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

		<div id="batchlist_additionalfields"></div>
	</MvIF>

	<MvIF EXPR = "{ l.load_fields }">
		<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load( g.TGReviewsGeneralSettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load( g.TGReviewsNotificationSettings ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.tab NE 'TGR_SETT' }">
		<MvHIDE FIELDS = "g.TGReviewsGeneralSettings, g.TGReviewsNotificationSettings">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.tab EQ 'TGR_SETT' }">
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'FIELDS' ) }">
		<div class="mm9_table_container tgreviews_fields_table_container">
			<table border="0" cellpadding="0" cellspacing="0" width="100%" class="mm9_table">
				<thead>
					<tr>
						<td align="left" valign="middle" vnowrap>Field</td>
						<td align="center" valign="middle" nowrap>Required</td>
						<td align="center" valign="middle" nowrap>Optional</td>
					</tr>
				</thead>
				<tbody>
					<tr>
						<td align="left" valign="middle" vnowrap>
							Email
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_email', 1, g.TGReviewsGeneralSettings:fld_email, '' ) }">
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_email', 0, g.TGReviewsGeneralSettings:fld_email, '' ) }">
						</td>
					</tr>
					<tr>
						<td align="left" valign="middle" vnowrap>
							Location
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_loc', 1, g.TGReviewsGeneralSettings:fld_loc, '' ) }">
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_loc', 0, g.TGReviewsGeneralSettings:fld_loc, '' ) }">
						</td>
					</tr>
					<tr>
						<td align="left" valign="middle" vnowrap>
							Name
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_name', 1, g.TGReviewsGeneralSettings:fld_name, '' ) }">
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_name', 0, g.TGReviewsGeneralSettings:fld_name, '' ) }">
						</td>
					</tr>
					<tr>
						<td align="left" valign="middle" vnowrap>
							Summary
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_summry', 1, g.TGReviewsGeneralSettings:fld_summry, '' ) }">
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_summry', 0, g.TGReviewsGeneralSettings:fld_summry, '' ) }">
						</td>
					</tr>
					<tr>
						<td align="left" valign="middle" vnowrap>
							Title
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_title', 1, g.TGReviewsGeneralSettings:fld_title, '' ) }">
						</td>
						<td align="center" valign="middle" nowrap>
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( 'TGReviewsGeneralSettings:fld_title', 0, g.TGReviewsGeneralSettings:fld_title, '' ) }">
						</td>
					</tr>
				</tbody>
			</table>
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'REVIEW' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tbody>
				<tr valign="top">
					<td nowrap><strong>Reviews Per Page:</strong></td>
					<td width="100%">
						<input type="text" name="TGReviewsGeneralSettings:per_page" size="20" value="{ encodeentities( g.TGReviewsGeneralSettings:per_page ) }">
					</td>
				</tr>
				<tr valign="top">
					<td nowrap><strong>Default Sorting Method:</strong></td>
					<td width="100%">
						<select name="TGReviewsGeneralSettings:sort">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '-id',			g.TGReviewsGeneralSettings:sort, 'Default' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '-created',	g.TGReviewsGeneralSettings:sort, 'Most Recent Reviews' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'created',		g.TGReviewsGeneralSettings:sort, 'Least Recent Reviews' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( '-rating',		g.TGReviewsGeneralSettings:sort, 'Highest To Lowest Rating' ) }">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'rating',		g.TGReviewsGeneralSettings:sort, 'Lowest to Highest Rating' ) }">
						</select>
					</td>
				</tr>
				<tr valign="top">
					<td colspan="2">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.TGReviewsGeneralSettings:auto_apprv, 'TGReviewsGeneralSettings:auto_apprv', 1, 'Auto Approve Reviews' ) }">
					</td>
				</tr>
			</tbody>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'RECAPTCHA' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tbody>
				<tr valign="top">
					<td colspan="2">
						<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.TGReviewsGeneralSettings:rc_enabled, 'TGReviewsGeneralSettings:rc_enabled', 1, 'reCAPTCHA Enabled' ) }">
					</td>
				</tr>
				<tr valign="top">
					<td nowrap><strong>Public Key:</strong></td>
					<td width="100%">
						<input type="text" name="TGReviewsGeneralSettings:rc_pub_key" size="50" value="{ encodeentities( g.TGReviewsGeneralSettings:rc_pub_key ) }">
					</td>
				</tr>
				<tr valign="top">
					<td nowrap><strong>Private Key:</strong></td>
					<td width="100%">
						<input type="password" name="TGReviewsGeneralSettings:rc_prv_key" size="50" value="">
					</td>
				</tr>
				<tr valign="top">
					<td nowrap><strong>Confirm Private Key:</strong></td>
					<td width="100%">
						<input type="password" name="TGReviewsGeneralSettings:rc_prv_key_confirm" size="50" value="">
					</td>
				</tr>
			</tbody>
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'MAIL_AFTER' ) }">
		<div class="mm9_table_container tgreviews_fields_table_container">
			<table border="0" cellpadding="0" cellspacing="0" width="100%" class="mm9_table">
				<thead>
					<tr>
						<td colspan="2">
							Page Template Used: <strong>TGR_MailAfter_Email</strong>
						</td>
					</tr>
				</thead>
				<tbody>
					<tr valign="top">
						<td colspan="2">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.TGReviewsNotificationSettings:ma_enable, 'TGReviewsNotificationSettings:ma_enable', 1, 'Notification Enabled' ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>Send After:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:ma_days" value="{ encodeentities( g.TGReviewsNotificationSettings:ma_days ) }"> day(s)
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>After:</strong></td>
						<td width="100%">
							<select name="TGReviewsNotificationSettings:ma_after">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'order',	g.TGReviewsNotificationSettings:ma_after, 'Order Date' ) }">
								<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'ship',	g.TGReviewsNotificationSettings:ma_after, 'Shipment Date' ) }">
							</select>
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>From:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:ma_from" value="{ encodeentities( g.TGReviewsNotificationSettings:ma_from ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap>CC:</td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:ma_cc" value="{ encodeentities( g.TGReviewsNotificationSettings:ma_cc ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap>BCC:</td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:ma_bcc" value="{ encodeentities( g.TGReviewsNotificationSettings:ma_bcc ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>Subject:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:ma_subject" value="{ encodeentities( g.TGReviewsNotificationSettings:ma_subject ) }">
						</td>
					</tr>
				</tbody>
			</table>
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'MERCHANT' ) }">
		<div class="mm9_table_container tgreviews_fields_table_container">
			<table border="0" cellpadding="0" cellspacing="0" width="100%" class="mm9_table">
				<thead>
					<tr>
						<td colspan="2">
							Page Template Used: <strong>TGR_Merchant_Email</strong>
						</td>
					</tr>
				</thead>
				<tbody>
					<tr valign="top">
						<td colspan="2">
							<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox_With_Label( g.TGReviewsNotificationSettings:mn_enable, 'TGReviewsNotificationSettings:mn_enable', 1, 'Notification Enabled' ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>To:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:mn_to" value="{ encodeentities( g.TGReviewsNotificationSettings:mn_to ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>From:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:mn_from" value="{ encodeentities( g.TGReviewsNotificationSettings:mn_from ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap>CC:</td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:mn_cc" value="{ encodeentities( g.TGReviewsNotificationSettings:mn_cc ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap>BCC:</td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:mn_bcc" value="{ encodeentities( g.TGReviewsNotificationSettings:mn_bcc ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>Subject:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:mn_subject" value="{ encodeentities( g.TGReviewsNotificationSettings:mn_subject ) }">
						</td>
					</tr>
				</tbody>
			</table>
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'CUSTOMER' ) }">
		<div class="mm9_table_container tgreviews_fields_table_container">
			<table border="0" cellpadding="0" cellspacing="0" width="100%" class="mm9_table">
				<thead>
					<tr>
						<td colspan="2">
							Page Template Used: <strong>TGR_Customer_Email</strong>
						</td>
					</tr>
				</thead>
				<tbody>
					<tr valign="top">
						<td nowrap><strong>From:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:cn_from" value="{ encodeentities( g.TGReviewsNotificationSettings:cn_from ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap>CC:</td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:cn_cc" value="{ encodeentities( g.TGReviewsNotificationSettings:cn_cc ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap>BCC:</td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:cn_bcc" value="{ encodeentities( g.TGReviewsNotificationSettings:cn_bcc ) }">
						</td>
					</tr>
					<tr valign="top">
						<td nowrap><strong>Subject:</strong></td>
						<td width="100%">
							<input type="text" name="TGReviewsNotificationSettings:cn_subject" value="{ encodeentities( g.TGReviewsNotificationSettings:cn_subject ) }">
						</td>
					</tr>
				</tbody>
			</table>
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvIF EXPR = "{ NOT TGReviewsTemplates_Load_Cached( g.TGReviewsTemplates ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'PRERENDER_LOGIC_TEMPLATE' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr>
				<td align="left" nowrap valign="top" colspan="2">
					On form submission (front-end submissions only), you can add extra validation before the review is submitted.<br>
					The following are able to be set to <code>0</code>, or <code>1</code>. If empty, it defaults the review module settings.<br>
					<code>l.settings:tgreviews_prelogic:allow_same_reviewer</code> If this is set to 1, it will bypass the check if that user's email has already submitted a review (approved or not)<br>
					<code>l.settings:tgreviews_prelogic:allow_submission</code> If you need to do your own checks (i.e. other spam filters/checks), you can set this to 0 to hault the review from processing. If set to empty, or 1, will default to the reviews module settings.<br>
					<code>l.settings:tgreviews_prelogic:approve</code> If you want to allow the review to go through and be approved (i.e. rating is over 4, approve it), you can set this to 1. If you set this to 0, and you have auto approval on, it will overwrite this setting and not approve the review.<br>
					You will need to handle the error messaging, and highlighting of fields via the Template Code.
					<br><br>
				</td>
			</tr>
			<MvEVAL EXPR = "{ Admin_Template( 'Pre-Render Logic', 'TGReviews_prlog_tmpl_', g.TGReviewsTemplates:prlog_tmpl, l.load_fields ) }">
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'FORM_TEMPLATE' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr>
				<td align="left" nowrap valign="top" colspan="2">
					<MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Form_Template', '' ) }">
					<br><br>
				</td>
			</tr>
			<MvEVAL EXPR = "{ Admin_Template( 'Form', 'TGReviews_form_tmpl_', g.TGReviewsTemplates:form_tmpl, l.load_fields ) }">
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'REVIEWS_TEMPLATE' ) }">
		<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr>
				<td align="left" nowrap valign="top" colspan="2">
					<MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Reviews_Template', ' l.settings:product:id ' ) }">
					<br><br>
				</td>
			</tr>
			<MvEVAL EXPR = "{ Admin_Template( 'Reviews', 'TGReviews_rvws_tmpl_', g.TGReviewsTemplates:rvws_tmpl, l.load_fields ) }">
		</table>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">

		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'TGR_SETT', 'FUNCTIONS' ) }">
		<div class="mm9_table_container">
			<table class="mm9_table">
				<thead>
					<tr>
						<td align="left" valign="middle">Function</td>
						<td align="left" valign="middle">Information</td>
					</tr>
				</thead>
				<tbody>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Form_Template', '') }"></td>
						<td>Render the Form Template.</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Reviews_Template', ' l.settings:product:id ') }"></td>
						<td>Render the Reviews Template. Returns <pre style="display:inline;">reviews array</pre> in <pre style="display:inline;">l.settings:tgr:reviews</pre>. If you do not pass an id (i.e. put 0, or an empty string), it will return all approved reviews for the store.</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Load_Product_Reviews', ' l.settings:product:id, l.settings:product:reviews ') }"></td>
						<td>Use this to save all the review data to a variable and utilize in template code.</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Date', ' \'F jS, Y\', l.settings:review:created, l.settings:review:formatted_created ') }"></td>
						<td>Format a timestamp, and save to a variable (3rd parameter). Uses PHP's date function as a reference.</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Product_Rating', ' l.settings:product:id, l.settings:product:product_rating ') }"></td>
						<td>Returns the average product rating value to a variable (2nd parameter).</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Product_Review_Breakdowns', ' l.settings:product:id, l.settings:tgr:breakdown ') }"></td>
						<td>Returns a breakdown of ratings for a product to a variable (2nd parameter). Array has 2 members: <pre style="display:inline;">count</pre> and <pre style="display:inline;">rating</pre></td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Product_Review_Count', ' l.settings:product:id, l.settings:tgr:review_count ') }"></td>
						<td>Returns a total number of approved reviews for a product to a variable (2nd parameter).</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Test_MailAfter_Email', ' 12345, \'hello@email.com\' ') }"></td>
						<td>This will trigger the email for any order you specify, to the email you specify. This is specifically made to test the email template. Please note: BCC and CC will not carry through. If you have left a review on a product in the order, it will skip that product.</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews', 'Review_Check_CustomerXProduct', ' g.Customer:id, l.settings:product:id, l.settings:customer_hasreviewed ') }"></td>
						<td>Pass through a customer id, a product id, it will return if the customer has reviewed that product, into reviewed var (1 or 0)</td>
					</tr>
					<tr>
						<td><MvEVAL EXPR = "{ Admin_Formatted_Item_Function( 'tgreviews_orderitems', '', '') }"></td>
						<td>Assign this item to the page you wish to display items a customer has purchased, and display if they have reviewed, or not reviewed the product yet.</td>
					</tr>
				</tbody>
			</table>
		</div>
		<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">	
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.general_settings:fld_name"		VALUE = "{ g.TGReviewsGeneralSettings:fld_name }">
	<MvASSIGN NAME = "l.general_settings:fld_email"		VALUE = "{ g.TGReviewsGeneralSettings:fld_email }">
	<MvASSIGN NAME = "l.general_settings:fld_loc"		VALUE = "{ g.TGReviewsGeneralSettings:fld_loc }">
	<MvASSIGN NAME = "l.general_settings:fld_summry"	VALUE = "{ g.TGReviewsGeneralSettings:fld_summry }">
	<MvASSIGN NAME = "l.general_settings:fld_title"		VALUE = "{ g.TGReviewsGeneralSettings:fld_title }">
	<MvASSIGN NAME = "l.general_settings:per_page"		VALUE = "{ g.TGReviewsGeneralSettings:per_page }">
	<MvASSIGN NAME = "l.general_settings:sort"			VALUE = "{ g.TGReviewsGeneralSettings:sort }">
	<MvASSIGN NAME = "l.general_settings:auto_apprv"	VALUE = "{ g.TGReviewsGeneralSettings:auto_apprv }">
	<MvASSIGN NAME = "l.general_settings:rc_enabled"	VALUE = "{ g.TGReviewsGeneralSettings:rc_enabled }">
	<MvASSIGN NAME = "l.general_settings:rc_pub_key"	VALUE = "{ g.TGReviewsGeneralSettings:rc_pub_key }">

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsGeneralSettings:rc_prv_key }">
		<MvASSIGN NAME = "l.general_settings:rc_prv_key"	VALUE = "{ g.TGReviewsGeneralSettings:rc_prv_key }">
	</MvIF>

	<MvASSIGN NAME = "l.notification_settings:ma_enable"	VALUE = "{ g.TGReviewsNotificationSettings:ma_enable }">
	<MvASSIGN NAME = "l.notification_settings:ma_days"		VALUE = "{ g.TGReviewsNotificationSettings:ma_days }">
	<MvASSIGN NAME = "l.notification_settings:ma_after"		VALUE = "{ g.TGReviewsNotificationSettings:ma_after }">
	<MvASSIGN NAME = "l.notification_settings:ma_from"		VALUE = "{ g.TGReviewsNotificationSettings:ma_from }">
	<MvASSIGN NAME = "l.notification_settings:ma_cc"		VALUE = "{ g.TGReviewsNotificationSettings:ma_cc }">
	<MvASSIGN NAME = "l.notification_settings:ma_bcc"		VALUE = "{ g.TGReviewsNotificationSettings:ma_bcc }">
	<MvASSIGN NAME = "l.notification_settings:ma_subject"	VALUE = "{ g.TGReviewsNotificationSettings:ma_subject }">
	<MvASSIGN NAME = "l.notification_settings:mn_enable"	VALUE = "{ g.TGReviewsNotificationSettings:mn_enable }">
	<MvASSIGN NAME = "l.notification_settings:mn_to"		VALUE = "{ g.TGReviewsNotificationSettings:mn_to }">
	<MvASSIGN NAME = "l.notification_settings:mn_from"		VALUE = "{ g.TGReviewsNotificationSettings:mn_from }">
	<MvASSIGN NAME = "l.notification_settings:mn_cc"		VALUE = "{ g.TGReviewsNotificationSettings:mn_cc }">
	<MvASSIGN NAME = "l.notification_settings:mn_bcc"		VALUE = "{ g.TGReviewsNotificationSettings:mn_bcc }">
	<MvASSIGN NAME = "l.notification_settings:mn_subject"	VALUE = "{ g.TGReviewsNotificationSettings:mn_subject }">
	<MvASSIGN NAME = "l.notification_settings:cn_from"		VALUE = "{ g.TGReviewsNotificationSettings:cn_from }">
	<MvASSIGN NAME = "l.notification_settings:cn_cc"		VALUE = "{ g.TGReviewsNotificationSettings:cn_cc }">
	<MvASSIGN NAME = "l.notification_settings:cn_bcc"		VALUE = "{ g.TGReviewsNotificationSettings:cn_bcc }">
	<MvASSIGN NAME = "l.notification_settings:cn_subject"	VALUE = "{ g.TGReviewsNotificationSettings:cn_subject }">

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Update( l.general_settings ) OR
					NOT TGReviewsNotificationSettings_Update( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ g.Tab EQ 'TGR_SETT' }">
		<MvIF EXPR = "{ NOT TGReviewsTemplates_Load_Cached( l.templates ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT Admin_Template_Update( l.module, 'Pre-Render Logic', 'TGReviews_prlog_tmpl_', l.templates:prlog_tmpl )	OR
						NOT Admin_Template_Update( l.module, 'Form', 'TGReviews_form_tmpl_', l.templates:form_tmpl )				OR
						NOT Admin_Template_Update( l.module, 'Reviews', 'TGReviews_rvws_tmpl_', l.templates:rvws_tmpl ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:fld_name"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:fld_name ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:fld_email"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:fld_email ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:fld_loc" 				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:fld_loc ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:fld_summry"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:fld_summry ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:fld_title"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:fld_title ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:fld_title"				VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:fld_title ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:auto_apprv"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:auto_apprv ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:per_page"				VALUE = "{ trim( g.TGReviewsGeneralSettings:per_page ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:sort"					VALUE = "{ trim( g.TGReviewsGeneralSettings:sort ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:rc_enabled"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsGeneralSettings:rc_enabled ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:rc_pub_key"			VALUE = "{ trim( g.TGReviewsGeneralSettings:rc_pub_key ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:rc_prv_key"			VALUE = "{ trim( g.TGReviewsGeneralSettings:rc_prv_key ) }">
	<MvASSIGN NAME = "g.TGReviewsGeneralSettings:rc_prv_key_confirm"	VALUE = "{ trim( g.TGReviewsGeneralSettings:rc_prv_key_confirm ) }">

	<MvIF EXPR = "{ g.TGReviewsGeneralSettings:rc_enabled AND ISNULL l.general_settings:rc_prv_key }">	<MvASSIGN NAME = "l.private_key_required" VALUE = 1>
	<MvELSE>																							<MvASSIGN NAME = "l.private_key_required" VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( g.TGReviewsGeneralSettings:per_page ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsGeneralSettings:per_page', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ ( g.TGReviewsGeneralSettings:sort NE '-id' )		AND
					( g.TGReviewsGeneralSettings:sort NE '-created' )	AND
					( g.TGReviewsGeneralSettings:sort NE 'created' )	AND
					( g.TGReviewsGeneralSettings:sort NE '-rating' )	AND
					( g.TGReviewsGeneralSettings:sort NE 'rating' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsGeneralSettings:sort', 'Please select a value for the default sorting method' ) }">
	</MvIF>

	<MvIF EXPR = "{ g.TGReviewsGeneralSettings:rc_enabled AND ISNULL g.TGReviewsGeneralSettings:rc_pub_key }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsGeneralSettings:rc_pub_key', 'Public key cannot be blank' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.private_key_required }">
		<MvIF EXPR = "{ ISNULL g.TGReviewsGeneralSettings:rc_prv_key }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsGeneralSettings:rc_prv_key', 'Private key cannot be blank' ) }">
		<MvELSEIF EXPR = "{ g.TGReviewsGeneralSettings:rc_prv_key NE g.TGReviewsGeneralSettings:rc_prv_key_confirm }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsGeneralSettings:rc_prv_key', 'Private keys do not match' ) }">
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_enable"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsNotificationSettings:ma_enable ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_days"		VALUE = "{ trim( g.TGReviewsNotificationSettings:ma_days ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_after"		VALUE = "{ trim( g.TGReviewsNotificationSettings:ma_after ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_from"		VALUE = "{ trim( g.TGReviewsNotificationSettings:ma_from ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_cc"		VALUE = "{ trim( g.TGReviewsNotificationSettings:ma_cc ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_bcc"		VALUE = "{ trim( g.TGReviewsNotificationSettings:ma_bcc ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:ma_subject"	VALUE = "{ trim( g.TGReviewsNotificationSettings:ma_subject ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:mn_enable"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGReviewsNotificationSettings:mn_enable ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:mn_to"		VALUE = "{ trim( g.TGReviewsNotificationSettings:mn_to ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:mn_from"		VALUE = "{ trim( g.TGReviewsNotificationSettings:mn_from ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:mn_cc"		VALUE = "{ trim( g.TGReviewsNotificationSettings:mn_cc ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:mn_bcc"		VALUE = "{ trim( g.TGReviewsNotificationSettings:mn_bcc ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:mn_subject"	VALUE = "{ trim( g.TGReviewsNotificationSettings:mn_subject ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:cn_from"		VALUE = "{ trim( g.TGReviewsNotificationSettings:cn_from ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:cn_cc"		VALUE = "{ trim( g.TGReviewsNotificationSettings:cn_cc ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:cn_bcc"		VALUE = "{ trim( g.TGReviewsNotificationSettings:cn_bcc ) }">
	<MvASSIGN NAME = "g.TGReviewsNotificationSettings:cn_subject"	VALUE = "{ trim( g.TGReviewsNotificationSettings:cn_subject ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:ma_from ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:ma_from', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsNotificationSettings:ma_cc }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:ma_cc ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:ma_cc', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsNotificationSettings:ma_bcc }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:ma_bcc ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:ma_bcc', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.TGReviewsNotificationSettings:ma_subject }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:ma_subject', 'Please enter a value for Subject' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( g.TGReviewsNotificationSettings:ma_days ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:ma_days', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ ( g.TGReviewsNotificationSettings:ma_after NE 'order' ) AND
					( g.TGReviewsNotificationSettings:ma_after NE 'ship' ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:ma_after', 'Please select a value for after' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:mn_to ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:mn_to', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:mn_from ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:mn_from', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsNotificationSettings:mn_cc }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:mn_cc ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:mn_cc', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsNotificationSettings:mn_bcc }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:mn_bcc ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:mn_bcc', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.TGReviewsNotificationSettings:mn_subject }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:mn_subject', 'Please enter a value for Subject' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:cn_from ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:cn_from', g.Validation_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsNotificationSettings:cn_cc }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:cn_cc ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:cn_cc', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.TGReviewsNotificationSettings:cn_bcc }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGReviewsNotificationSettings:cn_bcc ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:cn_bcc', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ISNULL g.TGReviewsNotificationSettings:cn_subject }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', 'TGReviewsNotificationSettings:cn_subject', 'Please enter a value for Subject' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Module JSON API Feature (json_api)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON_API" PARAMETERS = "module var, function" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.function EQ 'Product_Reviews_Load_Query' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_Product_Reviews_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'Product_Review_Insert' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_Product_Review_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'Product_Review_Update' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_Product_Review_Update( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'Product_Review_Delete' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_Product_Review_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'AdditionalFields_Load_Query' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_API_AdditionalFields_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'AdditionalField_Insert' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_AdditionalField_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'AdditionalField_Update' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_AdditionalField_Update( l.module ) }">
	<MvELSEIF EXPR = "{ l.function EQ 'AdditionalField_Delete' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_API_AdditionalField_Delete( l.module ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-SYS-TGR-00136', 'Invalid function' ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Module JSON Feature (json)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Module_Function EQ 'Product_Reviews_Load_Query' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Product_Review_Insert' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Product_Review_Update' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'Product_Review_Delete' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Delete( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Load_Query' }">	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Load_Query( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'AdditionalField_Insert' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Insert( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'AdditionalField_Update' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Update( l.module ) }">
	<MvELSEIF EXPR = "{ g.Module_Function EQ 'AdditionalField_Delete' }">		<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Delete( l.module ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_TGReviewsProduct" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	"id":			<MvEVAL EXPR = "{ int( l.review:id ) }">,
	"created":		<MvEVAL EXPR = "{ int( l.review:created ) }">,
	"product_id":	<MvEVAL EXPR = "{ int( l.review:product_id ) }">,
	"cust_id":		<MvEVAL EXPR = "{ int( l.review:cust_id ) }">,
	"order_id":		<MvEVAL EXPR = "{ int( l.review:order_id ) }">,
	"approved":		<MvEVAL EXPR = "{ int( l.review:approved ) }">,
	"rating":		<MvEVAL EXPR = "{ int( l.review:rating ) }">,
	"name":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.review:name ) }">",
	"email":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.review:email ) }">",
	"location":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.review:location ) }">",
	"notify":		<MvEVAL EXPR = "{ int( l.review:notify ) }">,
	"title":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.review:title ) }">",
	"summary":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.review:summary ) }">",
	"store_rply":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.review:store_rply ) }">",
	"notified":		<MvEVAL EXPR = "{ int( l.review:notified ) }">,
	"verified":		<MvEVAL EXPR = "{ int( l.review:verified ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Reviews_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Load_Query_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Product_Reviews_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Reviews_Load_Query_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Reviews_Load_Query_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvASSIGN NAME = "l.filter"			VALUE = "">
	<MvASSIGN NAME = "l.sort"			VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = 0>
	<MvASSIGN NAME = "l.count"			VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',	l.filter )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',	l.sort )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Offset',	l.offset,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Count',	l.count,	-1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "g.Filter_Fields" 		VALUE = "">

	<MvASSIGN NAME = "l.null"				VALUE = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 'tgrp.*' ) }">

	<MvASSIGN NAME = "l.null"				VALUE = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGReviewsProducts', 'tgrp' ) }">

	<MvASSIGN NAME = "l.filter_columns" 	VALUE = "{ JSON_BaseProductReviews_Filters() }">
	<MvASSIGN NAME = "l.orderby_columns"	VALUE = "{ JSON_BaseProductReviews_OrderBy() }">

	<MvASSIGN NAME = "l.null"				VALUE = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, l.filter, l.filter_columns ) }">
	
	<MvASSIGN NAME = "l.null"				VALUE = "{ JSON_ProductReviews_Filters_Query_Callback_With_Fields( l.module, l.search_query, l.filter, l.filter_columns ) }">
	<MvASSIGN NAME = "l.null"				VALUE = "{ JSON_ProductReviews_OrderBy_Callback_With_Fields( l.search_query, l.sort, l.orderby_columns, 'tgrp.id', l.filter_columns ) }">

	<MvASSIGN NAME = "l.search_sql"			VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'TGReviewsProducts', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-SYS-TGR-00035', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.review_count"			VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"total_count":	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset":	<MvEVAL EXPR = "{ int( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT TGReviewsProducts.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.review_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ TGReviewsProduct_Read( l.review ) }">

			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.review_count ) }">
				<MvEVAL EXPR = "{ JSON_TGReviewsProduct( l.review ) }">
				<MvEVAL EXPR = "{ JSON_TGReviewsProduct_Fields_With_Query( l.search_query, TGReviewsProducts.d.id, 'TGReviewsProducts' ) }">
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProducts" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_BaseProductReviews_Filters" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "id:tgrp.id,created:tgrp.created,product_id:tgrp.product_id,cust_id:tgrp.cust_id,order_id:tgrp.order_id,
							   approved:tgrp.approved,rating:tgrp.rating,name:tgrp.name,email:tgrp.email,
							   location:tgrp.location,notify:tgrp.notify,title:tgrp.title,summary:tgrp.summary,
							   store_rply:tgrp.store_rply,notified:tgrp.notified,verified:tgrp.verified">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_BaseProductReviews_OrderBy" PARAMETERS = "" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "id:tgrp.id,created:tgrp.created,product_id:tgrp.product_id,cust_id:tgrp.cust_id,order_id:tgrp.order_id,
							   approved:tgrp.approved,rating:tgrp.rating,name:tgrp.name,email:tgrp.email,
							   location:tgrp.location,notify:tgrp.notify,title:tgrp.title,summary:tgrp.summary,
							   store_rply:tgrp.store_rply,notified:tgrp.notified,verified:tgrp.verified">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductReviews_Filters_Query_Callback_With_Fields" PARAMETERS = "module var, query var, filterlist var, callback_query_fields" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.callback_query_fields }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter_Structify( l.filterlist ) }">

	<MvFOREACH ITERATOR = "l.filter" ARRAY = "l.filterlist">
		<MvIF EXPR = "{ l.filter:name EQ 'ondemandcolumns' }">
			<MvFOREACH ITERATOR = "l.field" ARRAY = "l.filter:value">
				<MvIF EXPR = "{ ( 'AdditionalFields:' IN l.field ) EQ 1 }">
					<MvASSIGN NAME = "l.null" VALUE = "{ JSON_Query_Callback_Handle_Field( l.query, l.callback_query_fields, gettoken( l.field, ':', 2 ) ) }">
				<MvELSEIF EXPR = "{ 'Product' IN l.field }">
					<MvASSIGN NAME = "l.null" VALUE = "{ JSON_Query_Callback_Handle_Product( l.query, l.callback_query_fields ) }">
				</MvIF>
			</MvFOREACH>
		</MvIF>

		<MvIF EXPR = "{ l.filter:name EQ 'search' }">			<MvASSIGN NAME = "l.field_count" VALUE = "{ [ g.Module_JSON ].JSON_Search_Filter_Clause_With_CustomFields( l.query, '',		'OR',	l.field_count, l.filter:value, l.correlationlist, g.Module_Root $ l.module:module, 'JSON_ProductReviews_Fields_Query_Filter', l.callback_search_customfields, l.callback_search_field_count, l.data ) }">
		<MvELSEIF EXPR = "{ l.filter:name EQ 'search_AND' }">	<MvASSIGN NAME = "l.field_count" VALUE = "{ [ g.Module_JSON ].JSON_Search_Filter_Clause_With_CustomFields( l.query, 'AND',	'OR',	l.field_count, l.filter:value, l.correlationlist, g.Module_Root $ l.module:module, 'JSON_ProductReviews_Fields_Query_Filter', l.callback_search_customfields, l.callback_search_field_count, l.data ) }">
		<MvELSEIF EXPR = "{ l.filter:name EQ 'search_OR' }">	<MvASSIGN NAME = "l.field_count" VALUE = "{ [ g.Module_JSON ].JSON_Search_Filter_Clause_With_CustomFields( l.query, 'OR',	'AND',	l.field_count, l.filter:value, l.correlationlist, g.Module_Root $ l.module:module, 'JSON_ProductReviews_Fields_Query_Filter', l.callback_search_customfields, l.callback_search_field_count, l.data ) }">
		</MvIF>
	</MvFOREACH>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductReviews_Fields_Query_Filter" PARAMETERS = "query var, field_count var, filter_name, filter_operator, filter_value, data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( 'AdditionalFields:' IN l.filter_name ) EQ 1 }">
		<MvASSIGN NAME = "l.field_code" VALUE = "{ gettoken( l.filter_name, ':', 2 ) }">

		<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Load_Code_Cached( l.field_code, l.additionalfield ) }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT JSON_Query_Callback_Handle_Field( l.query, '', l.field_code ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Search_Operator_Conversion( l.query, 'tgrav_' $ l.additionalfield:id $ '.value', l.filter_operator, l.filter_value, 'OR', 'OR' ) }">

		<MvFUNCTIONRETURN VALUE = 1>
	<MvELSEIF EXPR = "{ ( 'Product:' IN l.filter_name ) EQ 1 }">
		<MvASSIGN NAME = "l.field_code" VALUE = "{ gettoken( l.filter_name, ':', 2 ) }">

		<MvIF EXPR = "{ l.field_code NE 'id'			AND
						l.field_code NE 'code'			AND
						l.field_code NE 'sku'			AND
						l.field_code NE 'name'			AND
						l.field_code NE 'thumbnail'		AND
						l.field_code NE 'image'			AND
						l.field_code NE 'price'			AND
						l.field_code NE 'cost'			AND
						l.field_code NE 'descrip'		AND
						l.field_code NE 'catcount'		AND
						l.field_code NE 'weight'		AND
						l.field_code NE 'taxable'		AND
						l.field_code NE 'active'		AND
						l.field_code NE 'page_title'	AND
						l.field_code NE 'dt_created'	AND
						l.field_code NE 'dt_updated' }">
			<MvFUNCTIONRETURN>
		</MvIF>

		<MvIF EXPR = "{ NOT JSON_Query_Callback_Handle_Product( l.query, l.callback_query_fields ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Search_Operator_Conversion( l.query, 'prod.' $ l.field_code, l.filter_operator, l.filter_value, 'OR', 'OR' ) }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Query_Callback_Handle_Field" PARAMETERS = "query var, callback_query_customfields, field_code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_code EQ '*' }">
		<MvFOREACH ITERATOR = "l.additionalfield" ARRAY = "l.additionalfields" COUNT = "{ TGReviewsAdditionalField_Load_All_Cached( l.additionalfields ) }">
			<MvASSIGN NAME = "l.null" VALUE = "{ JSON_Query_Callback_Handle_Field( l.query, l.callback_query_customfields, l.additionalfield:code ) }">
		</MvFOREACH>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Load_Code_Cached( l.field_code, l.additionalfield ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Data_Reference( l.query, 'tgrav_' $ l.additionalfield:id, l.ref ) }">

	<MvREFERENCE NAME = "l.queried" VARIABLE = "l.ref:data:queried">

	<MvIF EXPR = "{ l.queried }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.queried" VALUE = 1>

	<MvASSIGN NAME = "g.Filter_Fields:AdditionalFields" MEMBER = "{ l.additionalfield:code }" VALUE = 1>

	<MvASSIGN NAME = "{ 'g.AdditionalField_' $ l.additionalfield:id $ '_id' }" VALUE = "{ l.additionalfield:id }">

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'tgrav_' $ l.additionalfield:id $ '.value AS additionalfield_' $ l.additionalfield:code ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'tgrp', g.Store_Table_Prefix $ 'TGReviewsAdditionalValues', 'tgrav_' $ l.additionalfield:id, 'tgrav_' $ l.additionalfield:id $ '.field_id = ? AND tgrav_' $ l.additionalfield:id $ '.review_id = tgrp.id', 'g.AdditionalField_' $ l.additionalfield:id $ '_id' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Query_Callback_Handle_Product" PARAMETERS = "query var, callback_query_customfields" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Data_Reference( l.query, 'prod', l.ref ) }">

	<MvREFERENCE NAME = "l.queried" VARIABLE = "l.ref:data:queried">

	<MvIF EXPR = "{ l.queried }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.queried" VALUE = 1>

	<MvASSIGN NAME = "g.Filter_Fields:Product" VALUE = 1>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query,
																'prod.id AS product_id,
																 prod.code AS product_code,
																 prod.sku AS product_sku,
																 prod.name AS product_name,
																 prod.thumbnail AS product_thumbnail,
																 prod.image AS product_image,
																 prod.price AS product_price,
																 prod.cost AS product_cost,
																 prod.descrip AS product_descrip,
																 prod.catcount AS product_catcount,
																 prod.weight AS product_weight,
																 prod.taxable AS product_taxable,
																 prod.active AS product_active,
																 prod.page_title AS product_page_title,
																 prod.dt_created AS product_dt_created,
																 prod.dt_updated AS product_dt_updated' ) }">
	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'tgrp', g.Store_Table_Prefix $ 'Products', 'prod', 'tgrp.product_id = prod.id', '' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductReviews_OrderBy_Callback_With_Fields" PARAMETERS = "query var, sort, list, default_sort, callback_query_fields" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.sort_field" VALUE = "{ [ g.Module_Library_DB ].SQL_Sort_Field( l.sort, l.sort_direction ) }">

	<MvIF EXPR = "{ ( 'AdditionalFields:' IN l.sort_field ) EQ 1 }">
		<MvASSIGN NAME = "l.null" VALUE = "{ JSON_ProductReviews_Fields_Query_OrderBy( l.query, l.callback_query_fields, l.sort_direction, gettoken( l.sort_field, ':', 2 ) ) }">
	<MvELSEIF EXPR = "{ ( 'Product:' IN l.sort_field ) EQ 1 }">
		<MvASSIGN NAME = "l.null" VALUE = "{ JSON_ProductReviews_Product_Query_OrderBy( l.query, l.callback_query_fields, l.sort_direction, gettoken( l.sort_field, ':', 2 ) ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.query, l.sort, l.list, l.default_sort ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_ProductReviews_Fields_Query_OrderBy" PARAMETERS = "query var, callback_query_fields, direction, field_code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Load_Code_Cached( l.field_code, l.additionalfield ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_Query_Callback_Handle_Field( l.query, l.callback_query_fields, l.field_code ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY_Flag_With_Alias_And_Member( l.query, 'tgrav_' $ l.additionalfield:id $ '.value', 'additionalfield_' $ l.additionalfield:code, 'tgrav_' $ l.additionalfield:id $ ':value', l.direction, '' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "JSON_ProductReviews_Product_Query_OrderBy" PARAMETERS = "query var, callback_query_fields, direction, field_code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.field_code NE 'id'			AND
					l.field_code NE 'code'			AND
					l.field_code NE 'sku'			AND
					l.field_code NE 'name'			AND
					l.field_code NE 'thumbnail'		AND
					l.field_code NE 'image'			AND
					l.field_code NE 'price'			AND
					l.field_code NE 'cost'			AND
					l.field_code NE 'descrip'		AND
					l.field_code NE 'catcount'		AND
					l.field_code NE 'weight'		AND
					l.field_code NE 'taxable'		AND
					l.field_code NE 'active'		AND
					l.field_code NE 'page_title'	AND
					l.field_code NE 'dt_created'	AND
					l.field_code NE 'dt_updated' }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT JSON_Query_Callback_Handle_Product( l.query, l.callback_query_fields ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY_Flag_With_Alias_And_Member( l.query, 'prod.' $ l.field_code, 'prod_' $ l.field_code, 'prod:' $ l.field_code, l.direction, '' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_TGReviewsProduct_Fields_With_Query" PARAMETERS = "query var, review_id, view" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ miva_struct_members( g.Filter_Fields:AdditionalFields, l.additionalfield_codes ) }">
		<MvASSIGN NAME = "l.additionalfield_count" VALUE = 0>
		,"AdditionalFields":
		{
			<MvFOREACH ITERATOR = "l.additionalfield_code" ARRAY = "l.additionalfield_codes">
				<MvIF EXPR = "{ l.additionalfield_count GT 0 }">,</MvIF>
				"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.additionalfield_code ) }">": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.additionalfield_' $ l.additionalfield_code ) ) }">"
				<MvASSIGN NAME = "l.additionalfield_count" VALUE = "{ l.additionalfield_count + 1 }">
			</MvFOREACH>
		}
	</MvIF>
	<MvIF EXPR = "{ miva_member_exists( g.Filter_Fields, 'Product' ) }">
		,"Product":
		{ 
			"id":			<MvEVAL EXPR = "{ int( miva_variable_value( l.view $ '.d.product_id' ) ) }">,
			"code":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_code' ) ) }">",
			"sku":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_sku' ) ) }">",
			"name":			"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_name' ) ) }">",
			"thumbnail":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_thumbnail' ) ) }">",
			"image":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_image' ) ) }">",
			"price":		<MvEVAL EXPR = "{ miva_variable_value( l.view $ '.d.product_price' ) ROUND 2 }">,
			"cost":			<MvEVAL EXPR = "{ miva_variable_value( l.view $ '.d.product_cost' ) ROUND 2 }">,
			"descrip":		"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_descrip' ) ) }">",
			"catcount":		<MvEVAL EXPR = "{ int( miva_variable_value( l.view $ '.d.product_catcount' ) ) }">,
			"weight":		<MvEVAL EXPR = "{ miva_variable_value( l.view $ '.d.product_weight' ) ROUND 2 }">,
			"taxable":		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( miva_variable_value( l.view $ '.d.product_taxable' ) ) }">,
			"active":		<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Boolean( miva_variable_value( l.view $ '.d.product_active' ) ) }">,
			"page_title":	"<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( miva_variable_value( l.view $ '.d.product_page_title' ) ) }">",
			"dt_created":	<MvEVAL EXPR = "{ int( miva_variable_value( l.view $ '.d.product_dt_created' ) ) }">,
			"dt_updated":	<MvEVAL EXPR = "{ int( miva_variable_value( l.view $ '.d.product_dt_updated' ) ) }">
		}
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Review_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Insert_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Product_Review_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Insert_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Review_Insert_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Product_Load( l.product ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Product_Code', 'Please select a valid Product Code.' ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_CUS_JSON ].JSON_Customer_Load( l.customer ) }">
		<MvASSIGN NAME = "l.review:cust_id" VALUE = "{ l.customer:id }">
	</MvIF>

	<MvASSIGN NAME = "l.review:product_id" VALUE = "{ l.product:id }">

	<MvASSIGN NAME = "l.fld_name"	VALUE = "o">
	<MvASSIGN NAME = "l.fld_email"	VALUE = "o">
	<MvASSIGN NAME = "l.fld_loc"	VALUE = "o">
	<MvASSIGN NAME = "l.fld_summry"	VALUE = "o">
	<MvASSIGN NAME = "l.fld_title"	VALUE = "o">

	<MvIF EXPR = "{ l.general_settings:fld_name }">		<MvASSIGN NAME = "l.fld_name"	VALUE = "R"> </MvIF>
	<MvIF EXPR = "{ l.general_settings:fld_email }">	<MvASSIGN NAME = "l.fld_email"	VALUE = "R"> </MvIF>
	<MvIF EXPR = "{ l.general_settings:fld_loc }">		<MvASSIGN NAME = "l.fld_loc"	VALUE = "R"> </MvIF>
	<MvIF EXPR = "{ l.general_settings:fld_summry }">	<MvASSIGN NAME = "l.fld_summry"	VALUE = "R"> </MvIF>
	<MvIF EXPR = "{ l.general_settings:fld_title }">	<MvASSIGN NAME = "l.fld_title"	VALUE = "R"> </MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 			'Approved',		l.review:approved )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 			'Created',		l.review:created,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 			'Order_ID',		l.review:order_id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 			'Rating',		l.review:rating,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 			'Notify',		l.review:notify )				OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 			'Verified',		l.review:verified )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 			'Notified',		l.review:notified,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 			'Store_Reply',	l.review:store_rply )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		l.fld_name,		'Name',			l.review:name )					OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		l.fld_email,	'Email',		l.review:email )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		l.fld_loc,		'Location',		l.review:location )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		l.fld_summry,	'Summary',		l.review:summary )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		l.fld_title,	'Title',		l.review:title )
				}">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_JSON ].JSON_Input_Retrieve( 'AdditionalFields', l.review:additionalfields ) }">

	<MvIF EXPR = "{ l.review:rating EQ 0 OR l.review:rating GT 5 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Rating', 'Rating must be between 1-5.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.review:email }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( l.review:email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Email', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Insert( l.review ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00036', 'Added review \'' $ l.review:id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Review_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Update_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Product_Review_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Update_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Review_Update_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 0, 0, 1, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'ID',	l.review:id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Load( l.review:id, l.review ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'A review with the id \'' $ l.review:id $ '\' not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_Feature_CUS_JSON ].JSON_Customer_Load( l.customer ) }">
		<MvASSIGN NAME = "l.review:cust_id" VALUE = "{ l.customer:id }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 	'Approved',		l.review:approved )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 	'Created',		l.review:created,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 	'Order_ID',		l.review:order_id,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 	'Rating',		l.review:rating,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 	'Notify',		l.review:notify )				OR
					NOT [ g.Module_JSON ].JSON_Input_Boolean(	'o', 	'Verified',		l.review:verified )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 	'Store_Reply',	l.review:store_rply )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',	'Name',			l.review:name )					OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',	'Email',		l.review:email )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',	'Location',		l.review:location )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',	'Summary',		l.review:summary )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o',	'Title',		l.review:title )
				}">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ [ g.Module_JSON ].JSON_Input_Retrieve( 'AdditionalFields', l.additionalfields ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ miva_struct_merge( l.additionalfields, l.review:additionalfields ) }">
	</MvIF>

	<MvIF EXPR = "{ l.review:rating EQ 0 OR l.review:rating GT 5 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Rating', 'Rating must be between 1-5.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.review:email }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( l.review:email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Email', g.Validation_Message ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Update( l.review ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00037', 'Updated review \'' $ l.review:id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Review_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Delete_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_Product_Review_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_Product_Review_Delete_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_Product_Review_Delete_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 0, 0, 0, 1 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number( 'R', 'ID',	l.review_id, -1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Load( l.review_id, l.review ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'A review with the id \'' $ l.review_id $ '\' not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Delete( l.review_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ TGReviewsProductsAverages_Update_Product( l.review:product_id ) }">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00038', 'Deleted review \'' $ l.review:id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalFields_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Load_Query_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_AdditionalFields_Load_Query" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Load_Query_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalFields_Load_Query_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvASSIGN NAME = "l.filter"			VALUE = "">
	<MvASSIGN NAME = "l.sort"			VALUE = "">
	<MvASSIGN NAME = "l.offset"			VALUE = 0>
	<MvASSIGN NAME = "l.count"			VALUE = 0>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Filter(	'o', 'Filter',	l.filter )				OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'o', 'Sort',	l.sort )				OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Offset',	l.offset,	-1, -1 )	OR
					NOT [ g.Module_JSON ].JSON_Input_Number(	'o', 'Count',	l.count,	-1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query, 's.*' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGReviewsAdditionalFields', 's' ) }">

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Filter( l.search_query, l.filter, 'id:s.id,code:s.code,name:s.name' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.search_query, l.sort, 'id:s.id,code:s.code,name:s.name', 's.id' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'TGReviewsAdditionalFields', l.search_sql, l.search_fields, l.offset, l.count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-SYS-TGR-00039', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.additionalfields_count" VALUE = 0>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"total_count":	<MvEVAL EXPR = "{ int( l.total_count ) }">,
		"start_offset":	<MvEVAL EXPR = "{ int( l.offset ) }">,
		"data":
		[
		<MvWHILE EXPR = "{ ( NOT TGReviewsAdditionalFields.d.EOF ) AND ( ( l.count EQ 0 ) OR ( l.additionalfields_count LT l.count ) ) }">
			<MvEVAL EXPR = "{ TGReviewsAdditionalField_Read( l.additionalfield ) }">

			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_Start( l.additionalfields_count ) }">
				"id": <MvEVAL EXPR = "{ int( l.additionalfield:id ) }">,
				"code": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.additionalfield:code ) }">",
				"name": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.additionalfield:name ) }">"
			<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_ArrayElement_End() }">

			<MvSKIP NAME = "Merchant" VIEW = "TGReviewsAdditionalFields" ROWS = 1>
		</MvWHILE>
		]
	}
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalField_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Insert_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_AdditionalField_Insert" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Insert_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalField_Insert_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code( 	'R', 'Code', l.additionalfield:code ) OR
					NOT [ g.Module_JSON ].JSON_Input_Text(	'R', 'Name', l.additionalfield:name ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsAdditionalField_Load_Code( l.additionalfield:code, l.existing_additionalfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'An Additional Field with the code \'' $ l.existing_additionalfield:code $ '\' already exists' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Insert( l.additionalfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00040', 'Added Review Additional Field \'' $ l.additionalfield:id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalField_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Update_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_AdditionalField_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Update_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalField_Update_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'ID',		l.additionalfield_id,	-1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Load_ID( l.additionalfield_id, l.previous_additionalfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'An Additional Field with the id \'' $ l.additionalfield_id $ '\' not found' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.additionalfield" VALUE = "{ l.previous_additionalfield }">

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Code( 		'O', 'Code',	l.additionalfield:code )			OR
					NOT [ g.Module_JSON ].JSON_Input_Text(		'O', 'Name',	l.additionalfield:name ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ toupper( l.additionalfield:code ) NE toupper( l.previous_additionalfield:code ) }">
		<MvIF EXPR = "{ TGReviewsAdditionalField_Load_Code( l.additionalfield:code, l.existing_additionalfield ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'Code', 'An Additional Field with the code \'' $ l.existing_additionalfield:code $ '\' already exists' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Update( l.additionalfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00041', 'Updated Review Additional Field \'' $ l.additionalfield:id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalField_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'admin' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Delete_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_API_AdditionalField_Delete" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Delete_LowLevel( l.module ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "JSON_AdditionalField_Delete_LowLevel" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 0, 1, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Input_Number(	'R', 'ID',		l.additionalfield_id,	-1, -1 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_InputErrors() }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Load_ID( l.additionalfield_id, l.previous_additionalfield ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_FieldError( 'ID', 'An Additional Field with the id \'' $ l.additionalfield_id $ '\' not found' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Delete( l.additionalfield_id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00042', 'Deleted Review Additional Field \'' $ l.additionalfield_id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Success() }">
</MvFUNCTION>

<MvCOMMENT>
|
| Feature prod_facet
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Facets" PARAMETERS = "module var, facets var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.facets"										VALUE = "">

	<MvASSIGN NAME = "l.facets" INDEX = 1 MEMBER = "code"			VALUE = "tgr_rating">
	<MvASSIGN NAME = "l.facets" INDEX = 1 MEMBER = "name"			VALUE = "Average Rating">
	<MvASSIGN NAME = "l.facets" INDEX = 1 MEMBER = "allowed_types"	VALUE = "checkbox,radio,select,rangeslider">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Facet_Values_Query_PostProcess" PARAMETERS = "module var, query var, facet var, values var, value_count" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.facet:input_value_count EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ l.value_count }">
	</MvIF>

	<MvIF EXPR = "{ l.facet:type EQ 'rangeslider' }">
		<MvIF EXPR = "{ l.value_count NE 1 }">
			<MvFUNCTIONRETURN VALUE = "{ l.value_count }">
		</MvIF>

		<MvASSIGN NAME = "l.low_value"	VALUE = "{ min( gettoken( l.values[ 1 ]:value, '-', 1 ), gettoken( l.facet:input_values[ 1 ], '-', 1 ) ) }">
		<MvASSIGN NAME = "l.high_value"	VALUE = "{ max( gettoken( l.values[ 1 ]:value, '-', 2 ), gettoken( l.facet:input_values[ 1 ], '-', 2 ) ) }">

		<MvCOMMENT>
		|
		| Rangeslider doesn't use the count so we don't bother calculating it
		|
		</MvCOMMENT>

		<MvASSIGN NAME = "l.values" INDEX = 1	MEMBER = "value"	VALUE = "{ l.low_value $ '-' $ l.high_value }">
		<MvASSIGN NAME = "l.values" INDEX = 1	MEMBER = "count"	VALUE = 0>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.value_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Facet_Value_Prompt" PARAMETERS = "module var, facet var, value var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.facet:type EQ 'rangeslider' }">
		<MvFUNCTIONRETURN VALUE = "{ l.value }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ l.value $ ' & Up' }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Facet_Value_Prompt_JavaScript" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session:cache:tgreviews:module_product_facet_value_javascript }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "g.Session:cache:tgreviews:module_product_facet_value_javascript" VALUE = 1>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_CurrencyFormatter_JavaScript() }">

	<script type="text/javascript">
		MMFacet_RangeSlider_FormatValue_AddHook( '<MvEVAL EXPR = "{ encodejavascriptstring( l.module:code ) }">', function( facet_code, value ) {
			return MMCurrencyFormatter( value, true );
		} );
	</script>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Facet_Value_Selected" PARAMETERS = "module var, facet var, value var, input_values var, input_value_count" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ miva_array_search( l.input_values, 1, l.input_value, 'l.input_value EQ l.value' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Facet_Values_Query" PARAMETERS = "module var, query var, facet var, values var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.facet:code NE 'tgr_rating' }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.values" 		VALUE = "">
	<MvASSIGN NAME = "l.value_count"	VALUE = 0>
	<MvASSIGN NAME = "l.facet_query"	VALUE = "{ l.query }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.facet_query, 'DISTINCT FLOOR( tgra.rating ) AS tgr_rating, COUNT( tgra.rating ) AS rating_count' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.facet_query, g.Store_Table_Prefix $ 'TGReviewsProductsAverages', 'tgra' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_GROUP_BY( l.facet_query, 'tgra.rating' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.facet_query, 'tgra.product_id = prod.id', '' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY( l.facet_query, 'tgra.rating', 'DESC' ) }">

	<MvASSIGN NAME = "l.facet_sql"		VALUE = "{ [ g.Module_Library_DB ].SQL_Query_Build( l.facet_query, l.facet_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'TGReviewsProductsAverages', l.facet_sql, l.facet_fields, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-137', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.ratings"		VALUE = "">
	<MvASSIGN NAME = "l.rating_count"	VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGReviewsProductsAverages.d.EOF }">
		<MvREFERENCEARRAY NAME = "l.value" VARIABLE = "l.values_by_value">
			<MvMEMBER NAME = "{ TGReviewsProductsAverages.d.tgr_rating }">
		</MvREFERENCEARRAY>

		<MvIF EXPR = "{ NOT ISNULL l.value:value }">
			<MvASSIGN NAME = "l.value:count"	VALUE = "{ l.value:count + TGReviewsProductsAverages.d.rating_count }">
		<MvELSE>
			<MvASSIGN NAME = "l.value:value"	VALUE = "{ TGReviewsProductsAverages.d.tgr_rating }">
			<MvASSIGN NAME = "l.value:count"	VALUE = "{ TGReviewsProductsAverages.d.rating_count }">

			<MvASSIGN NAME = "l.value_count"	VALUE = "{ miva_array_insert_ref( l.ratings, l.value, -1 ) }">
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProductsAverages" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">

	<MvASSIGN NAME = "l.count" VALUE = 4>
	
	<MvIF EXPR = "{ l.facet:type EQ 'rangeslider' }">
		<MvASSIGN NAME = "l.count" VALUE = 5>
	</MvIF>

	<MvWHILE EXPR = "{ l.count GT 0 }">
		<MvASSIGN NAME = "l.rating_value:value" VALUE = "{ l.count }">
		<MvASSIGN NAME = "l.rating_value:count" VALUE = 0>

		<MvFOREACH ITERATOR = "l.rating" ARRAY = "l.ratings">
			<MvIF EXPR = "{ l.rating:value LT l.count }">
				<MvFOREACHCONTINUE>
			</MvIF>

			<MvASSIGN NAME = "l.rating_value:count" VALUE = "{ l.rating_value:count + l.rating:count }">
		</MvFOREACH>

		<MvIF EXPR = "{ l.rating_value:count GT 0 }">
			<MvASSIGN NAME = "l.value_count" VALUE = "{ miva_array_insert_var( l.values, l.rating_value, -1 ) }">
		</MvIF>

		<MvASSIGN NAME = "l.count" VALUE = "{ l.count - 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.facet:type EQ 'rangeslider' AND l.value_count GT 1 }">
		<MvASSIGN NAME = "l.values" INDEX = 1	MEMBER = "value"	VALUE = "{ l.values[ 1 ]:value $ '-' $ l.values[ l.value_count ]:value }">
		<MvASSIGN NAME = "l.values" INDEX = 1	MEMBER = "count"	VALUE = "{ l.product_count }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-SYS-TGR-138', l.value_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Facet_Query_Search" PARAMETERS = "module var, query var, facet var, facet_values var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_DISTINCT( l.query ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM_Before( l.query, 'prod', g.Store_Table_Prefix $ 'TGReviewsProductsAverages', 'tgra' ) }">

	<MvIF EXPR = "{ l.facet:type EQ 'rangeslider' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SUB_WHERE_BEGIN( l.query ) }">
			<MvFOREACH ITERATOR = "l.facet_value" ARRAY = "l.facet_values">
				<MvASSIGN NAME = "l.rating_low"		VALUE = "{ floor( gettoken( l.facet_value, '-', 1 ) ) }">
				<MvASSIGN NAME = "l.rating_high"	VALUE = "{ floor( gettoken( l.facet_value, '-', 2 ) ) }">
	
				<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'FLOOR( tgra.rating ) >= ? AND FLOOR( tgra.rating ) <= ? AND prod.id = tgra.product_id', [ g.Module_Library_DB ].SQL_Query_Field( l.rating_low ) $ ',' $ [ g.Module_Library_DB ].SQL_Query_Field( l.rating_high ) ) }">
			</MvFOREACH>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SUB_WHERE_END_First( l.query, 'OR' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SUB_WHERE_BEGIN( l.query ) }">
			<MvFOREACH ITERATOR = "l.facet_value" ARRAY = "l.facet_values">
				<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.query, 'FLOOR( tgra.rating ) >= ? AND prod.id = tgra.product_id', [ g.Module_Library_DB ].SQL_Query_Field( l.facet_value ) ) }">
			</MvFOREACH>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SUB_WHERE_END_First( l.query, 'OR' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Client Side Feature (clientside)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( '.js' EIN g.Filename ) EQ len_var( g.Filename ) }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
	</MvIF>

	<MvIF EXPR = "{ Module_Clientside_Output_File( l.module, g.Filename ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.null"	VALUE = "{ miva_output_header( 'Status', '404 Not Found' ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Component Feature (component)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvFUNCTIONRETURN VALUE = "{ ComponentModule_Initialize_OrderItems( l.module, l.item, l.all_settings, l.settings ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvASSIGN NAME = "l.tabs"		VALUE = "{ 'GT_PAGE/' $ l.item $ ':Reviews - Order Item List Items Layout' }">

		<MvIF EXPR = "{ miva_array_elements( l.settings:imagetypes ) GT 0 }">
			<MvASSIGN NAME = "l.tabs"	VALUE = "{ l.tabs $ ',GT_PAGE/' $ l.item $ '_dimensions:Reviews - Order Items Image Dimensions' }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ l.tabs }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModuleValidate_SortMethod" PARAMETERS = "item, default_sort, value, sort_by, sorting_option, sorting_method" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ( l.default_sort EQ l.value ) AND ( NOT l.sort_by ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, '', 'Sorting option \'' $ l.sorting_option $ '\' must be enabled to use the Default Sort Method of ' $ l.sorting_method ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvASSIGN NAME = "l.fields:b_width"						VALUE = "{ trim( l.fields:b_width ) }">
		<MvASSIGN NAME = "l.fields:b_height"					VALUE = "{ trim( l.fields:b_height ) }">
		<MvASSIGN NAME = "l.fields:per_page"					VALUE = "{ trim( l.fields:per_page ) }">
		<MvASSIGN NAME = "l.fields:page_disp_count"				VALUE = "{ trim( l.fields:page_disp_count ) }">
		<MvASSIGN NAME = "l.fields:reverse_default_disp_order"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:reverse_default_disp_order ) }">
		<MvASSIGN NAME = "l.fields:predictdiscounts"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( l.fields:predictdiscounts ) }">

		<MvIF EXPR = "{ l.fields:paginate }">
			<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:per_page ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'per_page', g.Validation_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ l.fields:paginate_count }">
			<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:page_disp_count ) }">
				<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'page_disp_count', g.Validation_Message ) }">
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_width ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_width', g.Validation_Message ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_WholeNumber_Positive_Required( l.fields:b_height ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( l.item $ '_dimensions', l.field_prefix $ 'b_height', g.Validation_Message ) }">
		</MvIF>
	</MvIF>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = "{ l.fields:reverse_default_disp_order }">

		<MvASSIGN NAME = "l.selectedfield_count"					VALUE = "{ [ g.Module_Library_Utilities ].CustomFieldSelect_Selected( l.fields:fields_custom, l.settings:fields_custom ) }">

		<MvASSIGN NAME = "l.settings:predictdiscounts"	VALUE = "{ l.fields:predictdiscounts }">
		<MvASSIGN NAME = "l.imagetype_count"			VALUE = "{ [ g.Module_Library_Utilities ].ImageTypeSelect_Selected( l.fields:imagetypes, l.settings:imagetypes ) }">

		<MvCOMMENT>
		|
		| These settings are common between advanced and point + click mode
		|
		</MvCOMMENT>

		<MvIF EXPR = "{ l.fields:paginate }">
			<MvASSIGN NAME = "l.settings:per_page"			VALUE = "{ l.fields:per_page }">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:per_page"			VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ l.fields:paginate_count }">
			<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = "{ l.fields:page_disp_count }">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:page_disp_count"	VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.settings:items_per_page_filter"	VALUE = "{ l.fields:items_per_page_filter }">

		<MvASSIGN NAME = "l.settings:constrain"				VALUE = "{ l.fields:constrain }">
		<MvASSIGN NAME = "l.settings:b_width"				VALUE = "{ l.fields:b_width }">
		<MvASSIGN NAME = "l.settings:b_height"				VALUE = "{ l.fields:b_height }">

		<MvIF EXPR = "{ l.fields:default_sort EQ '' }">
			<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "id_desc">
		<MvELSE>
			<MvASSIGN NAME = "l.settings:default_sort"		VALUE = "{ l.fields:default_sort }">
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00147', l.module:name $ ' \'' $ l.item $ '\' settings updated' ) }">

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_ID( l.fields:template_version, l.template ) OR
						NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.settings:template_id, l.managedtemplate ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ crypto_md5( l.fields:template_code ) EQ crypto_md5( l.template:source ) }">
			<MvIF EXPR = "{ l.template:id NE l.managedtemplate:current_id }">
				<MvASSIGN NAME = "l.managedtemplate:current_id"	VALUE = "{ l.template:id }">

				<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Revert_ManagedTemplateVersion( l.managedtemplate, l.template, l.compile_error ) }">
					<MvIF EXPR = "{ len( l.compile_error ) }">
						<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', l.compile_error ) }">
						<MvFUNCTIONRETURN VALUE = 1>
					</MvIF>

					<MvFUNCTIONRETURN VALUE = 0>
				</MvIF>

				<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00148', l.module:name $ ' \'' $ l.item $ '\' template reverted' ) }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplateVersion( l.managedtemplate, l.fields:notes, l.fields:template_code, l.settings, l.compile_error ) }">
				<MvIF EXPR = "{ len( l.compile_error ) }">
					<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( l.item, l.field_prefix $ 'template_code', l.compile_error ) }">
					<MvFUNCTIONRETURN VALUE = 1>
				</MvIF>

				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00149',  l.module:name $ ' \'' $ l.item $ '\' template updated' ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var"  STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvIF EXPR = "{ l.fields:template_clear_history }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Delete_History( l.settings:template_id ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
	
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Template_Current( l.settings:template_id, l.template ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
	
			<MvASSIGN NAME = "l.fields:template_version"			VALUE = "{ l.template:id }">
			<MvASSIGN NAME = "l.fields:template_version_selected"	VALUE = "{ l.template:id }">
		<MvELSEIF EXPR = "{ l.fields:template_recall }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_ID( l.fields:template_version_selected, l.template ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
	
			<MvASSIGN NAME = "l.template_id"						VALUE = "{ l.settings:template_id }">
			<MvASSIGN NAME = "l.settings"							VALUE = "{ l.template:settings }">
			<MvASSIGN NAME = "l.settings:template_id"				VALUE = "{ l.template_id }">
	
			<MvASSIGN NAME = "l.load_fields"						VALUE = 1>
		<MvELSE>
			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Template_Current( l.settings:template_id, l.template ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	
		<MvCOMMENT>
		|
		| Load all image types
		|
		</MvCOMMENT>
	
		<MvASSIGN NAME = "l.imagetype_count"						VALUE = "{ [ g.Module_Library_DB ].ImageTypeList_Load_All( l.imagetypes ) }">
	
		<MvIF EXPR = "{ l.load_fields }">
			<MvIF EXPR = "{ ISNULL l.settings:constrain }">
				<MvASSIGN NAME = "l.fields:constrain"				VALUE = "B">
			<MvELSE>
				<MvASSIGN NAME = "l.fields:constrain"				VALUE = "{ l.settings:constrain }">
			</MvIF>
	
			<MvIF EXPR = "{ ISNULL l.settings:b_width }">
				<MvASSIGN NAME = "l.fields:b_width"					VALUE = 75>
			<MvELSE>
				<MvASSIGN NAME = "l.fields:b_width"					VALUE = "{ l.settings:b_width }">
			</MvIF>
	
			<MvIF EXPR = "{ ISNULL l.settings:b_height }">
				<MvASSIGN NAME = "l.fields:b_height"				VALUE = 75>
			<MvELSE>
				<MvASSIGN NAME = "l.fields:b_height"				VALUE = "{ l.settings:b_height }">
			</MvIF>
	
			<MvIF EXPR = "{ ISNULL l.settings:price }">
				<MvASSIGN NAME = "l.fields:price"					VALUE = "base">
			<MvELSE>
				<MvASSIGN NAME = "l.fields:price"					VALUE = "{ l.settings:price }">
			</MvIF>
	
			<MvASSIGN NAME = "l.fields:notes"						VALUE = "">
			<MvASSIGN NAME = "l.fields:template_code"				VALUE = "{ l.template:source }">
			<MvASSIGN NAME = "l.fields:predictdiscounts"			VALUE = "{ l.settings:predictdiscounts }">
			<MvASSIGN NAME = "l.fields:imagetypes"					VALUE = "{ l.settings:imagetypes }">
			<MvASSIGN NAME = "l.fields:image"						VALUE = "{ l.settings:imagetypes[ 1 ] }">
	
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].InitializeCustomFieldSelect( l.settings:fields_custom, l.fields:fields_custom ) }">
	
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "l.fields:paginate"				VALUE = 0>
				<MvASSIGN NAME = "l.fields:per_page"				VALUE = 12>
			<MvELSE>
				<MvASSIGN NAME = "l.fields:paginate"				VALUE = 1>
				<MvASSIGN NAME = "l.fields:per_page"				VALUE = "{ l.settings:per_page }">
			</MvIF>
	
			<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 }">
				<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 0>
				<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = 3>
			<MvELSE>
				<MvASSIGN NAME = "l.fields:paginate_count"			VALUE = 1>
				<MvASSIGN NAME = "l.fields:page_disp_count"			VALUE = "{ l.settings:page_disp_count }">
			</MvIF>
	
			<MvIF EXPR = "{ l.settings:items_per_page_filter EQ 0 }">
				<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = 0>
			<MvELSE>
				<MvASSIGN NAME = "l.fields:items_per_page_filter"	VALUE = "{ l.settings:items_per_page_filter }">
			</MvIF>
	
			<MvASSIGN NAME = "l.fields:template_version"			VALUE = "{ l.template:id }">
			<MvASSIGN NAME = "l.fields:template_version_selected"	VALUE = "{ l.template:id }">
	
			<MvIF EXPR = "{ l.settings:default_sort EQ '' }">
				<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "id_desc">
			<MvELSE>
				<MvASSIGN NAME = "l.fields:default_sort"			VALUE = "{ l.settings:default_sort }">
			</MvIF>
	
			<MvASSIGN NAME = "l.fields:reverse_default_disp_order" 	VALUE = "{ l.settings:reverse_default_disp_order }">
		</MvIF>
	
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'fields_custom,image,imagetypes,paginate,per_page,paginate_count,items_per_page_filter,page_disp_count,price,additionalprice,displaydiscounts,predictdiscounts,field_code,field_name,field_options,field_dtadded,field_notes,field_quantity,field_price,button_add,invmsg,default_sort,sort_by:switch,sort_by:name,sort_by:code,sort_by:price,sort_by:dtadded,reverse_default_disp_order,notes,template_code,template_version_selected,template_recall,template_clear_history,template_version,advanced' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item ) }">
			<table border="0" cellpadding="2" cellspacing="0" width="100%">
			<tr><td colspan="2" width="100%">
				<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea( l.field_prefix $ 'template_code', l.fields:template_code, 20, 100 ) }">
			</td></tr>
	
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'field_code,field_name,field_options,field_dtadded,field_notes,field_quantity,field_price' ) }">
	
			<MvASSIGN NAME = "l.customfield_count"					VALUE = "{ [ g.Module_Library_Utilities ].ProductCustomFieldList_Load( l.customfields ) }">
	
			<MvIF EXPR = "{ l.customfield_count }">
				<tr><td align="left" valign="top" nowrap>
					<b>Custom Fields:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCustomFieldSelect( l.field_prefix $ 'fields_custom', l.fields:fields_custom, l.customfields, l.customfield_count ) }">
				</td></tr>
			</MvIF>
	
			<MvIF EXPR = "{ l.imagetype_count EQ 0 }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image,imagetypes' ) }">
			<MvELSE>
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'image' ) }">

				<tr><td align="left" valign="top" nowrap>
					<b>Product Images:</b>
				</td><td align="left" width="100%">
					<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawImageTypeSelect( l.field_prefix $ 'imagetypes', l.fields:imagetypes, l.imagetypes, l.imagetype_count ) }">
				</td></tr>
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'price,additionalprice,displaydiscounts' ) }">

			<tr><td>
				&nbsp;
			</td><td width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckBox( l.fields:predictdiscounts, l.field_prefix $ 'predictdiscounts', 1, 'Calculate Predicted Discounts and Sale Price' ) }"></label>
			</td></tr>
	
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'button_add,invmsg' ) }">
	
			<tr>
				<td align="left" nowrap>
					<b>Sorting Options:</b>
				</td>
				<td width="100%">
					<table width="100%">
						<tr>
							<td align="left" style="padding-right: 15px;" nowrap>
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 0, l.fields:sort_by:switch, 'Off' ) }"></label><br />
								<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'sort_by:switch', 1, l.fields:sort_by:switch, 'On' ) }"></label>
							</td>
							<td width="100%">
								<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'sort_by:name,sort_by:code,sort_by:price,sort_by:dtadded' ) }">
							</td>
						</tr>
					</table>
				</td>
			</tr>
	
			<tr>
				<td align="left" style="padding-right: 15px;" nowrap>
					<b>Default Sort Method:</b>
				</td>
				<td width="100%">
					<table width="100%">
						<tr>
							<td align="left" style="padding-right: 15px;" nowrap>
								<select name="{ encodeentities( l.field_prefix ) $ 'default_sort' }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'id_desc',			l.fields:default_sort, 'Default' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_asc', 		l.fields:default_sort, 'Name Ascending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'name_desc',	 	l.fields:default_sort, 'Name Descending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_asc',		l.fields:default_sort, 'Code Ascending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'code_desc',		l.fields:default_sort, 'Code Descending' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'reviewed_desc',	l.fields:default_sort, 'Reviewed Products' ) }">
									<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawOption( 'reviewed_asc',	l.fields:default_sort, 'Products Awaiting Review' ) }">
								</select>
							</td>
							<td width="100%">
								<font color="red"><b>WARNING:</b></font>&nbsp;&nbsp;Changing the default sorting option to any value other than "default" will increase page load times and increase the load on your server. Be fully aware of the effect this will have on your store before changing this option!
							</td>
						</tr>
					</table>
				</td>
			</tr>
	
			<tr><td align="left" nowrap>
					&nbsp;
			</td><td width="100%">
					<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawCheckbox( l.fields:reverse_default_disp_order, l.field_prefix $ 'reverse_default_disp_order', '1', 'Reverse Default Display Order' ) }"></label>
			</td></tr>
	
			<tr><td align="left" nowrap valign="top">
				<b>Pagination:</b>
			</td><td align="left" nowrap width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 0, l.fields:paginate, 'Do not paginate order items' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate', 1, l.fields:paginate, 'Display' ) }"></label> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'per_page' }" value="{ encodeentities( l.fields:per_page ) }" size="5"> order items per page<br />
			</td></tr>
	
			<tr><td align="left" nowrap valign="top">
				<b>Page Numbers:</b>
			</td><td align="left" nowrap width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 0, l.fields:paginate_count, 'Do not show page count' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'paginate_count', 1, l.fields:paginate_count, 'Display' ) }"></label> <input type="text" name="{ encodeentities( l.field_prefix ) $ 'page_disp_count' }" value="{ encodeentities( l.fields:page_disp_count ) }" size="5"> pages at a time
			</td></tr>
	
			<tr><td align="left" nowrap valign="top">
				<b>Items Per Page Filter:</b>
			</td><td align="left" nowrap width="100%">
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 0, l.fields:items_per_page_filter, 'Off' ) }"></label><br />
				<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'items_per_page_filter', 1, l.fields:items_per_page_filter, 'On' ) }"></label>
			</td></tr>
	
			<tr><td colspan="2">&nbsp;</td></tr>
	
			<tr><td>
				Notes:
			</td><td width="100%">
				<input type="text" name="{ encodeentities( l.field_prefix ) $ 'notes' }" value="{ encodeentities( l.fields:notes ) }" size="40">
			</td></tr>
	
			<tr><td>
				Versions:
			</td><td width="100%">
				<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'template_recall' }" value="0">
				<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'template_clear_history' }" value="0">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_version' ) }">
				<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_ManagedTemplate_Version_Select( l.settings:template_id, l.field_prefix $ 'template_version_selected', l.fields:template_version_selected ) }">
				<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'template_recall' $ '\', 1 ); return false;' }">Recall</a>
				<a href="#" onclick="{ 'Toggle_Prompt( \'Are you sure you wish to delete all saved configuration settings for Review - Order Items Layout?\\r\\nThis action cannot be undone.\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'template_clear_history' $ '\', 1 ); return false;' }">Clear History</a>
			</td></tr>
			</table>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
		</MvIF>
	
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Tab_Visible( l.tab, l.item $ '_dimensions' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'constrain,b_width,b_height' ) }">
		<MvELSE>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_Start( 'GT_PAGE', l.item $ '_dimensions' ) }">
			<table border="0" cellpadding="2" cellspacing="0" width="100%">
				<tr>
					<td align="left" valign="top" nowrap><b>Product Image:</b></td>
					<td align="left" width="100%">
						<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'N', l.fields:constrain, 'No constraints' ) }"></label><br />
						<label><MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawRadio( l.field_prefix $ 'constrain', 'B', l.fields:constrain, 'Resize to fit within bounding box:' ) }"></label>
						<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_width' }" value="{ encodeentities( l.fields:b_width ) }" style="text-align:right;" size="5" /> x 
						<input type="text" name="{ encodeentities( l.field_prefix ) $ 'b_height' }" value="{ encodeentities( l.fields:b_height ) }" style="text-align:right;" size="5" /> pixels
					</td>
				</tr>
			</table>
			<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawGroupTabEntry_End() }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.settings:fields_custom"					VALUE = "">
	<MvASSIGN NAME = "l.settings:price"							VALUE = "base">
	<MvASSIGN NAME = "l.settings:additionalprice"				VALUE = "">
	<MvASSIGN NAME = "l.settings:predictdiscounts"				VALUE = 0>
	<MvASSIGN NAME = "l.settings:per_page"						VALUE = 10>
	<MvASSIGN NAME = "l.settings:page_disp_count"				VALUE = 3>
	<MvASSIGN NAME = "l.settings:items_per_page_filter"			VALUE = 1>
	<MvASSIGN NAME = "l.settings:imagetypes"					VALUE = "">
	<MvASSIGN NAME = "l.settings:constrain"						VALUE = "B">
	<MvASSIGN NAME = "l.settings:b_width"						VALUE = 75>
	<MvASSIGN NAME = "l.settings:b_height"						VALUE = 75>
	<MvASSIGN NAME = "l.settings:default_sort"					VALUE = "id_desc">
	<MvASSIGN NAME = "l.settings:reverse_default_disp_order" 	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvEVAL EXPR = "{ ComponentModule_Generate_Code( l.settings, l.template_source ) }">

		<MvASSIGN NAME = "l.settings:template_filename"	VALUE = "{ tolower( l.page:code ) $ '-' $ tolower( l.item ) $ '.mvc' }">
		<MvASSIGN NAME = "l.settings:template_id"		VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplate_NoDuplicates( l.template_source, l.settings, l.settings:template_filename ) }">

		<MvIF EXPR = "{ l.settings:template_id EQ 0 }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'buttons' )	OR
						NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Page_Assign_Item( l.page, 'urls' )	}">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Delete_ManagedTemplate( l.settings:template_id ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.TGR_Review_Check NE 1 }">
		<MvASSIGN NAME = "g.TGR_Error"				VALUE = "">
		<MvASSIGN NAME = "g.TGR_Review_ID"			VALUE = "">
		<MvASSIGN NAME = "g.TGR_Review_Success"		VALUE = 0>
		<MvASSIGN NAME = "g.TGR_Review_Check"		VALUE = 1>

		<MvIF EXPR = "{ NOT ISNULL g.Action }">
			<MvASSIGN NAME = "l.pos"		VALUE = 1>
			<MvASSIGN NAME = "l.current"	VALUE = "{ toupper( gettoken( g.Action, ',', l.pos ) ) }">

			<MvWHILE EXPR = "{ len( l.current ) }">
				<MvIF EXPR = "{ l.current EQ 'TGRADD' }">
					<MvASSIGN NAME = "l.null"					VALUE = "{ Product_Review_Insert( l.all_settings ) }">

					<MvWHILESTOP>
				</MvIF>

				<MvASSIGN NAME = "l.pos"		VALUE = "{ l.pos + 1 }">
				<MvASSIGN NAME = "l.current"	VALUE = "{ toupper( gettoken( g.Action, ',', l.pos ) ) }">
			</MvWHILE>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Head" PARAMETERS = "module var, item, all_settings var, settings" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.item EQ 'tgreviews_orderitems' }">
		<MvDO FILE = "{ g.Store_Template_Path $ l.item_settings:template_filename }" NAME = "l.null" VALUE = "{ Template_Render( l.null, l.settings ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	|	Parameters & Function check
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.function_name EQ 'form_template' }">
		<MvIF EXPR = "{ l.parameter_count NE 0 }">
			<MvASSIGN NAME = "l.null" VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' does not take any parameters' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSEIF EXPR = "{ l.function_name EQ 'reviews_template' }">
		<MvIF EXPR = "{ l.parameter_count NE 1 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' takes 1 parameter' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSEIF EXPR = "{ l.function_name EQ 'load_product_reviews' }">
		<MvIF EXPR = "{ l.parameter_count NE 2 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' takes 2 parameters' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Is_Variable( l.parameters[ 2 ] ) }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The second parameter must be a variable' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSEIF EXPR = "{ l.function_name EQ 'date' }">
		<MvIF EXPR = "{ l.parameter_count NE 3 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' takes 3 parameters' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Is_Variable( l.parameters[ 3 ] ) }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The third parameter must be a variable' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSEIF EXPR = "{ l.function_name EQ 'product_rating'				OR
						l.function_name EQ 'product_review_breakdowns'	OR
						l.function_name EQ 'product_review_count' }">
		<MvIF EXPR = "{ l.parameter_count NE 2 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' takes 2 parameters' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Is_Variable( l.parameters[ 2 ] ) }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The second parameter must be a variable' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSEIF EXPR = "{ l.function_name EQ 'test_mailafter_email' }">
		<MvIF EXPR = "{ l.parameter_count NE 2 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' takes 2 parameters' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSEIF EXPR = "{ l.function_name EQ 'review_check_customerxproduct' }">
		<MvIF EXPR = "{ l.parameter_count NE 3 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' takes 3 parameters' ) }">
		</MvIF>

		<MvIF EXPR = "{ NOT Is_Variable( l.parameters[ 3 ] ) }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, 'The second parameter must be a variable' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.param, '\'' $ l.function_name $ '\' is not a valid function' ) }">
	</MvIF>

	<MvCOMMENT>
	|
	|	tagerror
	|	22, 3 = display
	|	22, 0 = no display
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.new_param }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 0
			do_function
			pop
			tagerror	22, 3
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:
	</MvASSEMBLY>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Form_Template" PARAMETERS = "module var, param, all_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsTemplates_Load_Cached( l.templates ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.templates:form_tmpl, l.form_template ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.field_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.additionalfield" ARRAY = "l.additionalfields" COUNT = "{ TGReviewsAdditionalField_Load_All_Cached( l.additionalfields ) }">
		<MvASSIGN NAME = "l.field_count" VALUE = "{ l.field_count + 1 }">

		<MvASSIGN NAME = "l.all_settings:tgr:additional_fields" INDEX = "{ l.field_count }" MEMBER = "name" VALUE = "{ l.additionalfield:name }">
		<MvASSIGN NAME = "l.all_settings:tgr:additional_fields" INDEX = "{ l.field_count }" MEMBER = "code" VALUE = "{ l.additionalfield:code }">
		<MvASSIGN NAME = "l.all_settings:tgr:additional_fields" INDEX = "{ l.field_count }" MEMBER = "value" VALUE = "{ miva_variable_value( 'g.TGR_AdditionalFields:' $ l.additionalfield:code ) }">
	</MvFOREACH>

	<MvASSIGN NAME = "l.all_settings:tgr:fields_requred:name"		VALUE = "{ l.general_settings:fld_name }">
	<MvASSIGN NAME = "l.all_settings:tgr:fields_requred:email"		VALUE = "{ l.general_settings:fld_email }">
	<MvASSIGN NAME = "l.all_settings:tgr:fields_requred:location"	VALUE = "{ l.general_settings:fld_loc }">
	<MvASSIGN NAME = "l.all_settings:tgr:fields_requred:summary"	VALUE = "{ l.general_settings:fld_summry }">
	<MvASSIGN NAME = "l.all_settings:tgr:fields_requred:title"		VALUE = "{ l.general_settings:fld_title }">
	<MvASSIGN NAME = "l.all_settings:tgr:recaptcha:enabled"			VALUE = "{ l.general_settings:rc_enabled }">
	<MvASSIGN NAME = "l.all_settings:tgr:recaptcha:public_key"		VALUE = "{ l.general_settings:rc_pub_key }">

	<MvDO FILE = "{ g.Store_Template_Path $ l.form_template:filename }" NAME = "l.null" VALUE = "{ Template_Render( l.null, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Reviews_Template" PARAMETERS = "module var, param, all_settings var, product_id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsTemplates_Load_Cached( l.templates ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.templates:rvws_tmpl, l.review_template ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ Component_Load_Product_Reviews( l.module, l.param, l.all_settings, l.product_id, l.all_settings:tgr ) }">

	<MvDO FILE = "{ g.Store_Template_Path $ l.review_template:filename }" NAME = "l.null" VALUE = "{ Template_Render( l.null, l.all_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Load_Product_Reviews" PARAMETERS = "module var, param, all_settings var, product_id, output var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ Reviews_Load_Query_Cached( l.product_id, g.TGR_Show_Rating, g.TGR_Offset, g.TGR_Count, g.TGR_Sort, g.TGR_NextOffset, l.output ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Reviews_Load_Query" PARAMETERS = "product_id, rating_filter, offset, max, sort, nextoffset var, output var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.offset"			VALUE = "{ int( l.offset ) }">
	<MvASSIGN NAME = "l.max"			VALUE = "{ int( l.max ) }">
	<MvASSIGN NAME = "l.sort"			VALUE = "{ trim( l.sort ) }">
	<MvASSIGN NAME = "l.rating_filter"	VALUE = "{ int( l.rating_filter ) }">
	<MvASSIGN NAME = "l.product_id"		VALUE = "{ int( l.product_id ) }">

	<MvASSIGN NAME = "l.search_query"						VALUE = "">
	<MvASSIGN NAME = "l.output:pagination"					VALUE = "">
	<MvASSIGN NAME = "l.output:full_pagination"				VALUE = "">
	<MvASSIGN NAME = "l.output:reviews"						VALUE = "">
	<MvASSIGN NAME = "l.output:total_reviews_filtered"		VALUE = 0>
	<MvASSIGN NAME = "l.output:start_offset"				VALUE = 0>
	<MvASSIGN NAME = "l.nextoffset"							VALUE = 0>

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.max LE 0 AND l.general_settings:per_page GT 0 }">
		<MvASSIGN NAME = "l.max" VALUE = "{ l.general_settings:per_page }">
	<MvELSEIF EXPR = "{ l.max LE 0 }">
		<MvASSIGN NAME = "l.max" VALUE = 5>
	</MvIF>

	<MvIF EXPR = "{	l.sort NE '-id'			AND
					l.sort NE '-created'	AND
					l.sort NE 'created'		AND
					l.sort NE '-rating'		AND
					l.sort NE 'rating'
				}">
		<MvASSIGN NAME = "l.sort" VALUE = "{ l.general_settings:sort }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.search_query,'s.*' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_FROM( l.search_query, g.Store_Table_Prefix $ 'TGReviewsProducts', 's' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'approved = 1', '' ) }">

	<MvIF EXPR = "{ l.product_id GT 0 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'product_id = ?', [ g.Module_Library_DB ].SQL_Query_Field( l.product_id ) ) }">
	</MvIF>

	<MvIF EXPR = "{ l.rating_filter GT 0 AND l.rating_filter LE 5 }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_WHERE( l.search_query, 'rating = ?', [ g.Module_Library_DB ].SQL_Query_Field( l.rating_filter ) ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_OrderBy_Fields( l.search_query, l.sort, 'id:s.id,created:s.created,rating:s.rating', 's.id' ) }">

	<MvASSIGN NAME = "l.search_sql" VALUE = "{ [ g.Module_Library_DB].SQL_Query_Build( l.search_query, l.search_fields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].SQL_Query_Count( l.search_query, l.total_count ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00043' ) }">
	<MvELSEIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'TGReviewsProducts', l.search_sql, l.search_fields, l.offset, l.max ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00044', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.review_count"			VALUE = 0>
	<MvASSIGN NAME = "l.additionalfields_count"	VALUE = "{ TGReviewsAdditionalField_Load_All_Cached( l.additionalfields ) }">

	<MvWHILE EXPR = "{ ( NOT TGReviewsProducts.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.review_count LT l.max ) ) }">
		<MvEVAL EXPR = "{ TGReviewsProduct_Read( l.output:reviews[ ++l.review_count ] ) }">
		<MvASSIGN NAME = "l.output:reviews" INDEX = "{ l.review_count }" MEMBER = "additional_fields_count" VALUE = "{ TGReviewsAdditionalValue_Load_All_Review_Cached( l.output:reviews[ l.review_count ]:id, l.output:reviews[ l.review_count ]:additional_fields ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProducts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvIF EXPR = "{ l.review_count GT 0 }">
		<MvASSIGN NAME = "l.nextoffset" VALUE = "{ l.offset + l.review_count }">
	</MvIF>

	<MvASSIGN NAME = "l.output:total_reviews_filtered"	VALUE = "{ l.total_count }">
	<MvASSIGN NAME = "l.output:start_offset"			VALUE = "{ l.offset }">

	<MvASSIGN NAME = "l.null" 								VALUE = "{ TGReviewsProductsAverage_Load_Cached( l.product_id, l.product_rating ) }">
	<MvASSIGN NAME = "l.output:avg_rating"					VALUE = "{ l.product_rating:rating }">
	<MvASSIGN NAME = "l.output:review_count"				VALUE = "{ l.product_rating:rtng_count }">
	<MvASSIGN NAME = "l.output:product_rating:avg_rating"	VALUE = "{ l.output:avg_rating }">
	<MvASSIGN NAME = "l.output:product_rating:review_count"	VALUE = "{ l.output:review_count }">

	<MvCOMMENT>
	|
	|	Pagination
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ l.output:total_reviews_filtered EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.output:pagination:total"	VALUE = "{ ceil( l.total_count / l.max ) }">
	<MvASSIGN NAME = "l.prev_offset"				VALUE = "{ l.offset - l.max }">

	<MvIF EXPR = "{ l.prev_offset GE l.max }">
		<MvASSIGN NAME = "l.output:pagination:previous_params" VALUE = "{ 'TGR_Sort=' $ encodeattribute( l.sort ) $ '&TGR_Count=' $ encodeattribute( l.max ) $ '&TGR_Show_Rating=' $ encodeattribute( l.rating_filter ) $ '&TGR_Offset=' $ l.prev_offset }">
	</MvIF>

	<MvIF EXPR = "{ l.nextoffset LT l.output:total_reviews_filtered }">
		<MvASSIGN NAME = "l.output:pagination:next_params" VALUE = "{ 'TGR_Sort=' $ encodeattribute( l.sort ) $ '&TGR_Count=' $ encodeattribute( l.max ) $ '&TGR_Show_Rating=' $ encodeattribute( l.rating_filter ) $ '&TGR_Offset=' $ l.nextoffset }">
	</MvIF>

	<MvIF EXPR = "{ l.nextoffset }">	<MvASSIGN NAME = "l.output:pagination:current_page" VALUE = "{ ceil( l.nextoffset / l.max ) }">
	<MvELSE>							<MvASSIGN NAME = "l.output:pagination:current_page" VALUE = "{ ceil( l.total_count / l.max ) }">
	</MvIF>

	<MvIF EXPR = "{ l.output:pagination:total LE 5 }">
		<MvASSIGN NAME = "l.begin" VALUE = "1">
	<MvELSE>
		<MvIF EXPR = "{ l.output:pagination:current_page LE 3 }">
			<MvASSIGN NAME = "l.begin" VALUE = "1">
		<MvELSEIF EXPR = "{ ( l.output:pagination:current_page + 2 ) GE l.output:pagination:total }">
			<MvASSIGN NAME = "l.begin" VALUE = "{ l.output:pagination:total - 4 }">
		<MvELSE>
			<MvASSIGN NAME = "l.begin" VALUE = "{ l.output:pagination:current_page - 2 }">
		</MvIF>
	</MvIF>

	<MvFOR INDEX = "l.page_count" COUNT = "{ l.output:pagination:total }">
		<MvASSIGN NAME = "l.curr_offset" VALUE = "{ ( l.max * l.page_count ) - l.max }">

		<MvIF EXPR = "{ l.curr_offset EQ g.TGR_Offset }">
			<MvASSIGN NAME = "l.output:full_pagination:pages" INDEX = "{ l.page_count }" MEMBER = "current" VALUE = "1">
		</MvIF>

		<MvASSIGN NAME = "l.output:full_pagination:pages" INDEX = "{ l.page_count }" MEMBER = "page_number"	VALUE = "{ l.page_count }">
		<MvASSIGN NAME = "l.output:full_pagination:pages" INDEX = "{ l.page_count }" MEMBER = "params"		VALUE = "{ 'TGR_Sort=' $ encodeattribute( l.sort ) $ '&TGR_Count=' $ encodeattribute( l.max ) $ '&TGR_Show_Rating=' $ encodeattribute( l.rating_filter ) $ '&TGR_Offset=' $ l.curr_offset }">
	</MvFOR>

	<MvASSIGN NAME = "l.void" VALUE = "{ miva_array_copy( l.output:full_pagination:pages, l.begin, 5, l.output:pagination:pages, 0) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Reviews_Load_Query_Cached" PARAMETERS = "product_id, rating_filter, offset, max, sort, nextoffset var, output var"  STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id"								VALUE = "{ int( l.product_id ) }">

	<MvIF EXPR = "{ l.id LT 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00045' ) }">
	<MvELSEIF EXPR = "{ l.id EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ Reviews_Load_Query( 0, l.rating_filter, l.offset, l.max, l.sort, l.nextoffset, l.output ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:reviews_load_query_cache">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Reviews_Load_Query( l.id, l.rating_filter, l.offset, l.max, l.sort, l.nextoffset, l.cache:output ) }">

		<MvIF EXPR = "{ l.cache:result }">
			<MvREFERENCE NAME = "g.Session:cache:reviews_load_query_cache" INDEX = "{ l.id }" VARIABLE = "l.cache">
		<MvELSE>
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.output"						VALUE = "{ l.cache:output }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Product_Review_Breakdowns" PARAMETERS = "module var, param, all_settings var, product_id, breakdowns var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Product_Review_Breakdowns_Load_Cached( l.product_id, l.breakdowns ) }">
		<MvFUNCTIONRETURN>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Review_Breakdowns_Load" PARAMETERS = "product_id, breakdowns var"  STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.product_id EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ Product_Review_Breakdowns_Load_All( l.breakdowns ) }">
	</MvIF>

	<MvASSIGN NAME = "l.breakdowns" VALUE = "">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT COUNT(rating) as rating_count, rating FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE product_id = ? AND approved = 1 GROUP BY rating' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00046', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGReviewsProducts.d.EOF }">
		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ TGReviewsProducts.d.rating }" MEMBER = "rating"	VALUE = "{ TGReviewsProducts.d.rating }">
		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ TGReviewsProducts.d.rating }" MEMBER = "count"	VALUE = "{ TGReviewsProducts.d.rating_count }">

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProducts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ l.count LT 5 }">
		<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">

		<MvIF EXPR = "{ l.breakdowns[ l.count ] }">
			<MvWHILECONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ l.count }" MEMBER = "rating"	VALUE = "{ l.count }">
		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ l.count }" MEMBER = "count"	VALUE = 0>
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "Product_Review_Breakdowns_Load_Cached" PARAMETERS = "product_id, breakdowns var"  STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id"								VALUE = "{ int( l.product_id ) }">

	<MvIF EXPR = "{ l.id LT 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00047' ) }">
	<MvELSEIF EXPR = "{ l.id EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ Product_Review_Breakdowns_Load_All_Cached( l.breakdowns ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:product_review_breakdowns_load_cache">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Product_Review_Breakdowns_Load( l.id, l.cache:breakdowns ) }">

		<MvIF EXPR = "{ l.cache:result }">
			<MvREFERENCE NAME = "g.Session:cache:product_review_breakdowns_load_cache" INDEX = "{ l.id }" VARIABLE = "l.cache">
		<MvELSE>
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.breakdowns"						VALUE = "{ l.cache:breakdowns }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Review_Breakdowns_Load_All" PARAMETERS = "breakdowns var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.breakdowns" VALUE = "">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT COUNT(rating) as rating_count, rating FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE approved = 1 GROUP BY rating' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00143', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGReviewsProducts.d.EOF }">
		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ TGReviewsProducts.d.rating }" MEMBER = "rating"	VALUE = "{ TGReviewsProducts.d.rating }">
		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ TGReviewsProducts.d.rating }" MEMBER = "count"	VALUE = "{ TGReviewsProducts.d.rating_count }">

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProducts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ l.count LT 5 }">
		<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">

		<MvIF EXPR = "{ l.breakdowns[ l.count ] }">
			<MvWHILECONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ l.count }" MEMBER = "rating"	VALUE = "{ l.count }">
		<MvASSIGN NAME = "l.breakdowns" INDEX = "{ l.count }" MEMBER = "count"	VALUE = 0>
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Review_Breakdowns_Load_All_Cached" PARAMETERS = "breakdowns var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:product_review_breakdowns_load_all_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ Product_Review_Breakdowns_Load_All( l.cache:breakdowns ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.breakdowns"				VALUE = "{ l.cache:breakdowns }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Date" PARAMETERS = "module var, param, all_settings var, format, timestamp, return var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
	|
	|	Date( format, timestamp, return var )
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.time_zone"		VALUE = "{ g.Merchant_Local_Timezone }">
	<MvASSIGN NAME = "l.return"			VALUE = "">
	<MvASSIGN NAME = "l.months_long"	VALUE = "{ miva_array_deserialize( 'January,February,March,April,May,June,July,August,September,October,November,December' ) }">
	<MvASSIGN NAME = "l.months_short"	VALUE = "{ miva_array_deserialize( 'Jan,Feb,Mar,Apr,May,Jun,Jul,Aug,Sept,Oct,Nov,Dec' ) }">
	<MvASSIGN NAME = "l.days_long"		VALUE = "{ miva_array_deserialize( 'Sunday,Monday,Tuesday,Wednesday,Thursday,Friday,Saturday' ) }">
	<MvASSIGN NAME = "l.days_short"		VALUE = "{ miva_array_deserialize( 'Sun,Mon,Tues,Wed,Thurs,Fri,Sat' ) }">

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.format ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.format, l.pos, 1 ) }">

		<MvIF EXPR = "{ NOT ( l.char CIN 'djNSzlDmnFMYyLaAgGhHisc' ) }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ l.char }">
			<MvFORCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.char EQ 'd' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( time_t_dayofmonth( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'j' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ time_t_dayofmonth( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'N' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ time_t_dayofweek( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'S' }">
			<MvASSIGN NAME = "l.dow" VALUE = "{ time_t_dayofmonth( l.timestamp, l.time_zone ) }">
			<MvASSIGN NAME = "l.dow_a" VALUE = "{ l.dow MOD 10 }">
			<MvASSIGN NAME = "l.dow_b" VALUE = "{ l.dow MOD 100 }">

			<MvIF EXPR = "{ ( l.dow_a EQ 1 ) AND ( l.dow_b NE 11) }">
				<MvASSIGN NAME = "l.S" VALUE = "st">
			<MvELSEIF EXPR = "{ ( l.dow_a EQ 2) AND ( l.dow_b NE 12 ) }">
				<MvASSIGN NAME = "l.S" VALUE = "nd">
			<MvELSEIF EXPR = "{ ( l.dow_a EQ 3 ) AND ( l.dow_b NE 13 ) }">
				<MvASSIGN NAME = "l.S" VALUE = "rd">
			<MvELSE>
				<MvASSIGN NAME = "l.S" VALUE = "th">
			</MvIF>
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ l.S }">
		<MvELSEIF EXPR = "{ l.char EQ 'z' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ time_t_dayofyear( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'D' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ miva_variable_value( 'l.days_short[ ' $ time_t_dayofweek( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'l' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ miva_variable_value( 'l.days_long[ ' $ time_t_dayofweek( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'm' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( time_t_month( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'n' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ time_t_month( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'F' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ miva_variable_value( 'l.months_long[ ' $ time_t_month( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'M' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ miva_variable_value( 'l.months_short[ ' $ time_t_month( l.timestamp, l.time_zone ) $ ' ]') }">
		<MvELSEIF EXPR = "{ l.char EQ 'Y' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ time_t_year( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'y' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ substring_var( time_t_year( l.timestamp, l.time_zone ), 3, 2) }">
		<MvELSEIF EXPR = "{ l.char EQ 'L' }">
			<MvASSIGN NAME = "l.year" VALUE = "{ time_t_year( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ ( ( ( l.year MOD 400 ) EQ 0 ) OR ( ( l.year MOD 100 ) NE 0 ) ) AND ( ( l.year MOD 4 ) EQ 0 ) }">
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ '1' }">
			<MvELSE>
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ '0' }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'a' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 11 }">
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ 'pm' }">
			<MvELSE>
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ 'am' }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'A' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 11 }">
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ 'PM' }">
			<MvELSE>
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ 'AM' }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'g' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 12 }">
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ ( l.hour - 12 ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ l.hour }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'G' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ time_t_hour( l.timestamp, l.time_zone ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'h' }">
			<MvASSIGN NAME = "l.hour" VALUE = "{ time_t_hour( l.timestamp, l.time_zone ) }">
			<MvIF EXPR = "{ l.hour GT 12 }">
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( ( l.hour - 12 ), 2, 0 ) }">
			<MvELSE>
				<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( l.hour, 2, 0 ) }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ 'H' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( time_t_hour( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'i' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( time_t_min( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 's' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ padl( time_t_sec( l.timestamp, l.time_zone ), 2, 0 ) }">
		<MvELSEIF EXPR = "{ l.char EQ 'c' }">
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $
								padl( time_t_year( l.timestamp, l.time_zone ), 4, '0' )			$
								 padl( time_t_month( l.timestamp, l.time_zone ), 2, '0' ) 		$
								 padl( time_t_dayofmonth( l.timestamp, l.time_zone ), 2, '0' )	$
								 'T' 															$
								 padl( time_t_hour( l.timestamp, l.time_zone ), 2, '0' )		$
								 padl( time_t_min( l.timestamp, l.time_zone ), 2, '0' )			$
								 padl( time_t_sec( l.timestamp, l.time_zone ), 2, '0' )			$
								 'Z' }">
		<MvELSE>
			<MvASSIGN NAME = "l.return" VALUE = "{ l.return $ l.char }">
		</MvIF>
	</MvFOR>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Product_Rating" PARAMETERS = "module var, param, all_settings var, product_id, return var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.return" VALUE = 0>

	<MvIF EXPR = "{ TGReviewsProductsAverage_Load_Cached( l.product_id, l.product_rating ) }">
		<MvASSIGN NAME = "l.return" VALUE = "{ l.product_rating:rating }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Product_Review_Count" PARAMETERS = "module var, param, all_settings var, product_id, return var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.return" VALUE = 0>

	<MvIF EXPR = "{ TGReviewsProductsAverage_Load_Cached( l.product_id, l.product_rating ) }">
		<MvASSIGN NAME = "l.return" VALUE = "{ l.product_rating:tgr_review_count }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Test_MailAfter_Email" PARAMETERS = "module var, param, all_settings var, order_id, email_address" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Load_ID( l.order_id, l.order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load_Cached( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.email:to" 		VALUE = "{ l.email_address }">
	<MvASSIGN NAME = "l.email:from"		VALUE = "{ l.notification_settings:ma_from }">
	<MvASSIGN NAME = "l.email:cc"		VALUE = "">
	<MvASSIGN NAME = "l.email:bcc"		VALUE = "">
	<MvASSIGN NAME = "l.email:subject"	VALUE = "{ l.notification_settings:ma_subject }">

	<MvIF EXPR = "{ MailAfter_Email_Trigger_LowLevel( l.order, l.email ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Component_Review_Check_CustomerXProduct" PARAMETERS = "module var, param, all_settings var, cust_id, product_id, reviewed var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.reviewed" VALUE = "{ Review_Check_CustomerXProduct( l.cust_id, l.product_id ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Is_Variable" PARAMETERS = "variable var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.valid_chars" 		VALUE = "_.: ">
	<MvASSIGN NAME = "l.square_bracket_pos" VALUE = 0>
	<MvASSIGN NAME = "l.bracket_count" 		VALUE = 0>

	<MvFOR INDEX = "l.pos" COUNT = "{ len_var( l.variable ) }">
		<MvASSIGN NAME = "l.char" VALUE = "{ substring_var( l.variable, l.pos, 1 ) }">

		<MvIF EXPR = "{ l.pos EQ 1 }">
			<MvIF EXPR = "{ ( NOT isalpha( l.char ) ) AND
							( l.char NE '_' ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFORCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.char EQ '[' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count + 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos + 1 ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSEIF EXPR = "{ l.char EQ ']' }">
			<MvASSIGN NAME = "l.bracket_count" 				VALUE = "{ l.bracket_count - 1 }">
			<MvASSIGN NAME = "l.opening_square_bracket_pos" VALUE = "{ indexof( '[', l.variable, l.pos ) }">
			<MvASSIGN NAME = "l.closing_square_bracket_pos" VALUE = "{ indexof( ']', l.variable, l.pos + 1 ) }">

			<MvIF EXPR = "{ ( l.opening_square_bracket_pos GT 0 ) AND
							( l.opening_square_bracket_pos LT l.closing_square_bracket_pos ) }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.opening_square_bracket_pos - 1 }">
			<MvELSEIF EXPR = "{ l.closing_square_bracket_pos GT 0 }">
				<MvASSIGN NAME = "l.pos" VALUE = "{ l.closing_square_bracket_pos - 1 }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ ( NOT isdigit( l.char ) ) AND
							( NOT isalpha( l.char ) ) AND
							( NOT ( l.char IN l.valid_chars ) ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOR>

	<MvIF EXPR = "{ l.bracket_count NE 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Initialize_OrderItems" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.settings:items_per_page_filter }">
		<MvIF EXPR = "{ ISNULL g.Per_Page OR NOT [ g.Module_Admin ].Validate_WholeNumber_Required( g.Per_Page ) }">
			<MvIF EXPR = "{ l.settings:per_page EQ 0 }">
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = "{ l.settings:per_page }">
			</MvIF>
		<MvELSE>
			<MvIF EXPR = "{ g.Per_Page GE 0 }">
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = "{ g.Per_Page }">
			<MvELSE>
				<MvASSIGN NAME = "g.Per_Page"			VALUE = -1>
				<MvASSIGN NAME = "l.settings:per_page"	VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.Sort_By }">
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ g.Sort_By }">
	<MvELSE>
		<MvASSIGN NAME = "l.sort_by"					VALUE = "{ l.settings:default_sort }">
		<MvASSIGN NAME = "g.Sort_By"					VALUE = "{ l.settings:default_sort }">
	</MvIF>

	<MvIF EXPR = "{ l.sort_by EQ 'id_desc' AND 
	 				l.settings:reverse_default_disp_order }">
		<MvASSIGN NAME = "l.sort_by" 					VALUE = "id">
	</MvIF>

	<MvASSIGN NAME = "l.settings:item_count"			VALUE = "{ Runtime_ReviewsOrderItems_Load_Offset_Customer( g.Customer:id, g.Offset, l.settings:per_page, l.sort_by, g.NextOffset, l.settings:items ) }">
	<MvASSIGN NAME = "l.null"							VALUE = "{ Create_Pagination( l.all_settings, l.settings ) }">

	<MvIF EXPR = "{ l.settings:item_count EQ 0 }">
		<MvASSIGN NAME = "l.settings:empty"				VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.PrevOffset"						VALUE = "{ g.Offset - l.settings:per_page }">

	<MvIF EXPR = "{ g.PrevOffset LT 0 }">
		<MvASSIGN NAME = "g.PrevOffset"					VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomField_Lookup( l.all_settings, l.settings:fields_custom ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:predictdiscounts }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_CreateFromBasket( g.Basket, l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Load_PriceGroups_Basket( g.Basket, l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Disable_Unused_Item_PriceGroups( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Baseline_Discounts( l.discount_state ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFOREACH INDEX = "l.item_pos" ITERATOR = "l.orderitem" ARRAY = "l.settings:items" COUNT = "{ l.settings:item_count }">
		<MvASSIGN NAME = "l.null"												VALUE = "{ Load_Product_MostRecent_Order_Customer( l.orderitem:product_id, g.Customer:id, l.orderitem:order ) }">

		<MvIF EXPR = "{ l.orderitem:order:order_id }">
			<MvASSIGN NAME = "l.orderitem:order:formatted_orderdate_date"		VALUE = "{ [ g.Module_Library_Utilities ].Format_Date( l.orderitem:order:orderdate, s.miva_language ) }">
			<MvASSIGN NAME = "l.orderitem:order:formatted_orderdate_time"		VALUE = "{ [ g.Module_Library_Utilities ].Format_Time( l.orderitem:order:orderdate, s.miva_language ) }">
			<MvASSIGN NAME = "l.orderitem:order:formatted_price"				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.orderitem:order:price) }">
			<MvASSIGN NAME = "l.orderitem:order:formatted_retail"				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.orderitem:order:retail) }">
			<MvASSIGN NAME = "l.orderitem:order:formatted_base_price"			VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.orderitem:order:base_price) }">
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_Runtime( l.orderitem:product ) OR
						NOT [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_CustomFields( l.orderitem:product:id, l.orderitem:product, l.all_settings, l.settings:fields_custom ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.null" 												VALUE = "{ [ g.Module_Feature_TUI_UT ].CommonComponentFields_Initialize_Product_ImageTypes( l.orderitem:product:id, 0, l.orderitem:product, l.all_settings, l.settings:imagetypes, l.settings:constrain, l.settings:b_width, l.settings:b_height ) }">

		<MvIF EXPR = "{ l.settings:predictdiscounts }">
			<MvIF EXPR = "{ NOT [ g.Module_Feature_PGR_UT ].DiscountState_Predict_Product_Discounts( l.discount_state, l.orderitem:product, l.variant_id, l.orderitem:quantity, l.orderitem:product:price, l.orderitem:product:discounts, l.orderitem:product:discount_count ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvASSIGN NAME = "l.orderitem:product:formatted_price" 				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.orderitem:product:price ) }">

			<MvFOREACH ITERATOR = "l.discount" ARRAY = "l.orderitem:product:discounts" COUNT = "{ l.orderitem:product:discount_count }">
				<MvASSIGN NAME = "l.discount:formatted_discount" 				VALUE = "{ [ g.Module_Root $ g.Store:currncy_mod:module ].CurrencyModule_AddFormatting( g.Store:currncy_mod, l.discount:discount ) }">
			</MvFOREACH>
		</MvIF>

		<MvASSIGN NAME = "l.orderitem:product:link" 							VALUE = "{ [ g.Module_Feature_URI_UT ].Store_Product_URL( l.orderitem:product, l.null ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_ReviewsOrderItems_Load_Offset_Customer" PARAMETERS = "cust_id, offset, max, sort, nextoffset var, orderitems var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.cust_id }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.sort EQ 'id_desc' }">				<MvASSIGN NAME = "l.sort_col" VALUE = "prod.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'code_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.code">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'code_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.code">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'name_asc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.name">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSEIF EXPR = "{ l.sort EQ 'name_desc' }">		<MvASSIGN NAME = "l.sort_col" VALUE = "prod.name">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'reviewed_desc' }">	<MvASSIGN NAME = "l.sort_col" VALUE = "tgrp.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "DESC">
	<MvELSEIF EXPR = "{ l.sort EQ 'reviewed_asc' }">	<MvASSIGN NAME = "l.sort_col" VALUE = "tgrp.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	<MvELSE>											<MvASSIGN NAME = "l.sort_col" VALUE = "prod.id">			<MvASSIGN NAME = "l.sort_dir" VALUE = "ASC">
	</MvIF>

	<MvIF EXPR = "{ l.max EQ 0 }">	<MvASSIGN NAME = "l.limit" VALUE = 0>
	<MvELSE>						<MvASSIGN NAME = "l.limit" VALUE = "{ l.max + 1 }">
	</MvIF>

	<MvASSIGN NAME = "g.Session:cache:runtime_reviewsorderitems_load_offset_customer:cust_id"	VALUE = "{ int( l.cust_id ) }">
	<MvASSIGN NAME = "l.fields"																	VALUE = "g.Session:cache:runtime_reviewsorderitems_load_offset_customer:cust_id, g.Session:cache:runtime_reviewsorderitems_load_offset_customer:cust_id">

	<MvASSIGN NAME = "l.query"	VALUE = "{ 'SELECT
												oi.product_id				AS product_id,
												orders.cust_id				AS order_cust_id,
												prod.catcount 				AS product_catcount,
												prod.cancat_id 				AS product_cancat_id,
												prod.agrpcount 				AS product_agrpcount,
												prod.pgrpcount 				AS product_pgrpcount,
												prod.disp_order 			AS product_disp_order,
												prod.page_id 				AS product_page_id,
												prod.code 					AS product_code,
												prod.sku 					AS product_sku,
												prod.name 					AS product_name,
												prod.page_title 			AS product_page_title,
												prod.thumbnail 				AS product_thumbnail,
												prod.image 					AS product_image,
												prod.price 					AS product_price,
												prod.cost 					AS product_cost,
												prod.descrip 				AS product_descrip,
												prod.weight 				AS product_weight,
												prod.taxable 				AS product_taxable,
												prod.active 				AS product_active,
												prod.dt_created				AS product_dt_created,
												prod.dt_updated				AS product_dt_updated,
												' $ l.sort_col $ ' 			AS sort_order,
												ipc.inventory				AS ipc_inventory,
												ips.active					AS ips_active,
												ips.in_long					AS ips_in_long,
												ips.in_short				AS ips_in_short,
												ips.low_level				AS ips_low_level,
												ips.low_long				AS ips_low_long,
												ips.low_lvl_d				AS ips_low_lvl_d,
												ips.low_short				AS ips_low_short,
												ips.low_track				AS ips_low_track,
												ips.out_level				AS ips_out_level,
												ips.out_long				AS ips_out_long,
												ips.out_lvl_d				AS ips_out_lvl_d,
												ips.out_short				AS ips_out_short,
												ips.out_track				AS ips_out_track,
												iset.active					AS iset_active,
												iset.in_long				AS iset_in_long,
												iset.in_short				AS iset_in_short,
												iset.low_level				AS iset_low_level,
												iset.low_long				AS iset_low_long,
												iset.low_short				AS iset_low_short,
												iset.low_track				AS iset_low_track,
												iset.out_level				AS iset_out_level,
												iset.out_long				AS iset_out_long,
												iset.out_short				AS iset_out_short,
												iset.out_track				AS iset_out_track,
												COUNT( tgrp.id )			AS product_reviewed
											FROM ' $ 
												g.Store_Table_Prefix $ 'OrderItems oi 
												LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Orders orders ON orders.id = oi.order_id
												LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'TGReviewsProducts tgrp ON tgrp.product_id = oi.product_id AND tgrp.cust_id	= ?, ' $
												g.Store_Table_Prefix $ 'Products prod
												LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'InventoryProductSettings ips ON ips.product_id = prod.id
												LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'InventoryProductCounts ipc ON ipc.product_id = prod.id, ' $
												g.Store_Table_Prefix $ 'InventorySettings iset
											WHERE
												orders.cust_id	= ?					AND
												oi.product_id 	= prod.id 			AND
												prod.active		= 1 				AND
												( ' $ [ g.Module_Library_DB ].Runtime_Product_AvailabilityGroup_Where( l.fields ) $ ' )
											GROUP BY
												oi.product_id
											ORDER BY ' $
												l.sort_col $ ' ' $ l.sort_dir }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_OPENVIEW_Range( 'Merchant', 'OrderItems',
																			 l.query, l.fields,
																			 l.offset, l.limit ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00150', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.orderitem_count" VALUE = 0>
	<MvWHILE EXPR = "{ ( NOT OrderItems.d.EOF ) AND ( ( l.max EQ 0 ) OR ( l.orderitem_count LT l.max ) ) }">
		<MvREFERENCEARRAY NAME = "l.orderitem" VARIABLE = "l.orderitems">
			<MvDIMENSION INDEX = "{ ++l.orderitem_count }">
		</MvREFERENCEARRAY>

		<MvASSIGN NAME = "l.orderitem:product_id"			VALUE = "{ OrderItems.d.product_id }">
		<MvASSIGN NAME = "l.orderitem:product_reviewed"		VALUE = "{ OrderItems.d.product_reviewed }">

		<MvASSIGN NAME = "l.orderitem:product:id"			VALUE = "{ OrderItems.d.product_id }">
		<MvASSIGN NAME = "l.orderitem:product:catcount"		VALUE = "{ OrderItems.d.product_catcount }">
		<MvASSIGN NAME = "l.orderitem:product:cancat_id"	VALUE = "{ OrderItems.d.product_cancat_id }">
		<MvASSIGN NAME = "l.orderitem:product:agrpcount"	VALUE = "{ OrderItems.d.product_agrpcount }">
		<MvASSIGN NAME = "l.orderitem:product:pgrpcount"	VALUE = "{ OrderItems.d.product_pgrpcount }">
		<MvASSIGN NAME = "l.orderitem:product:disp_order"	VALUE = "{ OrderItems.d.product_disp_order }">
		<MvASSIGN NAME = "l.orderitem:product:page_id"		VALUE = "{ OrderItems.d.product_page_id }">
		<MvASSIGN NAME = "l.orderitem:product:code"			VALUE = "{ OrderItems.d.product_code }">
		<MvASSIGN NAME = "l.orderitem:product:sku"			VALUE = "{ OrderItems.d.product_sku }">
		<MvASSIGN NAME = "l.orderitem:product:name"			VALUE = "{ OrderItems.d.product_name }">
		<MvASSIGN NAME = "l.orderitem:product:page_title"	VALUE = "{ OrderItems.d.product_page_title }">
		<MvASSIGN NAME = "l.orderitem:product:thumbnail"	VALUE = "{ OrderItems.d.product_thumbnail }">
		<MvASSIGN NAME = "l.orderitem:product:image"		VALUE = "{ OrderItems.d.product_image }">
		<MvASSIGN NAME = "l.orderitem:product:price"		VALUE = "{ OrderItems.d.product_price }">
		<MvASSIGN NAME = "l.orderitem:product:cost"			VALUE = "{ OrderItems.d.product_cost }">
		<MvASSIGN NAME = "l.orderitem:product:descrip"		VALUE = "{ OrderItems.d.product_descrip }">
		<MvASSIGN NAME = "l.orderitem:product:weight"		VALUE = "{ OrderItems.d.product_weight }">
		<MvASSIGN NAME = "l.orderitem:product:taxable"		VALUE = "{ OrderItems.d.product_taxable }">
		<MvASSIGN NAME = "l.orderitem:product:active"		VALUE = "{ OrderItems.d.product_active }">
		<MvASSIGN NAME = "l.orderitem:product:dt_created"	VALUE = "{ OrderItems.d.product_dt_created }">
		<MvASSIGN NAME = "l.orderitem:product:dt_updated"	VALUE = "{ OrderItems.d.product_dt_updated }">

		<MvEVAL EXPR = "{ [ g.Module_Feature_INV_RT ].Runtime_Product_InventoryFields_Read( l.orderitem:product ) }">

		<MvSKIP NAME = "Merchant" VIEW = "OrderItems" ROWS = 1>
	</MvWHILE>
	
	<MvIF EXPR = "{ OrderItems.d.EOF }">
		<MvASSIGN NAME = "l.nextoffset"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.nextoffset"	VALUE = "{ l.offset + l.orderitem_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-SYS-TGR-00151', l.orderitem_count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Create_Pagination" PARAMETERS = "all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Runtime_ReviewsOrderItems_Count_Customer( g.Customer:id, l.settings:total_count ) }">
		<MvASSIGN NAME = "l.settings:page_disp_count" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.settings:page_disp_count EQ 0 OR l.settings:per_page EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL g.Per_Page }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Per_Page=' $ encodeattribute( g.Per_Page ) }">	</MvIF>
	<MvIF EXPR = "{ NOT ISNULL g.Sort_By }">	<MvASSIGN NAME = "l.link_additions"	VALUE = "{ l.link_additions $ '&Sort_By=' $ encodeattribute( g.Sort_By ) }">	</MvIF>

	<MvASSIGN NAME = "l.page_count"						VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	<MvASSIGN NAME = "l.settings:page_links:last_page"	VALUE = "{ l.page_count }">

	<MvIF EXPR = "{ g.NextOffset }">	<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( g.NextOffset / l.settings:per_page ) }">
	<MvELSE>							<MvASSIGN NAME = "l.settings:page_links:current_page" VALUE = "{ ceil( l.settings:total_count / l.settings:per_page ) }">
	</MvIF>

	<MvASSIGN NAME = "l.settings:page_links:next_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:prev_link_params"		VALUE = "{ 'Offset=' $ encodeattribute( ( l.settings:page_links:current_page - 2 ) * l.settings:per_page ) $ l.link_additions }">
	<MvASSIGN NAME = "l.settings:page_links:first_link_params"		VALUE = "{ 'Offset=0' $ l.link_additions }">

	<MvIF EXPR = "{ floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page EQ l.settings:total_count }">
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( ( l.settings:total_count - 1 ) / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
	<MvELSE>
		<MvASSIGN NAME = "l.last_page_offset"						VALUE = "{ floor( ( l.settings:total_count ) / l.settings:per_page ) * l.settings:per_page }">
		<MvASSIGN NAME = "l.settings:page_links:last_link_params"	VALUE = "{ 'Offset=' $ encodeattribute( floor( l.settings:total_count / l.settings:per_page ) * l.settings:per_page ) $ l.link_additions }">
	</MvIF>

	<MvIF EXPR = "{ l.page_count LT l.settings:page_disp_count }">
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.page_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.page_count - 1 ) / 2 ) }">
	<MvELSE>
		<MvASSIGN NAME = "l.left"	VALUE = "{ floor( ( l.settings:page_disp_count - 1 ) / 2 ) }">
		<MvASSIGN NAME = "l.right"	VALUE = "{ ceil( ( l.settings:page_disp_count - 1 ) / 2 ) }">
	</MvIF>

	<MvASSIGN NAME = "l.counter"	VALUE = 0>
	<MvASSIGN NAME = "l.left_final" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.left }">
		<MvIF EXPR = "{ ( ( l.settings:page_links:current_page - l.left + 1 ) + l.counter ) GT 1 }">
			<MvASSIGN NAME = "l.left_final"	VALUE = "{ l.left_final + 1 }">
		<MvELSE>
			<MvASSIGN NAME = "l.right" VALUE = "{ l.right + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvIF EXPR = "{ l.left_final EQ l.left }">
		<MvASSIGN NAME = "l.counter"	 VALUE = 0>
		<MvASSIGN NAME = "l.right_final" VALUE = 0>

		<MvWHILE EXPR = "{ l.counter LT l.right }">
			<MvIF EXPR = "{ ( ( l.settings:page_links:current_page + l.right - 1 ) - l.counter ) LT l.page_count }">
				<MvASSIGN NAME = "l.right_final" VALUE = "{ l.right_final + 1 }">
			<MvELSE>
				<MvASSIGN NAME = "l.left_final" VALUE = "{ l.left_final + 1 }">
			</MvIF>

			<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
		</MvWHILE>

		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right_final + 1 }">
	<MvELSE>
		<MvASSIGN NAME = "l.pages" VALUE = "{ l.left_final + l.right + 1 }">
	</MvIF>

	<MvASSIGN NAME = "l.counter" VALUE = 0>

	<MvWHILE EXPR = "{ l.counter LT l.pages }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "page_num" 		VALUE = "{ ( l.settings:page_links:current_page - l.left_final ) + l.counter }">
		<MvASSIGN NAME = "l.settings:page_links:pages" INDEX = "{ l.counter + 1 }" MEMBER = "link_params" 	VALUE = "{ 'Offset=' $ encodeattribute( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) $ l.link_additions }">

		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ 0 }">					<MvASSIGN NAME = "l.settings:page_links:contains_first"	VALUE = "YES">	</MvIF>
		<MvIF EXPR = "{ ( ( ( l.settings:page_links:current_page - l.left_final - 1 ) + l.counter ) * l.settings:per_page ) EQ l.last_page_offset }">	<MvASSIGN NAME = "l.settings:page_links:contains_last"	VALUE = "YES">	</MvIF>

		<MvASSIGN NAME = "l.counter" VALUE = "{ l.counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Generate_Code" PARAMETERS = "settings var, code var" STANDARDOUTPUTLEVEL = "">
	<MvCAPTURE VARIABLE = "l.code">
<MIVA STANDARDOUTPUTLEVEL = "text, html"><mvt:if expr="l.settings:tgreviews_orderitems:total_count EQ 0">
	<div class="o-layout">
		<div class="o-layout__item">
			<p class="x-messages x-messages--info">There are no products to review.</p>
		</div>
	</div>
	<mvt:exit />
</mvt:if>

<mvt:if expr="l.settings:tgreviews_orderitems:items_per_page_filter EQ 1">
	<div class="u-flex o-layout--align-center o-layout--justify-end o-layout--wrap">
		<div class="x-display-list-filtering t-display-list-filtering u-flex">
			<div class="x-display-list-filtering__per-page">
				<ul class="c-form-list">
					<li class="c-form-list__item">
						<label class="c-form-label u-font-xsmall u-text-uppercase u-text-normal" for="l-per_page">View</label>
						<div class="c-form-select">
							<select id="l-per_page" class="c-form-select__dropdown u-font-small u-text-uppercase u-text-bold" name="Per_Page" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Per_Page=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;Sort_By=' + encodeURIComponent( '&mvtj:global:Sort_By;' );">
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 12, g.Per_Page, '12 Items/Page' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 24, g.Per_Page, '24 Items/Page' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 48, g.Per_Page, '48 Items/Page' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( -1, g.Per_Page, 'All Items' )" />
							</select>
						</div>
					</li>
				</ul>
			</div>
			<!-- end .x-display-list-filtering__per-page -->
			<div class="x-display-list-filtering__sort-by">
				<ul class="c-form-list">
					<li class="c-form-list__item">
						<label class="c-form-label u-font-xsmall u-text-uppercase u-text-normal" for="l-sort_by">Sort By</label>
						<div class="c-form-select">
							<select id="l-sort_by" class="c-form-select__dropdown u-font-small u-text-uppercase u-text-bold" name="Sort_By" onchange="window.location.href='&mvtj:urls:_self:auto_sep;Sort_By=' + encodeURIComponent( this.options[ this.selectedIndex ].value ) + '&amp;Per_Page=' + encodeURIComponent( '&mvtj:global:Per_Page;' );">
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'id_desc', g.Sort_By, 'Default' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'name_asc', g.Sort_By, 'Name Ascending' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'name_desc', g.Sort_By, 'Name Descending' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'code_asc', g.Sort_By, 'Code Ascending' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'code_desc', g.Sort_By, 'Code Descending' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'reviewed_desc', g.Sort_By, 'Reviewed Products' )" />
								<mvt:do file="g.Module_Library_Utilities" name="l.success" value="DrawOption( 'reviewed_asc', g.Sort_By, 'Products Awaiting Review' )" />
							</select>
						</div>
					</li>
				</ul>
			</div>
			<!-- end .x-display-list-filtering__sort-by -->
		</div>
		<!-- end .x-display-list-filtering -->
	</div>
</mvt:if>

<div class="o-layout o-layout--wide t-basket">
	<div class="o-layout__item u-width-12">
		<table class="c-table-responsive t-basket__product-summary">
			<thead class="c-table-responsive_thead u-text-left">
				<tr class="c-table-responsive__row u-text-uppercase u-font-xsmall u-color-gray-3">
					<th class="c-table-responsive__cell" scope="col">Product</th>
					<th class="c-table-responsive__cell u-text-right" scope="col">&nbsp;</th>
				</tr>
			</thead>
			<tbody>
				<mvt:foreach iterator="orderitem" array="tgreviews_orderitems:items">
					<tr class="c-table-responsive__row">
						<td class="c-table-responsive__cell" scope="row" data-label="">
							<div class="o-layout">
								<div class="o-layout__item u-width-12 u-width-4--m u-width-3--l u-text-center">
									<p><img src="&mvte:orderitem:product:imagetypes:main;" alt="&mvte:orderitem:product:name;"></p>
								</div>
								<div class="o-layout__item u-width-12 u-width-8--m u-width-9--l t-basket__product-details">
									<a class="u-text-bold u-color-black" href="&mvte:orderitem:product:link;" title="&mvt:orderitem:product:name;" rel="nofollow">&mvt:orderitem:product:name;</a><br>
									<p>
										<span class="u-font-small">SKU: &mvt:orderitem:product:code;</span><br>
										<span class="u-font-small">Last Purchased: &mvt:orderitem:order:formatted_orderdate_date;</span><br>
										<mvt:if expr="l.settings:orderitem:order:price">
											<span class="u-font-small">Purchased for &mvt:orderitem:order:formatted_price;</span>
										</mvt:if>
									</p>
								</div>
							</div>
						</td>

						<td class="c-table-responsive__cell">
							<mvt:if expr="l.settings:orderitem:product_reviewed">
								Thanks for your review!
							<mvt:else>
								<a class="u-text-bold u-color-black" href="&mvte:orderitem:product:link;" title="&mvt:orderitem:product:name;" rel="nofollow">Review this Product</a>
							</mvt:if>
						</td>
					</tr>
				</mvt:foreach>
			</tbody>
		</table>
	</div>
</div>

<mvt:if expr="l.settings:tgreviews_orderitems:page_links:last_page GT 1">
	<nav class="x-pagination x-pagination--centered t-pagination">
		<ul class="o-list-inline">
			<mvt:if expr="l.settings:tgreviews_orderitems:page_links:current_page NE 1">
				<li class="o-list-inline__item">
					<a class="c-button c-button--hollow u-border-rounded u-color-gray-4" href="&mvte:urls:_self:auto_sep;&mvte:tgreviews_orderitems:page_links:prev_link_params;" title="Go to the previous page.">
						<span class="u-icon-chevron-left"></span>
					</a>
				</li>
			<mvt:else>
				<li class="o-list-inline__item">
					<span class="c-button c-button--hollow u-border-rounded u-color-gray-4" title="You are on the first page.">
						<span class="u-icon-chevron-left"></span>
					</span>
				</li>
			</mvt:if>
			<li class="o-list-inline__item o-layout--grow">
				<div class="c-form-select">
					<select class="c-form-select__dropdown u-font-small u-text-uppercase u-text-bold u-color-gray-4" name="Sort_By" onchange="window.location.href=this.options[ this.selectedIndex ].value">
						<mvt:foreach iterator="pages" array="tgreviews_orderitems:page_links:pages">
							<mvt:if expr="l.settings:tgreviews_orderitems:page_links:current_page EQ l.settings:pages:page_num">
								<option value="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;" selected>Page &mvte:pages:page_num; of &mvte:tgreviews_orderitems:page_links:last_page;</option>
							<mvt:else>
								<option value="&mvte:urls:_self:auto_sep;&mvte:pages:link_params;">Page &mvte:pages:page_num; of &mvte:tgreviews_orderitems:page_links:last_page;</option>
							</mvt:if>
						</mvt:foreach>
					</select>
				</div>
			</li>
			<mvt:if expr="l.settings:tgreviews_orderitems:page_links:current_page NE l.settings:tgreviews_orderitems:page_links:last_page">
				<li class="o-list-inline__item">
					<a class="c-button c-button--hollow u-border-rounded u-color-gray-4" href="&mvte:urls:_self:auto_sep;&mvte:tgreviews_orderitems:page_links:next_link_params;" title="Go to the next page.">
						<span class="u-icon-chevron-right"></span>
					</a>
				</li>
			<mvt:else>
				<li class="o-list-inline__item">
					<span class="c-button c-button--hollow u-border-rounded u-color-gray-4" title="You are on the last page.">
						<span class="u-icon-chevron-right"></span>
					</span>
				</li>
			</mvt:if>
		</ul>
	</nav>
	<!-- end .x-pagination -->
</mvt:if>
<MIVA STANDARDOUTPUTLEVEL = ""></MvCAPTURE>
</MvFUNCTION>

<MvFUNCTION NAME = "Runtime_ReviewsOrderItems_Count_Customer" PARAMETERS = "cust_id, count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.cust_id }">
		<MvASSIGN NAME = "l.count" VALUE = 0>
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.fields" VALUE = "l.cust_id">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "OrderItems"
				QUERY	= "{ 'SELECT
								COUNT( DISTINCT oi.product_id ) AS total_count
							  FROM ' $
								g.Store_Table_Prefix $ 'OrderItems oi ' $ '
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Orders orders ON orders.id = oi.order_id, ' $
								g.Store_Table_Prefix $ 'Products prod
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'InventoryProductSettings ips ON ips.product_id = prod.id
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'InventoryProductCounts ipc ON ipc.product_id = prod.id, ' $
								g.Store_Table_Prefix $ 'InventorySettings iset
							  WHERE
								orders.cust_id 	= ? 				AND
								oi.product_id = prod.id				AND
								prod.active		= 1					AND
								( ' $ [ g.Module_Library_DB ].Runtime_Product_AvailabilityGroup_Where( l.fields ) $ ' )' }"
				FIELDS	= "{ l.fields }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">	<MvASSIGN NAME = "l.count" VALUE = 0>
	<MvELSE>								<MvASSIGN NAME = "l.count" VALUE = "{ OrderItems.d.total_count }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvFUNCTION NAME = "Parse_Function_Parameters" PARAMETERS = "string, function_name var, parameters var, parameter_count var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.parameter_count" 	VALUE = 0>
	<MvASSIGN NAME = "l.loop_counter" 		VALUE = 0>
	<MvASSIGN NAME = "l.string" 			VALUE = "{ trim( l.string ) }">
	<MvASSIGN NAME = "l.starting_pos" 		VALUE = "{ indexof( '(', l.string, 1 ) + 1 }">
	<MvASSIGN NAME = "l.function_name" 		VALUE = "{ tolower( substring_var( l.string, 1, l.starting_pos - 2 ) ) }">

	<MvIF EXPR = "{ l.starting_pos EQ 1 }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Expected \'(\' to start the function call' ) }">
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.function_name }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Missing function name' ) }">
	</MvIF>

	<MvIF EXPR = "{ substring_var( l.string, len_var( l.string ), 1 ) NE ')' }">
		<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Expected \')\' to end the function call' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ 1 }">
		<MvASSIGN NAME = "l.quote_pos" 		VALUE = "{ indexof( '\'', l.string, l.starting_pos ) }">
		<MvASSIGN NAME = "l.end_quote_pos" 	VALUE = "{ indexof( '\'', l.string, l.quote_pos + 1 ) }">
		<MvASSIGN NAME = "l.comma_pos" 		VALUE = "{ indexof( ',', l.string, l.starting_pos ) }">

		<MvIF EXPR = "{ l.quote_pos AND NOT l.end_quote_pos }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Missing single quote' ) }">
		</MvIF>

		<MvWHILE EXPR = "{ ( l.comma_pos LT l.end_quote_pos ) AND ( l.comma_pos GT l.quote_pos ) }"> <MvCOMMENT> comma in a quoted string </MvCOMMENT>
			<MvASSIGN NAME = "l.comma_pos" VALUE = "{ indexof( ',', l.string, l.comma_pos + 1 ) }">
		</MvWHILE>

		<MvIF EXPR = "{ l.comma_pos EQ 0 }"> <MvCOMMENT> on the last parameter or we only have 1 parameter </MvCOMMENT>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ len_var( l.string ) - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ len_var( l.string ) }">
			<MvASSIGN NAME = "l.quote_pos" 		VALUE = 0>
		<MvELSE> <MvCOMMENT> if there are more commas to come, grab from the start to the comma</MvCOMMENT>
			<MvASSIGN NAME = "l.start" 			VALUE = "{ l.starting_pos }">
			<MvASSIGN NAME = "l.end" 			VALUE = "{ l.comma_pos - l.starting_pos }">
			<MvASSIGN NAME = "l.starting_pos" 	VALUE = "{ l.comma_pos + 1 }">
		</MvIF>

		<MvASSIGN NAME = "l.param" VALUE = "{ trim( substring_var( l.string, l.start, l.end ) ) }">

		<MvIF EXPR = "{ ISNULL l.param }">
			<MvIF EXPR = "{ l.comma_pos EQ 0 AND l.parameter_count EQ 0 }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Parameter number ' $ ( ++l.parameter_count ) $ ' cannot be empty' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.parameters" INDEX = "{ ++l.parameter_count }" VALUE = "{ l.param }">

		<MvIF EXPR = "{ l.quote_pos EQ 0 AND l.comma_pos EQ 0 }">
			<MvWHILESTOP>
		</MvIF>

		<MvIF EXPR = "{ l.loop_counter GT 1000 }">
			<MvFUNCTIONRETURN VALUE = "{ Parameter_Error( l.string, 'Error parsing parameters' ) }">
		</MvIF>

		<MvASSIGN NAME = "l.loop_counter" VALUE = "{ l.loop_counter + 1 }">
	</MvWHILE>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Parameter_Error" PARAMETERS = "param, message" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.error_count" 																	VALUE = "{ miva_array_elements( g.Session:tgreviews_errors ) }">
	<MvASSIGN NAME = "g.Session:tgreviews_errors" INDEX = "{ l.error_count + 1 }" MEMBER = "param"		VALUE = "{ l.param }">
	<MvASSIGN NAME = "g.Session:tgreviews_errors" INDEX = "{ l.error_count + 1 }" MEMBER = "message"	VALUE = "{ l.message }">

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvCOMMENT>
|
| Product Custom Fields Feature (fields_prod)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Fields" PARAMETERS = "module var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "code" VALUE = "tgr_rating">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "name" VALUE = "Product Rating">
	<MvASSIGN NAME = "l.fields" INDEX = 1 MEMBER = "type" VALUE = "currency">

	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "code" VALUE = "tgr_review_count">
	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "name" VALUE = "Product Review Count">
	<MvASSIGN NAME = "l.fields" INDEX = 2 MEMBER = "type" VALUE = "numeric">

	<MvFUNCTIONRETURN VALUE = 2>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Name" PARAMETERS = "module var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code EQ 'tgr_rating' }">				<MvFUNCTIONRETURN VALUE = "Product Rating">
	<MvELSEIF EXPR = "{ l.code EQ 'tgr_review_count' }">	<MvFUNCTIONRETURN VALUE = "Product Review Count">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Value" PARAMETERS = "module var, product_id, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvASSIGN NAME = "l.product_rating:tgr_rating"			VALUE = 0>
	<MvASSIGN NAME = "l.product_rating:tgr_review_count"	VALUE = 0>

	<MvASSIGN NAME = "l.null" VALUE = "{ TGReviewsProductsAverage_Load_Cached( l.product_id, l.product_rating ) }">

	<MvIF EXPR = "{ l.code EQ 'tgr_rating' }">				<MvFUNCTIONRETURN VALUE = "{ l.product_rating:rating }">
	<MvELSEIF EXPR = "{ l.code EQ 'tgr_review_count' }">	<MvFUNCTIONRETURN VALUE = "{ l.product_rating:rtng_count }">
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Set_Field" PARAMETERS = "module var, product_id, code, value" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Capabilities" PARAMETERS = "module var, code, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvASSIGN NAME = "l.capabilities:query"		VALUE = 0>
		<MvASSIGN NAME = "l.capabilities:search"	VALUE = 0>
		<MvASSIGN NAME = "l.capabilities:orderby"	VALUE = 0>
	<MvELSE>
		<MvASSIGN NAME = "l.capabilities:query"		VALUE = 1>
		<MvASSIGN NAME = "l.capabilities:search"	VALUE = 1>
		<MvASSIGN NAME = "l.capabilities:orderby"	VALUE = 1>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Query" PARAMETERS = "module var, query var, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00048', 'Unsupported field code' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_Data_Reference( l.query, 'tgrpa', l.ref ) }">

	<MvREFERENCE NAME = "l.queried" VARIABLE = "l.ref:data:queried">

	<MvIF EXPR = "{ l.queried }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.queried" VALUE = 1>

	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_SELECT( l.query, 'tgrpa.rating		AS tgrpa_tgr_rating,
																		  tgrpa.rtng_count	AS tgrpa_tgr_review_count' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_LEFT_OUTER_JOIN( l.query, 'prod', g.Store_Table_Prefix $ 'TGReviewsProductsAverages', 'tgrpa', 'tgrpa.product_id = prod.id', '' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Query_OrderBy" PARAMETERS = "module var, query var, code, direction" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00049', 'Unsupported field code' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Module_Product_Field_Query( l.module, l.query, l.code ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.code EQ 'tgr_rating' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY_Flag_With_Alias_And_Member( l.query, 'tgrpa.rating', 'tgrpa_' $ l.code, 'tgrpa:' $ l.code, l.direction, '' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_ORDER_BY_Flag_With_Alias_And_Member( l.query, 'tgrpa.rtng_count', 'tgrpa_' $ l.code, 'tgrpa:' $ l.code, l.direction, '' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Query_OrderBy_LoadIndexRecord" PARAMETERS = "module var, loaded_record var, product_id, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00050', 'Unsupported field code' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProductsAverage_Load_Cached( l.product_id, l.loaded_record:tgrpa ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Query_Search" PARAMETERS = "module var, query var, code, operator, value" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00051', 'Unsupported field code' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT Module_Product_Field_Query( l.module, l.query, l.code ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.code EQ 'tgr_rating' }">
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_Search_Operator_Conversion( l.query, 'tgrpa.rating', l.operator, l.value, 'OR', 'OR' ) }">
	<MvELSE>
		<MvEVAL EXPR = "{ [ g.Module_Library_DB ].SQL_Query_Search_Operator_Conversion( l.query, 'tgrpa.rtng_count', l.operator, l.value, 'OR', 'OR' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Query_Value" PARAMETERS = "module var, view_name, code" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.code NE 'tgr_rating' AND
					l.code NE 'tgr_review_count' }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ miva_variable_value( l.view_name $ '.d.' $ 'tgrpa_' $ l.code ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Field_Value_Array" PARAMETERS = "module var, product_id, code, values var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Set_Field_Array" PARAMETERS = "module var, product_id, code, values var, value_count" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00052', 'Custom field code \'' $ l.code $ '\' does not support arrays' ) }">
</MvFUNCTION>

<MvCOMMENT>
|
| Order Fulfillment Feature (fulfill)
|
</MvCOMMENT>

<MvFUNCTION NAME = "FulfillmentModule_ProcessOrder" PARAMETERS = "module var, order var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.order_source" VALUE = "{ tolower( l.order:source ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].Value_In_List( l.order_source, 'shopper,user' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load_Cached( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.notification_settings:ma_enable }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsOrder_Insert( l.order:id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Scheduled Task Feature (scheduledtask)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ScheduledTaskModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:provision_settings"	VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:provision"				VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Operations" PARAMETERS = "module var, operations var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.operations" INDEX = "1" MEMBER = "code"		VALUE = "tgr_order_emails">
	<MvASSIGN NAME = "l.operations" INDEX = "1" MEMBER = "descrip"	VALUE = "Trigger 'Mail After' Emails for Reviews">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Fields" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Validate" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Update" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Delete" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Execute" PARAMETERS = "module var, task var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.task:operation EQ 'tgr_order_emails' }">
		<MvFUNCTIONRETURN VALUE = "{ MailAfter_Email_Trigger_All( l.task ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00053', 'Unsupported operation' ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ScheduledTaskModule_Provision_Settings" PARAMETERS = "module var, task var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Product Add/Edit Screen Feature (vis_product)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Product_Tabs" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.product:id }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'SYSM', 1, 0, 0, 0 ) }">
		<MvFUNCTIONRETURN VALUE = "">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "TGR_PROD:Product Reviews">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Head" PARAMETERS = "module var, tab, product var" STANDARDOUTPUTLEVEL = "html, text, compresswhitespace">
	<MvIF EXPR = "{ l.tab NE 'TGR_PROD' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_JavaScript() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_ProductLookup_Dialog_CSS() }">

	<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=functions.js' }"></script>
	<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=productlookupcolumn.js' }"></script>
	<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=productreviewlist.js' }"></script>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].JavaScript_Set_A_Variable_Boolean( 'TGReviewsGeneralSettings_auto_apprv', l.general_settings:auto_apprv ) }">

	<script language="JavaScript">
		MMScreen_LoadFinished( function() { new ProductReview_List( MMScreenContext.product, TGReviewsGeneralSettings_auto_apprv); } );
	</script>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Content" PARAMETERS = "module var, tab, load_fields, product var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ l.tab NE 'TGR_PROD' }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].DrawButtons_Suppress( '[UPDATE]' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

	<div id="batchlist_productreviews"></div>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Update" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Insert" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Delete" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Product_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Product Configuration Change Notification Feature (not_prod)
|
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Notify_Product_Delete" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsProducts_Delete_Product( l.product:id ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProductsAverage_Delete( l.product:id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Notify_Product_Insert" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Notify_Product_Update" PARAMETERS = "module var, product var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| Data Export Feature (export)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ExportModule_Export" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.directory"							VALUE = "{ g.MerchantPath $ 's' $ padl( g.Store:id, 2, '0' ) $ '/export/' }">
	<MvASSIGN NAME = "l.output_file"						VALUE = "{ l.directory $ g.ReviewExport_File }">
	<MvASSIGN NAME = "g.ReviewExport_Export_Time_Start" 	VALUE = "{ s.time_t }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Start( 'Review Export Progress', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Show() }">

	<MvIF EXPR = "{ NOT ReviewExport_Initialize_Export( l.directory, l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	<MvELSEIF EXPR = "{ ReviewExport_Finalize_Export( l.directory, l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT ReviewExport_Export( l.output_file ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ g.ReviewExport_Export_ExportedCount GE g.ReviewExport_Export_ExportedTotalCount }">
		<MvASSIGN NAME = "g.ReviewExport_Export_NeedRefresh" VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "g.ReviewExport_File" 		VALUE = "{ trim( g.ReviewExport_File ) }">
	<MvASSIGN NAME = "g.ReviewExport_Email"		VALUE = "{ trim( g.ReviewExport_Email ) }">
	<MvASSIGN NAME = "g.ReviewExport_SendEmail"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.ReviewExport_SendEmail ) }">
	<MvASSIGN NAME = "g.ReviewExport_Append"	VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.ReviewExport_Append ) }">

	<MvIF EXPR = "{ ISNULL g.ReviewExport_File }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'ReviewExport_File', 'Please specify a file' ) }">
	<MvELSE>
		<MvIF EXPR = "{ '/' IN g.ReviewExport_File }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'ReviewExport_File', 'Please do not include a path in the file name' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ g.ReviewExport_SendEmail AND len( g.ReviewExport_Email ) EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'ReviewExport_Email', 'Please specify a valid email address' ) }">
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.ReviewExport_Email ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', 'ReviewExport_Email', 'Please specify a valid email address' ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ExportModule_Screen" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.ReviewExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "g.ReviewExport_Working_Append" VALUE = 1>

		<MvHIDE FIELDS = "g.Module_Code, g.ReviewExport_Export_Initialized, g.Reset, g.ReviewExport_Working_Append,
						  g.ReviewExport_File, g.ReviewExport_Append, g.ReviewExport_Email, g.ReviewExport_SendEmail,
						  g.ReviewExport_Export_ExportedCount, g.ReviewExport_Export_ExportedTotalCount, g.ReviewExport_Delimiter,
						  g.ReviewExport_LastReview">
		<script type="text/javascript">
			function exp_continue()
			{
				document.forms[ Screen ].submit();
			}
			window.onload = exp_continue;
		</script>

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_End() }">

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ g.Load_Fields }">
		<MvIF EXPR = "{ g.Reset NE 'No' }">
			<MvASSIGN NAME = "g.ReviewExport_File"			VALUE = "">
			<MvASSIGN NAME = "g.ReviewExport_Append"		VALUE = "">
			<MvASSIGN NAME = "g.ReviewExport_SendEmail"		VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ ISNULL g.ReviewExport_File }">		<MvASSIGN NAME = "g.ReviewExport_File"		VALUE = "reviews.csv">	</MvIF>
		<MvIF EXPR = "{ ISNULL g.ReviewExport_Append }">	<MvASSIGN NAME = "g.ReviewExport_Append"	VALUE = 0>				</MvIF>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_Start( 'Export Reviews to CSV File', 'EXPT', '' ) }">

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_JavaScript( 'Exporting Reviews to CSV File', 'EXPT', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_CSS() }">

	<script language="JavaScript">
		HaveCustomReset = 1;

		function CustomReset()
		{
			document.EXPT.Reset.value = 'Yes';
		}
	</script>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginScreen_End( 'Export Reviews to CSV File', 'EXPT', '' ) }">
	<input type="hidden" name="Reset" value="No">
	<MvHIDE FIELDS = "g.Module_Code">

	<MvEVAL EXPR = "{ [ g.Module_Admin ].BeginContent() }">
	<table width="100%">
		<tr>
			<td nowrap>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Open( 'Destination', 110 ) }">
				<tr>
					<td nowrap>
						<font face="Arial, Helvetica" size=-1><b>Export Reviews to File:</font></b>
					</td>
					<td width="100%">
						<input type="text" name="ReviewExport_File" size="40" value="{ encodeentities( g.ReviewExport_File ) }">
					</td>
				</tr>

				<tr>
					<td valign="top" nowrap>
						<b>If File Exists:</b>
					</td>
					<td width="100%">
						<MvIF EXPR = "{ g.ReviewExport_Append }">
							<label><input type="radio" name="ReviewExport_Append" value="Yes" checked>Append To File<br></label>
							<label><input type="radio" name="ReviewExport_Append" value="">Replace File</label>
						<MvELSE>
							<label><input type="radio" name="ReviewExport_Append" value="Yes">Append To File</label>

							<br>

							<label><input type="radio" name="ReviewExport_Append" value="" checked>Replace File</label>
						</MvIF>
					</td>
				</tr>

				<tr>
					<td valign="top" nowrap>
						Email File To:
					</td>
					<td width="100%" nowrap>
						<MvIF EXPR = "{ NOT g.ReviewExport_SendEmail }">
							<label><input type="radio" name="ReviewExport_SendEmail" value="0" checked>Do Not Email<br></label>
							<input type="radio" name="ReviewExport_SendEmail" value=1>
						<MvELSE>
							<label><input type="radio" name="ReviewExport_SendEmail" value="0">Do Not Email<br></label>
							<input type="radio" name="ReviewExport_SendEmail" value="1" checked>
						</MvIF>

						<input type="text" size="40" name="ReviewExport_Email" value="{ encodeentities( g.Store:email ) }">
					</td>
				</tr>
				<MvEVAL EXPR = "{ [ g.Module_Admin ].Draw_FieldSet_Close() }">
			</td>
		</tr>
	</table>

	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Element_ExportProgressBar_HTML() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndContent() }">
	<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Buttons( 'Export', 'EXPT' ) }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].EndScreen() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReviewExport_Initialize_Export"  PARAMETERS = "directory, output_file" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.ReviewExport_Export_Initialized }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "g.ReviewExport_Export_ExportedCount"		VALUE = 0>
	<MvASSIGN NAME = "g.ReviewExport_Export_ExportedTotalCount"	VALUE = 0>
	<MvASSIGN NAME = "g.ReviewExport_LastReview"				VALUE = 0>
	<MvASSIGN NAME = "g.ReviewExport_Delimiter"					VALUE = "{ asciichar( 44 ) }">
	<MvASSIGN NAME = "g.ReviewExport_AdditionalFields_Count"	VALUE = "{ TGReviewsAdditionalField_Load_All_Cached( g.ReviewExport_AdditionalFields ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_Utilities ].EnsurePathExists( 'data', l.directory ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ fexists( l.output_file ) }">
		<MvIF EXPR = "{ NOT g.ReviewExport_Append }">
			<MvASSIGN NAME = "l.null" VALUE = "{ fdelete( l.output_file ) }">
			<MvEVAL EXPR = "{ ReviewExport_Export_Header( l.output_file ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ReviewExport_Export_Count( g.ReviewExport_Export_ExportedTotalCount ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "g.ReviewExport_Export_Initialized" VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReviewExport_Finalize_Export" PARAMETERS = "directory, output_file" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.ReviewExport_Export_ExportedCount GE g.ReviewExport_Export_ExportedTotalCount }">
		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_ProgressComplete( g.ReviewExport_Export_ExportedCount $ [ g.Module_Library_Utilities ].Plural( g.ReviewExport_Export_ExportedCount, ' Review', ' Reviews' ) $ ' exported to \'' $ l.output_file $ '\'' ) }">

		<MvIF EXPR = "{ g.ReviewExport_SendEmail }">
			<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Library_Utilities ].Send_Email_Attachment( g.ReviewExport_Email, g.ReviewExport_Email, '', 'Miva Merchant Reviews Export', g.ReviewExport_File, l.directory, 'data' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00054', 'ReviewsExport Email Sent' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ReviewExport_Export_Header" PARAMETERS = "output_file" STANDARDOUTPUTLEVEL = "">
	<MvFOREACH ITERATOR = "l.additionalfield" ARRAY = "g.ReviewExport_AdditionalFields">
		<MvASSIGN NAME = "l.additionalfield_headers" VALUE = "{ l.additionalfield_headers $ g.ReviewExport_Delimiter $ miva_csv_encode( 'FIELD:' $ toupper( l.additionalfield:code ), g.ReviewExport_Delimiter ) }">
	</MvFOREACH>

	<MvEVAL EXPR = "{ ReviewExport_Export_Line( l.output_file, 
		miva_csv_encode( 'CREATED',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'PRODUCT_CODE',	g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'CUSTOMER_LOGIN',	g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'ORDER_ID',		g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'APPROVED',		g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'RATING',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'NAME',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'EMAIL',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'LOCATION',		g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'NOTIFY',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'TITLE',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'SUMMARY',			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'STORE_REPLY',		g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'NOTIFIED',		g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
		miva_csv_encode( 'VERIFIED',		g.ReviewExport_Delimiter ) $ l.additionalfield_headers ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ReviewExport_Export" PARAMETERS = "output_file" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.ReviewExport_Export_ExportedCount GE g.ReviewExport_Export_ExportedTotalCount }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE id > ? ORDER BY id' }"
				FIELDS	= "g.ReviewExport_LastReview">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00055', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGReviewsProducts.d.EOF AND NOT g.ReviewExport_Export_NeedRefresh }">
		<MvASSIGN NAME = "g.ReviewExport_Export_ExportedCount"	VALUE = "{ g.ReviewExport_Export_ExportedCount + 1 }">
		<MvASSIGN NAME = "l.review" 							VALUE = "">
		<MvASSIGN NAME = "l.additionalfields_output"			VALUE = "">

		<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].Draw_ExportProgressBar_Update( g.ReviewExport_Export_ExportedCount, g.ReviewExport_Export_ExportedTotalCount ) }">

		<MvEVAL EXPR = "{ TGReviewsProduct_Read( l.review ) }">

		<MvIF EXPR = "{ [ g.Module_Library_DB ].Product_Load_ID_Cached( l.review:product_id, l.review:product ) }">
			<MvFOREACH ITERATOR = "l.additionalfield" ARRAY = "g.ReviewExport_AdditionalFields">
				<MvASSIGN NAME = "l.additionfield_value:field_id"	VALUE = "{ l.additionalfield:id }">
				<MvASSIGN NAME = "l.additionfield_value:review_id"	VALUE = "{ l.review:id }">
				<MvASSIGN NAME = "l.additionfield_value:value"		VALUE = "">
				<MvASSIGN NAME = "l.null"							VALUE = "{ TGReviewsAdditionalValue_Load( l.additionfield_value ) }">

				<MvASSIGN NAME = "l.additionalfields_output" VALUE = "{ l.additionalfields_output $ g.ReviewExport_Delimiter $ miva_csv_encode( l.additionfield_value:value, g.ReviewExport_Delimiter ) }">
			</MvFOREACH>

			<MvASSIGN NAME = "l.review:formatted_created_date"		VALUE = "{ [ g.Module_Library_Utilities ].Format_Date( l.review:created, s.miva_language ) }">
			<MvASSIGN NAME = "l.review:formatted_created_time"		VALUE = "{ [ g.Module_Library_Utilities ].Format_Time( l.review:created, s.miva_language ) }">
			<MvASSIGN NAME = "l.review:created_formatted"			VALUE = "{ l.review:formatted_created_date $ ' ' $ l.review:formatted_created_time }">

			<MvASSIGN NAME = "l.review:notified_formatted"			VALUE = "">
			<MvIF EXPR = "{ l.review:notified GT 0 }">
				<MvASSIGN NAME = "l.review:formatted_notified_date" VALUE = "{ [ g.Module_Library_Utilities ].Format_Date( l.review:created, s.miva_language ) }">
				<MvASSIGN NAME = "l.review:formatted_notified_time" VALUE = "{ [ g.Module_Library_Utilities ].Format_Time( l.review:created, s.miva_language ) }">
				<MvASSIGN NAME = "l.review:notified_formatted"		VALUE = "{ l.review:formatted_notified_date $ ' ' $ l.review:formatted_notified_time }">
			</MvIF>

			<MvASSIGN NAME = "l.review:customer_login" 				VALUE = "">

			<MvIF EXPR = "{ l.review:cust_id GT 0 }">
				<MvIF EXPR = "{ [ g.Module_Feature_CUS_DB ].Customer_Load_ID_Cached( l.review:cust_id, l.customer ) }">
					<MvASSIGN NAME = "l.review:customer_login"		VALUE = "{ l.customer:login }">
				</MvIF>
			</MvIF>

			<MvEVAL EXPR = "{ ReviewExport_Export_Line( l.output_file, 
				miva_csv_encode( l.review:created_formatted,	g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:product:code,			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:customer_login,		g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:order_id,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:approved,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:rating,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:name,					g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:email,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:location,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:notify,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:title,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:summary,				g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:store_rply,			g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:notified_formatted,	g.ReviewExport_Delimiter ) $ g.ReviewExport_Delimiter $
				miva_csv_encode( l.review:verified,				g.ReviewExport_Delimiter ) $ l.additionalfields_output ) }">

			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00056', 'Review \'' $ TGReviewsProducts.d.id $ '\' exported' ) }">
		</MvIF>

		<MvIF EXPR = "{ ( s.globaltimeout EQ 0 ) OR ( s.globaltimeout GT 20 ) }">
			<MvASSIGN NAME = "l.end_time" VALUE = "20">
		<MvELSE>
			<MvASSIGN NAME = "l.end_time" VALUE = "{ s.globaltimeout / 3 }">
		</MvIF>

		<MvIF EXPR = "{ ( s.dyn_time_t - g.ReviewExport_Export_Time_Start ) GT l.end_time }">
			<MvASSIGN NAME = "g.ReviewExport_LastReview"			VALUE = "{ TGReviewsProducts.d.id }">
			<MvASSIGN NAME = "g.ReviewExport_Export_NeedRefresh"	VALUE = 1>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProducts" ROWS = 1>
	</MvWHILE>

	<MvIF EXPR = "{ g.ReviewExport_Export_NeedRefresh AND TGReviewsProducts.d.EOF }">
		<MvASSIGN NAME = "g.ReviewExport_Export_NeedRefresh" VALUE = 0>
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReviewExport_Export_Count" PARAMETERS = "total_count var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT COUNT( * ) AS total_count FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00057', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvASSIGN NAME = "l.total_count" VALUE = "{ TGReviewsProducts.d.total_count }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ReviewExport_Export_Line" PARAMETERS = "output_file, line" STANDARDOUTPUTLEVEL = "">
	<MvEXPORT FILE = "{ l.output_file }" DELIMITER = "" FIELDS = "l.line">
</MvFUNCTION>


<MvCOMMENT>
|
| Data Import Feature (import)
|
</MvCOMMENT>

<MvFUNCTION NAME = "ImportModule_Delete" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Capabilities" PARAMETERS = "module var, capabilities var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.capabilities:screen"				VALUE = 0>
	<MvASSIGN NAME = "l.capabilities:persistent"			VALUE = 1>
	<MvASSIGN NAME = "l.capabilities:format"				VALUE = "delimited">
	<MvASSIGN NAME = "l.capabilities:persistent_provision"	VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Columns" PARAMETERS = "module var, import var, columns var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>											field			name						header						required						default			validation		</MvCOMMENT>
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'created',				'Created',					'CREATED',				0,								'',			'time_t' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'product_code',			'Product Code',				'PRODUCT_CODE',			1,								'',			'code' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'customer_login',		'Customer Login',			'CUSTOMER_LOGIN',		0,								'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'order_id',				'Order ID',					'ORDER_ID',				0,								0,			'integer_nonneg' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'approved',				'Approved',					'APPROVED',				1,								0,			'bool' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'rating',				'Rating',					'RATING',				1,								'',			'integer_nonneg' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'name',					'Name',						'NAME',					l.general_settings:fld_name,	'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'email',				'Email',					'EMAIL',				l.general_settings:fld_email,	'',			'email' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'location',				'Location',					'LOCATION',				l.general_settings:fld_loc,		'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'notify',				'Notify',					'NOTIFY',				0,								0,			'bool' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'title',				'Title',					'TITLE',				l.general_settings:fld_title,	'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'summary',				'Summary',					'SUMMARY',				l.general_settings:fld_summry,	'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'store_rply',			'Store Reply',				'STORE_REPLY',			0,								'',			'string' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'notified',				'Notified',					'NOTIFIED',				0,								0,			'time_t' ) }">
	<MvEVAL EXPR = "{ Add_Standard_Column( l.columns,	'verified',				'Verified',					'VERIFIED',				0,								0,			'bool' ) }">

	<MvFOREACH ITERATOR = "l.additionalfield" ARRAY = "l.additionalfields" COUNT = "{ TGReviewsAdditionalField_Load_All_Cached( l.additionalfields ) }">
		<MvEVAL EXPR = "{ Add_Standard_Column( l.columns, 'field_' $ l.additionalfield:code, 'Field: ' $ l.additionalfield:name, 'FIELD:'$ toupper( l.additionalfield:code ), 0, 0, 'string' ) }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ miva_array_max( l.columns ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_StatusFields" PARAMETERS = "module var, import var, fields var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.fields"	VALUE = "">

	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'records_processed',	'Records Processed:',	0 ) }">
	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'records_skipped',	'Records Skipped:',		0 ) }">
	<MvEVAL EXPR = "{ Add_Status_Field( l.fields, 'record_problems',	'Record Problems:',		0 ) }">

	<MvFUNCTIONRETURN VALUE = "{ miva_array_max( l.fields ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_Begin" PARAMETERS = "module var, import var, session var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ Create_Columns_Present( l.import, l.run_data:columns_present ) }">

	<MvIF EXPR = "{ l.import:automap EQ 1 }">
		<MvIF EXPR = "{ NOT Validate_Columns( l.import, l.run_data:columns_present ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.list_pos"				VALUE = 0>
	<MvFOREACH ITERATOR = "l.column" ARRAY = "l.run_data:columns_all" INDEX = "l.index" COUNT = "{ ImportModule_Delimited_Columns( l.module, l.import, l.run_data:columns_all ) }">
		<MvIF EXPR = "{ miva_variable_value( 'l.run_data:columns_present:' $ l.column:field ) }">
			<MvASSIGN NAME = "l.column:present"	VALUE = 1>

			<MvREFERENCE NAME = "l.run_data:columns_list"		INDEX = "{ ++l.list_pos }"		VARIABLE = "{ 'l.run_data:columns_all[' $ l.index $ ']' }">
			<MvREFERENCE NAME = "l.run_data:columns_present"	MEMBER = "{ l.column:field }"	VARIABLE = "{ 'l.run_data:columns_all[' $ l.index $ ']' }">
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_Record" PARAMETERS = "module var, import var, session var, record var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvEVAL EXPR = "{ [ g.Module_Feature_IMP_UT ].Import_Session_StatusField_Increment( l.session, 'records_processed', 1 ) }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_Code_Cached( l.record:product_code, l.product ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.record:customer_login }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_CUS_DB ].Customer_Load_Login( l.record:customer_login, l.customer ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.product:id }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Product Code not found' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.record:customer_login AND NOT l.customer:id }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Customer Login not found' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Review_Import( l.import, l.session, l.record, l.product, l.customer, l.run_data ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Delimited_Import_End" PARAMETERS = "module var, import var, session var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Fields" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Invalid" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 0>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Prompt" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Field" PARAMETERS = "module var, field_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Validate" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.import:automap EQ 0 }">
		<MvEVAL EXPR = "{ Create_Columns_Present( l.import, l.columns_present ) }">

		<MvIF EXPR = "{ NOT Validate_Columns( l.import, l.columns_present ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Admin ].FieldError( '', '', g.Error_Message ) }">
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Update" PARAMETERS = "module var, import var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ImportModule_Persistent_Provision" PARAMETERS = "module var, import var, provide_xml var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Review_Import" PARAMETERS = "import var, session var, record var, product var, customer var, run_data var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Validate_Fields_Create( l.session, l.record, l.run_data ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvASSIGN NAME = "l.review:created"		VALUE = "{ l.record:created }">
	<MvASSIGN NAME = "l.review:product_id"	VALUE = "{ l.product:id }">
	<MvASSIGN NAME = "l.review:order_id"	VALUE = "{ l.record:order_id }">
	<MvASSIGN NAME = "l.review:cust_id"		VALUE = 0>
	<MvIF EXPR = "{ l.customer:id }">
		<MvASSIGN NAME = "l.review:cust_id"	VALUE = "{ l.customer:id }">
	</MvIF>
	<MvASSIGN NAME = "l.review:approved"	VALUE = "{ l.record:approved }">
	<MvASSIGN NAME = "l.review:rating"		VALUE = "{ l.record:rating }">
	<MvASSIGN NAME = "l.review:name"		VALUE = "{ l.record:name }">
	<MvASSIGN NAME = "l.review:email"		VALUE = "{ l.record:email }">
	<MvASSIGN NAME = "l.review:location"	VALUE = "{ l.record:location }">
	<MvASSIGN NAME = "l.review:notify"		VALUE = "{ l.record:notify }">
	<MvASSIGN NAME = "l.review:title"		VALUE = "{ l.record:title }">
	<MvASSIGN NAME = "l.review:summary"		VALUE = "{ l.record:summary }">
	<MvASSIGN NAME = "l.review:store_rply"	VALUE = "{ l.record:store_rply }">
	<MvASSIGN NAME = "l.review:notified"	VALUE = "{ l.record:notified }">
	<MvASSIGN NAME = "l.review:verified"	VALUE = "{ l.record:verified }">

	<MvIF EXPR = "{ TGReviewsProducts_Load_Similar( l.review ) }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Similar Review found (product code, title, summary & email match)' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ l.review:rating EQ 0 OR l.review:rating GT 5 }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Rating must be between 1-5.' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.review:email }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( l.review:email ) }">
			<MvEVAL EXPR = "{ Record_Skip( l.session, g.Validation_Message ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvFOREACH ITERATOR = "l.field" ARRAY = "l.members" COUNT = "{ miva_struct_members( l.record, l.members ) }">
		<MvIF EXPR = "{ 'field_' CIN l.field NE 1 }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.review:additionalfields" MEMBER = "{ substring_var( l.field, 7, ( len_var( l.field ) - 6 ) ) }" VALUE = "{ miva_variable_value( 'l.record:' $ l.field ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Insert( l.review ) }">
		<MvEVAL EXPR = "{ Record_Skip( l.session, 'Review failed to create: ' $ g.Error_Message ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00058', 'Added review \'' $ l.review:id $ '\'' ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Validate_Columns" PARAMETERS = "import var, columns_present var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.columns_present:product_code }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00059', 'The Product Code field must be assigned to an import file column' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.columns_present:approved }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00060', 'The Approved field must be assigned to an import file column' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT l.columns_present:rating }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00061', 'The Rating field must be assigned to an import file column' ) }">
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_name }">
		<MvIF EXPR = "{ NOT l.columns_present:name }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00062', 'The Name field must be assigned to an import file column' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_email }">
		<MvIF EXPR = "{ NOT l.columns_present:email }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00063', 'The Email field must be assigned to an import file column' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_loc }">
		<MvIF EXPR = "{ NOT l.columns_present:location }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00064', 'The Location field must be assigned to an import file column' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_title }">
		<MvIF EXPR = "{ NOT l.columns_present:title }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00065', 'The Title field must be assigned to an import file column' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_summry }">
		<MvIF EXPR = "{ NOT l.columns_present:summary }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00066', 'The Summary field must be assigned to an import file column' ) }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.columns_present:rating }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00067', 'The Rating field must be assigned to an import file column' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsGeneralSettings
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsGeneralSettings_Read" PARAMETERS = "general_settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.general_settings:fld_name"		VALUE = "{ TGReviewsGeneralSettings.d.fld_name }">
	<MvASSIGN NAME = "l.general_settings:fld_email"		VALUE = "{ TGReviewsGeneralSettings.d.fld_email }">
	<MvASSIGN NAME = "l.general_settings:fld_loc"		VALUE = "{ TGReviewsGeneralSettings.d.fld_loc }">
	<MvASSIGN NAME = "l.general_settings:fld_summry"	VALUE = "{ TGReviewsGeneralSettings.d.fld_summry }">
	<MvASSIGN NAME = "l.general_settings:fld_title"		VALUE = "{ TGReviewsGeneralSettings.d.fld_title }">
	<MvASSIGN NAME = "l.general_settings:per_page"		VALUE = "{ TGReviewsGeneralSettings.d.per_page }">
	<MvASSIGN NAME = "l.general_settings:sort"			VALUE = "{ TGReviewsGeneralSettings.d.sort }">
	<MvASSIGN NAME = "l.general_settings:auto_apprv"	VALUE = "{ TGReviewsGeneralSettings.d.auto_apprv }">
	<MvASSIGN NAME = "l.general_settings:rc_enabled"	VALUE = "{ TGReviewsGeneralSettings.d.rc_enabled }">
	<MvASSIGN NAME = "l.general_settings:rc_pub_key"	VALUE = "{ TGReviewsGeneralSettings.d.rc_pub_key }">
	<MvASSIGN NAME = "l.general_settings:rc_prv_key"	VALUE = "{ TGReviewsGeneralSettings.d.rc_prv_key }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsGeneralSettings_Load" PARAMETERS = "general_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsGeneralSettings"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsGeneralSettings' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00068', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsGeneralSettings_Read( l.general_settings ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsGeneralSettings">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsGeneralSettings_Load_Cached" PARAMETERS = "general_settings var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:TGReviewsGeneralSettings_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TGReviewsGeneralSettings_Load( l.cache:TGReviewsGeneralSettings ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.general_settings"				VALUE = "{ l.cache:TGReviewsGeneralSettings }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsGeneralSettings_Insert" PARAMETERS = "general_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsGeneralSettings
							( fld_name, fld_email, fld_loc, fld_summry, fld_title, per_page, sort, auto_apprv, rc_enabled, rc_pub_key, rc_prv_key )
						  VALUES
							( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS = "l.general_settings:fld_name, l.general_settings:fld_email, l.general_settings:fld_loc,
					   l.general_settings:fld_summry, l.general_settings:fld_title, l.general_settings:per_page,
					   l.general_settings:sort, l.general_settings:auto_apprv, l.general_settings:rc_enabled,
					   l.general_settings:rc_pub_key, l.general_settings:rc_prv_key">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00069', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsGeneralSettings_Update" PARAMETERS = "general_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsGeneralSettings
			 			  SET
			 				fld_name = ?,
							fld_email = ?,
							fld_loc = ?,
							fld_summry = ?,
							fld_title = ?,
							per_page = ?,
							sort = ?,
							auto_apprv = ?,
							rc_enabled = ?,
							rc_pub_key = ?,
							rc_prv_key = ?' }"
			 FIELDS = "l.general_settings:fld_name, l.general_settings:fld_email, l.general_settings:fld_loc,
					   l.general_settings:fld_summry, l.general_settings:fld_title, l.general_settings:per_page,
					   l.general_settings:sort, l.general_settings:auto_apprv, l.general_settings:rc_enabled,
					   l.general_settings:rc_pub_key, l.general_settings:rc_prv_key">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00070', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsNotificationSettings
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsNotificationSettings_Read" PARAMETERS = "notification_settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.notification_settings:ma_enable"	VALUE = "{ TGReviewsNotificationSettings.d.ma_enable }">
	<MvASSIGN NAME = "l.notification_settings:ma_days"		VALUE = "{ TGReviewsNotificationSettings.d.ma_days }">
	<MvASSIGN NAME = "l.notification_settings:ma_after"		VALUE = "{ TGReviewsNotificationSettings.d.ma_after }">
	<MvASSIGN NAME = "l.notification_settings:ma_from"		VALUE = "{ TGReviewsNotificationSettings.d.ma_from }">
	<MvASSIGN NAME = "l.notification_settings:ma_cc"		VALUE = "{ TGReviewsNotificationSettings.d.ma_cc }">
	<MvASSIGN NAME = "l.notification_settings:ma_bcc"		VALUE = "{ TGReviewsNotificationSettings.d.ma_bcc }">
	<MvASSIGN NAME = "l.notification_settings:ma_subject"	VALUE = "{ TGReviewsNotificationSettings.d.ma_subject }">
	<MvASSIGN NAME = "l.notification_settings:mn_enable"	VALUE = "{ TGReviewsNotificationSettings.d.mn_enable }">
	<MvASSIGN NAME = "l.notification_settings:mn_to"		VALUE = "{ TGReviewsNotificationSettings.d.mn_to }">
	<MvASSIGN NAME = "l.notification_settings:mn_from"		VALUE = "{ TGReviewsNotificationSettings.d.mn_from }">
	<MvASSIGN NAME = "l.notification_settings:mn_cc"		VALUE = "{ TGReviewsNotificationSettings.d.mn_cc }">
	<MvASSIGN NAME = "l.notification_settings:mn_bcc"		VALUE = "{ TGReviewsNotificationSettings.d.mn_bcc }">
	<MvASSIGN NAME = "l.notification_settings:mn_subject"	VALUE = "{ TGReviewsNotificationSettings.d.mn_subject }">
	<MvASSIGN NAME = "l.notification_settings:cn_from"		VALUE = "{ TGReviewsNotificationSettings.d.cn_from }">
	<MvASSIGN NAME = "l.notification_settings:cn_cc"		VALUE = "{ TGReviewsNotificationSettings.d.cn_cc }">
	<MvASSIGN NAME = "l.notification_settings:cn_bcc"		VALUE = "{ TGReviewsNotificationSettings.d.cn_bcc }">
	<MvASSIGN NAME = "l.notification_settings:cn_subject"	VALUE = "{ TGReviewsNotificationSettings.d.cn_subject }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsNotificationSettings_Load" PARAMETERS = "notification_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsNotificationSettings"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsNotificationSettings' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00071', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsNotificationSettings_Read( l.notification_settings ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsNotificationSettings">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsNotificationSettings_Load_Cached" PARAMETERS = "notification_settings var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:TGReviewsNotificationSettings_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TGReviewsNotificationSettings_Load( l.cache:TGReviewsNotificationSettings ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.notification_settings"			VALUE = "{ l.cache:TGReviewsNotificationSettings }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsNotificationSettings_Insert" PARAMETERS = "notification_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsNotificationSettings
							( ma_enable, ma_days, ma_after, ma_from, ma_cc, ma_bcc, ma_subject, mn_enable, mn_to, mn_from, mn_cc, mn_bcc, mn_subject, cn_from, cn_cc, cn_bcc, cn_subject )
						  VALUES
							( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS = "l.notification_settings:ma_enable, l.notification_settings:ma_days, l.notification_settings:ma_after,
					   l.notification_settings:ma_from, l.notification_settings:ma_cc, l.notification_settings:ma_bcc,
					   l.notification_settings:ma_subject, l.notification_settings:mn_enable, l.notification_settings:mn_to,
					   l.notification_settings:mn_from, l.notification_settings:mn_cc, l.notification_settings:mn_bcc,
					   l.notification_settings:mn_subject, l.notification_settings:cn_from, l.notification_settings:cn_cc,
					   l.notification_settings:cn_bcc, l.notification_settings:cn_subject">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00072', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsNotificationSettings_Update" PARAMETERS = "notification_settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsNotificationSettings
			 			  SET
			 				ma_enable = ?,
							ma_days = ?,
							ma_after = ?,
							ma_from = ?,
							ma_cc = ?,
							ma_bcc = ?,
							ma_subject = ?,
							mn_enable = ?,
							mn_to = ?,
							mn_from = ?,
							mn_cc = ?,
							mn_bcc = ?,
							mn_subject = ?,
							cn_from = ?,
							cn_cc = ?,
							cn_bcc = ?,
							cn_subject = ?' }"
			 FIELDS = "l.notification_settings:ma_enable, l.notification_settings:ma_days, l.notification_settings:ma_after,
					   l.notification_settings:ma_from, l.notification_settings:ma_cc, l.notification_settings:ma_bcc,
					   l.notification_settings:ma_subject, l.notification_settings:mn_enable, l.notification_settings:mn_to,
					   l.notification_settings:mn_from, l.notification_settings:mn_cc, l.notification_settings:mn_bcc,
					   l.notification_settings:mn_subject, l.notification_settings:cn_from, l.notification_settings:cn_cc,
					   l.notification_settings:cn_bcc, l.notification_settings:cn_subject">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00073', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsTemplates
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsTemplates_Read" PARAMETERS = "templates var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.templates:ma_tmpl_id"	VALUE = "{ TGReviewsTemplates.d.ma_tmpl_id }">
	<MvASSIGN NAME = "l.templates:mn_tmpl_id"	VALUE = "{ TGReviewsTemplates.d.mn_tmpl_id }">
	<MvASSIGN NAME = "l.templates:cn_tmpl_id"	VALUE = "{ TGReviewsTemplates.d.cn_tmpl_id }">
	<MvASSIGN NAME = "l.templates:form_tmpl"	VALUE = "{ TGReviewsTemplates.d.form_tmpl }">
	<MvASSIGN NAME = "l.templates:rvws_tmpl"	VALUE = "{ TGReviewsTemplates.d.rvws_tmpl }">
	<MvASSIGN NAME = "l.templates:prlog_tmpl"	VALUE = "{ TGReviewsTemplates.d.prlog_tmpl }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsTemplates_Load" PARAMETERS = "templates var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsTemplates"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsTemplates' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00074', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsTemplates_Read( l.templates ) }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsTemplates">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsTemplates_Load_Cached" PARAMETERS = "templates var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:TGReviewsTemplates_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TGReviewsTemplates_Load( l.cache:templates ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.templates"						VALUE = "{ l.cache:templates }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsTemplates_Insert" PARAMETERS = "templates var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsTemplates
							( ma_tmpl_id, mn_tmpl_id, cn_tmpl_id, form_tmpl, rvws_tmpl, prlog_tmpl )
						  VALUES
							( ?, ?, ?, ?, ?, ? )' }"
			 FIELDS = "l.templates:ma_tmpl_id, l.templates:mn_tmpl_id, l.templates:cn_tmpl_id, l.templates:form_tmpl, l.templates:rvws_tmpl, l.templates:prlog_tmpl">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00075', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsTemplates_Update" PARAMETERS = "settings var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsTemplates
			 			  SET
							ma_tmpl_id = ?,
							mn_tmpl_id = ?,
							cn_tmpl_id = ?,
							form_tmpl = ?,
							rvws_tmpl = ?,
							prlog_tmpl = ?' }"
			 FIELDS = "l.templates:ma_tmpl_id, l.templates:mn_tmpl_id, l.templates:cn_tmpl_id, l.templates:form_tmpl, l.templates:rvws_tmpl, l.templates:prlog_tmpl">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00076', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsProducts
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsProduct_Read" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.review:id"			VALUE = "{ TGReviewsProducts.d.id }">
	<MvASSIGN NAME = "l.review:created"		VALUE = "{ TGReviewsProducts.d.created }">
	<MvASSIGN NAME = "l.review:product_id"	VALUE = "{ TGReviewsProducts.d.product_id }">
	<MvASSIGN NAME = "l.review:cust_id"		VALUE = "{ TGReviewsProducts.d.cust_id }">
	<MvASSIGN NAME = "l.review:order_id"	VALUE = "{ TGReviewsProducts.d.order_id }">
	<MvASSIGN NAME = "l.review:approved"	VALUE = "{ TGReviewsProducts.d.approved }">
	<MvASSIGN NAME = "l.review:rating"		VALUE = "{ TGReviewsProducts.d.rating }">
	<MvASSIGN NAME = "l.review:name"		VALUE = "{ TGReviewsProducts.d.name }">
	<MvASSIGN NAME = "l.review:email"		VALUE = "{ TGReviewsProducts.d.email }">
	<MvASSIGN NAME = "l.review:location"	VALUE = "{ TGReviewsProducts.d.location }">
	<MvASSIGN NAME = "l.review:notify"		VALUE = "{ TGReviewsProducts.d.notify }">
	<MvASSIGN NAME = "l.review:title"		VALUE = "{ TGReviewsProducts.d.title }">
	<MvASSIGN NAME = "l.review:summary"		VALUE = "{ TGReviewsProducts.d.summary }">
	<MvASSIGN NAME = "l.review:store_rply"	VALUE = "{ TGReviewsProducts.d.store_rply }">
	<MvASSIGN NAME = "l.review:notified"	VALUE = "{ TGReviewsProducts.d.notified }">
	<MvASSIGN NAME = "l.review:verified"	VALUE = "{ TGReviewsProducts.d.verified }">

	<MvCOMMENT>
	|
	|	Backwards Compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.review:store_reply"	VALUE = "{ TGReviewsProducts.d.store_rply }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Load" PARAMETERS = "id, review var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00077', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProducts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00078' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsProduct_Read( l.review ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Insert" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT TGReviewsProduct_Insert_LowLevel( l.review ) OR
					NOT TGReviewsAdditionalValues_InsertUpdate( l.review ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Insert_LowLevel" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.review:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGRProduct' ) }">

	<MvIF EXPR = "{ ISNULL l.review:created }">		<MvASSIGN NAME = "l.review:created"		VALUE = "{ s.dyn_time_t }"> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:product_id }">	<MvASSIGN NAME = "l.review:product_id"	VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:cust_id }">		<MvASSIGN NAME = "l.review:cust_id"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:order_id }">	<MvASSIGN NAME = "l.review:order_id"	VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:approved }">	<MvASSIGN NAME = "l.review:approved"	VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:rating }">		<MvASSIGN NAME = "l.review:rating"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:notify }">		<MvASSIGN NAME = "l.review:notify"		VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:notified }">	<MvASSIGN NAME = "l.review:notified"	VALUE = 0> </MvIF>
	<MvIF EXPR = "{ ISNULL l.review:verified }">	<MvASSIGN NAME = "l.review:verified"	VALUE = "{ Product_Review_Check_Verified( l.review ) }"> </MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsProducts
							( id, created, product_id, cust_id, order_id, approved, rating, name, email, location, notify, title, summary, store_rply, notified, verified )
						  VALUES
							( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )' }"
			 FIELDS	= "l.review:id, l.review:created, l.review:product_id, l.review:cust_id, l.review:order_id,
					   l.review:approved, l.review:rating, l.review:name, l.review:email, l.review:location, l.review:notify,
					   l.review:title, l.review:summary, l.review:store_rply, l.review:notified, l.review:verified">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00079', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProductsAverages_Update_Product( l.review:product_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Update" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT TGReviewsProduct_Update_LowLevel( l.review ) OR
					NOT TGReviewsAdditionalValues_InsertUpdate( l.review ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMIT NAME = "Merchant">

	<MvASSIGN NAME = "l.null" VALUE = "{ Customer_Notification_Email_Trigger( l.review ) }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Update_LowLevel" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsProducts
			 			  SET
							created = ?,
							product_id = ?,
							cust_id = ?,
							order_id = ?,
							approved = ?,
							rating = ?,
							name = ?,
							email = ?,
							location = ?,
							notify = ?,
							title = ?,
							summary = ?,
							store_rply = ?,
							notified = ?,
							verified = ?
			 			  WHERE
			 				id	= ?' }"
			 FIELDS = "l.review:created, l.review:product_id, l.review:cust_id, l.review:order_id, l.review:approved,
					   l.review:rating, l.review:name, l.review:email, l.review:location, l.review:notify, l.review:title,
					   l.review:summary, l.review:store_rply, l.review:notified, l.review:verified, l.review:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00080', g.MvQUERY_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProductsAverages_Update_Product( l.review:product_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Delete" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsProduct_Load( l.id, l.review ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvTRANSACT NAME = "Merchant">

	<MvIF EXPR = "{ NOT TGReviewsProduct_Delete_LowLevel( l.id ) OR
					NOT TGReviewsAdditionalValues_Delete_Review( l.id ) }">
		<MvROLLBACK NAME = "Merchant">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMIT NAME = "Merchant">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProducts_Delete_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProductsDelete"
				QUERY	= "{ 'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00081', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProductsDelete.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsDelete">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00082' ) }">
	</MvIF>

	<MvWHILE EXPR = "{ NOT TGReviewsProductsDelete.d.EOF }">
		<MvIF EXPR = "{ NOT TGReviewsProduct_Delete( TGReviewsProductsDelete.d.id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProductsDelete" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsDelete">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProduct_Delete_LowLevel" PARAMETERS = "id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE id = ?' }"
			 FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00083', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverage_Load_Cached" PARAMETERS = "product_id, product_rating var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id"								VALUE = "{ int( l.product_id ) }">

	<MvIF EXPR = "{ l.id LT 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00084' ) }">
	<MvELSEIF EXPR = "{ l.id EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ TGReviewsProductsAverageAll_Load_Cached( l.product_rating ) }">
	</MvIF>
	

	<MvREFERENCEARRAY NAME = "l.cache"					VARIABLE = "g.Session:cache:TGReviewsProductsAverage_Load_Cached">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result" 				VALUE = "{ TGReviewsProductsAverage_Load( l.id, l.cache:product_rating ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE =  "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.product_rating"					VALUE = "{ l.cache:product_rating }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverageAll_Load_Cached" PARAMETERS = "product_rating var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache"						VARIABLE = "g.Session:cache:tgreviewsproductsaverageall_load_cached">

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TGReviewsProductsAverageAll_Load( l.cache:product_rating ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.product_rating"				VALUE = "{ l.cache:product_rating }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverageAll_Load" PARAMETERS = "review_averages var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProductsAverages"
				QUERY	= "{ 'SELECT ROUND( AVG( rating ), 2) as tgr_rating, COUNT( rating ) as tgr_review_count FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00144', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProductsAverages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00145' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.review_averages:rating"				VALUE = "{ TGReviewsProductsAverages.d.tgr_rating }">
	<MvASSIGN NAME = "l.review_averages:rtng_count"			VALUE = "{ TGReviewsProductsAverages.d.tgr_review_count }">

	<MvCOMMENT>
	|
	|	Backwards Compatibility for the customfield codes
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.review_averages:tgr_rating"			VALUE = "{ TGReviewsProductsAverages.d.tgr_rating }">
	<MvASSIGN NAME = "l.review_averages:tgr_review_count"	VALUE = "{ TGReviewsProductsAverages.d.tgr_review_count }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProducts_Load_Similar" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE product_id = ? AND title = ? AND summary = ? AND email = ?' }"
				FIELDS	= "l.review:product_id, l.review:title, l.review:summary, l.review:email">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00085', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProducts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00086' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsProduct_Read( l.review ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsProductsAverages
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsProductsAverage_Read" PARAMETERS = "review_averages var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.review_averages:product_id"			VALUE = "{ TGReviewsProductsAverages.d.product_id }">
	<MvASSIGN NAME = "l.review_averages:rating"				VALUE = "{ TGReviewsProductsAverages.d.rating }">
	<MvASSIGN NAME = "l.review_averages:rtng_count"			VALUE = "{ TGReviewsProductsAverages.d.rtng_count }">

	<MvCOMMENT>
	|
	|	Backwards Compatibility for the customfield codes
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.review_averages:tgr_rating"			VALUE = "{ TGReviewsProductsAverages.d.rating }">
	<MvASSIGN NAME = "l.review_averages:tgr_review_count"	VALUE = "{ TGReviewsProductsAverages.d.rtng_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverage_Load" PARAMETERS = "product_id, review_averages var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.product_id EQ 0 }">
		<MvFUNCTIONRETURN VALUE = "{ TGReviewsProductsAverageAll_Load( l.review_averages ) }">
	</MvIF>

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProductsAverages"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages WHERE product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00087', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProductsAverages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00088' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsProductsAverage_Read( l.review_averages ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverage_Insert" PARAMETERS = "review_averages var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages
							( product_id, rating, rtng_count )
						  VALUES
							( ?, ?, ? )' }"
			 FIELDS	= "l.review_averages:product_id, l.review_averages:rating, l.review_averages:rtng_count">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00089', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverage_Update" PARAMETERS = "review_averages var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages
			 			  SET
							rating = ?,
							rtng_count = ?
			 			  WHERE
							product_id = ?' }"
			 FIELDS = "l.review_averages:rating, l.review_averages:rtng_count, l.review_averages:product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00090', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverages_UpdateAll" PARAMETERS = "" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{ 'SELECT product_id, AVG( rating ) as tgr_rating, COUNT( id ) AS tgr_review_count FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE approved = 1 GROUP BY product_id' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00091', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProducts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00092' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGReviewsProducts.d.EOF }">
		<MvASSIGN NAME = "l.count" VALUE = "{ l.count + 1 }">

		<MvASSIGN NAME = "l.review_averages:product_id"	VALUE = "{ TGReviewsProducts.d.product_id }">
		<MvASSIGN NAME = "l.review_averages:rating"		VALUE = "{ TGReviewsProducts.d.tgr_rating ROUND 2 }">
		<MvASSIGN NAME = "l.review_averages:rtng_count"	VALUE = "{ TGReviewsProducts.d.tgr_review_count }">

		<MvIF EXPR = "{ NOT TGReviewsProductsAverage_InsertUpdate( l.review_averages ) }">
			<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsProducts" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-SYS-TGR-00093', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverage_InsertUpdate" PARAMETERS = "review_averages var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.review_averages:rtng_count EQ 0 }">
		<MvIF EXPR = "{ NOT TGReviewsProductsAverage_Delete( l.review_averages:product_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	<MvELSEIF EXPR = "{ NOT TGReviewsProductsAverage_Insert( l.review_averages ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">	<MvFUNCTIONRETURN VALUE = 0>
		<MvELSEIF EXPR = "{ NOT TGReviewsProductsAverage_Update( l.review_averages ) }">						<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverage_Delete" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProductsAverages WHERE product_id = ?' }"
			 FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00094', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsProductsAverages_Update_Product" PARAMETERS = "product_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsProductsAverages"
				QUERY	= "{ 'SELECT AVG( rating ) as tgr_rating, COUNT( id ) AS tgr_review_count FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE approved = 1 AND product_id = ?' }"
				FIELDS	= "l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00095', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProductsAverages.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">	
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00096' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.review_averages:product_id"	VALUE = "{ l.product_id }">
	<MvASSIGN NAME = "l.review_averages:rating"		VALUE = "{ TGReviewsProductsAverages.d.tgr_rating ROUND 2 }">
	<MvASSIGN NAME = "l.review_averages:rtng_count"	VALUE = "{ TGReviewsProductsAverages.d.tgr_review_count }">

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProductsAverages">

	<MvIF EXPR = "{ NOT TGReviewsProductsAverage_InsertUpdate( l.review_averages ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsAdditionalFields
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Read" PARAMETERS = "additionalfield var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.additionalfield:id"		VALUE = "{ TGReviewsAdditionalFields.d.id }">
	<MvASSIGN NAME = "l.additionalfield:code"	VALUE = "{ TGReviewsAdditionalFields.d.code }">
	<MvASSIGN NAME = "l.additionalfield:name"	VALUE = "{ TGReviewsAdditionalFields.d.name }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Load_ID" PARAMETERS = "id, additionalfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsAdditionalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields WHERE id = ?' }"
				FIELDS	= "l.id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00097', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsAdditionalFields.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00098' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsAdditionalField_Read( l.additionalfield ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Load_ID_Cached" PARAMETERS = "id, additionalfield var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id"								VALUE = "{ int( l.id ) }">

	<MvIF EXPR = "{ l.id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00099' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache"					VARIABLE = "g.Session:cache:tgreviewsadditionalfield_load_id_cached">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result" 				VALUE = "{ TGReviewsAdditionalField_Load_ID( l.id, l.cache:additionalfield ) }">
		<MvIF EXPR = "{ NOT l.cache:result }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE =  "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.additionalfield"				VALUE = "{ l.cache:additionalfield }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Load_All" PARAMETERS = "additionalfields var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsAdditionalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00100', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsAdditionalFields.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00101' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGReviewsAdditionalFields.d.EOF }">
		<MvEVAL EXPR = "{ TGReviewsAdditionalField_Read( l.additionalfields[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsAdditionalFields" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-SYS-TGR-00102', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Load_All_Cached" PARAMETERS = "additionalfields var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCE NAME = "l.cache" VARIABLE = "g.Session:cache:tgreviewsadditionalfield_load_all">

	<MvIF EXPR = "{ ISNULL l.cache:count }">
		<MvASSIGN NAME = "l.cache:count"				VALUE = "{ TGReviewsAdditionalField_Load_All( l.cache:additionalfields ) }">
		<MvIF EXPR = "{ l.cache:count EQ 0 }">
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.cache:count EQ 0 }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.additionalfields"				VALUE = "{ l.cache:additionalfields }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:count }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Load_Code" PARAMETERS = "code, additionalfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsAdditionalFields"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields WHERE ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( 'code' ) $ ' = ' $ [ g.Module_Library_Native_DBAPI ].DB_Compare_UPPER( '?' ) }"
				FIELDS	= "l.code">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00103', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsAdditionalFields.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00104' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsAdditionalField_Read( l.additionalfield ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalFields">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Load_Code_Cached" PARAMETERS = "code, additionalfield var" STANDARDOUTPUTLEVEL = "">
	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:tgreviewsadditionalfield_load_code_cached">
		<MvMEMBER NAME = "{ l.code }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TGReviewsAdditionalField_Load_Code( l.code, l.cache:additionalfield ) }">

		<MvIF EXPR = "{ l.cache:result }">
			<MvREFERENCE NAME = "g.Session:cache:tgreviewsadditionalfield_load_id_cached" INDEX = "{ l.cache:additionalfield:id }" VARIABLE = "l.cache">
		<MvELSE>
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.additionalfield"				VALUE = "{ l.cache:additionalfield }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Insert" PARAMETERS = "additionalfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.additionalfield:id" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Generate( 'TGRAdditional' ) }">

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields
							( id, code, name )
						  VALUES
							( ?, ?, ? )' }"
			 FIELDS	= "l.additionalfield:id, l.additionalfield:code, l.additionalfield:name">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00105', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Update" PARAMETERS = "additionalfield var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields
			 			  SET
							code = ?,
							name = ?
			 			  WHERE
			 				id	= ?' }"
			 FIELDS = "l.additionalfield:code, l.additionalfield:name, l.additionalfield:id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00106', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Delete" PARAMETERS = "additionalfield_id" STANDARDOUTPUTLEVEL = "">	
	<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Delete_LowLevel( l.additionalfield_id ) OR
					NOT TGReviewsAdditionalValues_Delete_Field( l.additionalfield_id ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalField_Delete_LowLevel" PARAMETERS = "additionalfield_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalFields WHERE id = ?' }"
			 FIELDS	= "l.additionalfield_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00107', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| sNN_TGReviewsAdditionalValues
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Read" PARAMETERS = "additionalvalue var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.additionalvalue:field_id"	VALUE = "{ TGReviewsAdditionalValues.d.field_id }">
	<MvASSIGN NAME = "l.additionalvalue:review_id"	VALUE = "{ TGReviewsAdditionalValues.d.review_id }">
	<MvASSIGN NAME = "l.additionalvalue:value"		VALUE = "{ TGReviewsAdditionalValues.d.value }">

	<MvCOMMENT>
	|
	| For backwards compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.additionalvalue:rating_id" VALUE = "{ l.additionalvalue:review_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Load" PARAMETERS = "additionalvalue var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsAdditionalValues"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues WHERE field_id = ? AND review_id = ?' }"
				FIELDS	= "l.additionalvalue:field_id, l.additionalvalue:review_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00108', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsAdditionalValues.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalValues">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00109' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsAdditionalValue_Read( l.additionalvalue ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsAdditionalValues">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Load_All_Review" PARAMETERS = "review_id, additionfieldsvalues var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.additionfieldsvalues"		VALUE = "">
	<MvASSIGN NAME = "l.additionfieldsvalues_count"	VALUE = 0>
	<MvASSIGN NAME = "l.additionalfields_count"		VALUE = "{ TGReviewsAdditionalField_Load_All_Cached( l.additionalfields ) }">

	<MvFOREACH ITERATOR = "l.additionalfield" ARRAY = "l.additionalfields" COUNT = "{ l.additionalfields_count }">
		<MvASSIGN NAME = "l.additionfield_value:field_id"	VALUE = "{ l.additionalfield:id }">
		<MvASSIGN NAME = "l.additionfield_value:review_id"	VALUE = "{ l.review_id }">
		<MvASSIGN NAME = "l.additionfield_value:value"		VALUE = "">
		<MvASSIGN NAME = "l.null"							VALUE = "{ TGReviewsAdditionalValue_Load( l.additionfield_value ) }">
		<MvASSIGN NAME = "l.additionfieldsvalues_count"		VALUE = "{ l.additionfieldsvalues_count + 1 }">

		<MvASSIGN NAME = "l.additionfieldsvalues" INDEX = "{ l.additionfieldsvalues_count }" MEMBER = "code"	VALUE = "{ l.additionalfield:code }">
		<MvASSIGN NAME = "l.additionfieldsvalues" INDEX = "{ l.additionfieldsvalues_count }" MEMBER = "name"	VALUE = "{ l.additionalfield:name }">
		<MvASSIGN NAME = "l.additionfieldsvalues" INDEX = "{ l.additionfieldsvalues_count }" MEMBER = "value"	VALUE = "{ l.additionfield_value:value }">
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = "{ l.additionfieldsvalues_count }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Load_All_Review_Cached" PARAMETERS = "review_id, additionfieldsvalues var"  STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.id"								VALUE = "{ int( l.review_id ) }">

	<MvIF EXPR = "{ l.id LE 0 }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00110' ) }">
	</MvIF>

	<MvREFERENCEARRAY NAME = "l.cache" 					VARIABLE = "g.Session:cache:tgreviewsadditionalvalue_load_all_review_cached">
		<MvDIMENSION INDEX = "{ l.id }">
	</MvREFERENCEARRAY>

	<MvIF EXPR = "{ ISNULL l.cache:result }">
		<MvASSIGN NAME = "l.cache:result"				VALUE = "{ TGReviewsAdditionalValue_Load_All_Review( l.id, l.cache:additionfieldsvalues ) }">

		<MvIF EXPR = "{ l.cache:result }">
			<MvREFERENCE NAME = "g.Session:cache:tgreviewsadditionalvalue_load_all_review_cached" INDEX = "{ l.id }" VARIABLE = "l.cache">
		<MvELSE>
			<MvASSIGN NAME = "l.cache:error_code"		VALUE = "{ g.Error_Code }">
			<MvASSIGN NAME = "l.cache:error_message"	VALUE = "{ g.Error_Message }">
			<MvASSIGN NAME = "l.cache:error_db_eof"		VALUE = "{ g.Error_DB_EOF }">
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT l.cache:result }">
		<MvIF EXPR = "{ l.cache:error_db_eof }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( l.cache:error_code ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( l.cache:error_code, l.cache:error_message ) }">
	</MvIF>

	<MvASSIGN NAME = "l.additionfieldsvalues"				VALUE = "{ l.cache:additionfieldsvalues }">

	<MvFUNCTIONRETURN VALUE = "{ l.cache:result }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Insert" PARAMETERS = "additionalvalue var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues
							( field_id, review_id, value )
						  VALUES
							( ?, ?, ? )' }"
			 FIELDS	= "l.additionalvalue:field_id, l.additionalvalue:review_id, l.additionalvalue:value">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00111', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValues_InsertUpdate" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.review:additionalfields }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFOREACH ITERATOR = "l.additionalfield_code" ARRAY = "l.additionalfields" COUNT = "{ miva_struct_members( l.review:additionalfields, l.additionalfields ) }">
		<MvIF EXPR = "{ NOT TGReviewsAdditionalField_Load_Code_Cached( l.additionalfield_code, l.additionalfield ) }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvASSIGN NAME = "l.additionalvalue:field_id"	VALUE = "{ l.additionalfield:id }">
		<MvASSIGN NAME = "l.additionalvalue:review_id"	VALUE = "{ l.review:id }">
		<MvASSIGN NAME = "l.additionalvalue:value"		VALUE = "{ miva_variable_value( 'l.review:additionalfields:' $ l.additionalfield_code) }">

		<MvIF EXPR = "{ NOT TGReviewsAdditionalValue_Insert( l.additionalvalue ) }">
			<MvIF EXPR = "{ NOT [ g.Module_Library_Native_DBAPI ].DB_Error_IsIndexDuplicate( g.Error_Message ) }">	<MvFUNCTIONRETURN VALUE = 0>
			<MvELSEIF EXPR = "{ NOT TGReviewsAdditionalValue_Update( l.additionalvalue ) }">						<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Update" PARAMETERS = "additionalvalue var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME 	= "Merchant"
			 QUERY 	= "{ 'UPDATE ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues
			 			  SET
							value = ?
			 			  WHERE
							field_id = ? AND
							review_id = ?' }"
			 FIELDS = "l.additionalvalue:value, l.additionalvalue:field_id, l.additionalvalue:review_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00112', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValue_Delete" PARAMETERS = "additionalvalue var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues WHERE field_id = ? AND review_id = ?' }"
			 FIELDS	= "l.additionalvalue:field_id, l.additionalvalue:review_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00113', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValues_Delete_Field" PARAMETERS = "field_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues WHERE field_id = ?' }"
			 FIELDS	= "l.field_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00114', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsAdditionalValues_Delete_Review" PARAMETERS = "review_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsAdditionalValues WHERE review_id = ?' }"
			 FIELDS	= "l.review_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00115', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	sNN_TGReviewsOrders
|
</MvCOMMENT>

<MvFUNCTION NAME = "TGReviewsOrder_Read" PARAMETERS = "order_id var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.order_id"	VALUE = "{ TGReviewsOrders.d.order_id }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsOrder_Load" PARAMETERS = "order_id, order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsOrders"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsOrders WHERE order_id = ?' }"
				FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00116', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsOrders.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsOrders">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00117' ) }">
	</MvIF>

	<MvEVAL EXPR = "{ TGReviewsOrder_Read( l.order ) }">
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsOrders">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsOrder_Load_All" PARAMETERS = "order_ids var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.order_ids" VALUE = "">

	<MvOPENVIEW NAME	= "Merchant"
				VIEW	= "TGReviewsOrders"
				QUERY	= "{ 'SELECT * FROM ' $ g.Store_Table_Prefix $ 'TGReviewsOrders' }">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00118', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsOrders.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsOrders">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00119' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.count" VALUE = 0>

	<MvWHILE EXPR = "{ NOT TGReviewsOrders.d.EOF }">
		<MvEVAL EXPR = "{ TGReviewsOrder_Read( l.order_ids[ ++l.count ] ) }">

		<MvSKIP NAME = "Merchant" VIEW = "TGReviewsOrders" ROWS = 1>
	</MvWHILE>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsOrders">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].ListLoad_EOF_Return( 'MOD-SYS-TGR-00120', l.count ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsOrder_Insert" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGReviewsOrders
							( order_id )
						  VALUES
							( ? )' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00121', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "TGReviewsOrder_Delete" PARAMETERS = "order_id" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'DELETE FROM ' $ g.Store_Table_Prefix $ 'TGReviewsOrders WHERE order_id = ?' }"
			 FIELDS	= "l.order_id">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00122', g.MvQUERY_Error ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
| System Module Content: Template Helper Function
|
</MvCOMMENT>

<MvFUNCTION NAME = "Admin_Template" PARAMETERS = "name, field_prefix, template_id, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ miva_variable_value( 'g.' $ l.field_prefix $ 'template_clear_history' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Delete_History( l.template_id ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Template_Current( l.template_id, l.template ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "{ l.field_prefix $ 'template_version' }"				VALUE = "{ l.template:id }">
		<MvASSIGN NAME = "{ l.field_prefix $ 'template_version_selected' }"		VALUE = "{ l.template:id }">
	<MvELSEIF EXPR = "{ miva_variable_value( 'g.' $ l.field_prefix $ 'template_recall' ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_ID(  miva_variable_value( 'g.' $ l.field_prefix $ 'template_version_selected' ), l.template ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvASSIGN NAME = "l.load_fields"										VALUE = 1>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_Template_Current( l.template_id, l.template ) }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "{ 'g.' $ l.field_prefix $ 'notes' }"						VALUE = "">

	<MvIF EXPR = "{ g.Data_Entry_Error AND NOT ISNULL miva_variable_value( 'g.' $ l.field_prefix $ 'template_code' ) }">
		<MvASSIGN NAME = "{ 'g.' $ l.field_prefix $ 'template_code' }"			VALUE = "{ miva_variable_value( 'g.' $ l.field_prefix $ 'template_code' ) }">
	<MvELSE>
		<MvASSIGN NAME = "{ 'g.' $ l.field_prefix $ 'template_code' }"			VALUE = "{ l.template:source }">
	</MvIF>

	<MvASSIGN NAME = "{ 'g.' $ l.field_prefix $ 'template_version' }"			VALUE = "{ l.template:id }">
	<MvASSIGN NAME = "{ 'g.' $ l.field_prefix $ 'template_version_selected' }"	VALUE = "{ l.template:id }">

	<tr>
		<td colspan="2" width="100%">
			<MvEVAL EXPR = "{ [ g.Module_Library_Utilities ].DrawTemplateTextArea( l.field_prefix $ 'template_code', miva_variable_value( 'g.' $  l.field_prefix $ 'template_code' ), 20, 100 ) }">
		</td>
	</tr>
	<tr>
		<td>
			Notes:
		</td>
		<td width="100%">
			<input type="text" name="{ encodeentities( l.field_prefix ) $ 'notes' }" value="{ encodeentities( miva_variable_value( 'g.' $  l.field_prefix $ 'notes' ) ) }" size="40">
		</td>
	</tr>
	<tr>
		<td>
			Versions:
		</td>
		<td width="100%">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'template_recall' }" value="0">
			<input type="hidden" name="{ encodeentities( l.field_prefix ) $ 'template_clear_history' }" value="0">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_AD ].TUI_Hide_ComponentFields( l.field_prefix, 'template_version' ) }">
			<MvEVAL EXPR = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_ManagedTemplate_Version_Select( l.template_id, l.field_prefix $ 'template_version_selected', miva_variable_value( 'g.' $ l.field_prefix $ 'template_version_selected' ) ) }">
			<a href="#" onclick="{ 'Toggle( \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'template_recall' $ '\', 1 ); return false;' }">Recall</a>
			<a href="#" onclick="{ 'Toggle_Prompt( \'Are you sure you wish to delete all templates for ' $ l.name $ '?\\r\\nThis action cannot be undone.\', \'' $ [ g.Module_Admin ].JavaScriptEncode( l.field_prefix ) $ 'template_clear_history' $ '\', 1 ); return false;' }">Clear History</a>
		</td>
	</tr>
</MvFUNCTION>

<MvFUNCTION NAME = "Admin_Template_Update" PARAMETERS = "module var, name, field_prefix, template_id" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplateVersion_Load_ID( miva_variable_value( 'g.' $ l.field_prefix $ 'template_version' ), l.template ) OR
					NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.template_id, l.managedtemplate ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ crypto_md5( miva_variable_value( 'g.' $ l.field_prefix $ 'template_code' ) ) EQ crypto_md5( l.template:source ) }">
		<MvIF EXPR = "{ l.template:id NE l.managedtemplate:current_id }">
			<MvASSIGN NAME = "l.managedtemplate:current_id"	VALUE = "{ l.template:id }">

			<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Revert_ManagedTemplateVersion( l.managedtemplate, l.template, l.compile_error ) }">
				<MvIF EXPR = "{ len( l.compile_error ) }">
					<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', l.field_prefix $ 'template_code', l.compile_error ) }">
					<MvFUNCTIONRETURN VALUE = 1>
				</MvIF>

				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00123', l.module:name $ ' ' $ l.name $ ' template reverted' ) }">
		</MvIF>
	<MvELSE>
		<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_MGR ].TemplateManager_Create_ManagedTemplateVersion( l.managedtemplate, miva_variable_value( 'g.' $ l.field_prefix $ 'notes' ), miva_variable_value( 'g.' $ l.field_prefix $ 'template_code' ), l.settings, l.compile_error ) }">
			<MvIF EXPR = "{ len( l.compile_error ) }">
				<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Admin ].FieldError( 'TGR_SETT', l.field_prefix $ 'template_code', l.compile_error ) }">
				<MvFUNCTIONRETURN VALUE = 1>
			</MvIF>

			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvEVAL EXPR = "{ [ g.Module_Admin ].Admin_Log_Action( 'MOD-SYS-TGR-00124',  l.module:name $ ' ' $ l.name $ ' template updated' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	Front-end User Submission
|
</MvCOMMENT>

<MvFUNCTION NAME = "Product_Review_Insert" PARAMETERS = "all_settings var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsTemplates_Load_Cached( l.templates ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].ManagedTemplate_Load_ID( l.templates:prlog_tmpl, l.prelogic_template ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "g.TGR_Error"							VALUE = "">
	<MvASSIGN NAME = "g.TGR_Review_ID"						VALUE = "">
	<MvASSIGN NAME = "g.TGR_Review_Success"					VALUE = 0>
	<MvASSIGN NAME = "l.all_settings:tgreviews_prelogic"	VALUE = "">

	<MvCAPTURE VARIABLE = "l.null">
		<MvEVAL EXPR = "{ [ g.Store_Template_Path $ l.prelogic_template:filename ].Template_Render( l.null, l.all_settings ) }">
	</MvCAPTURE>

	<MvIF EXPR = "{ NOT ISNULL l.all_settings:tgreviews_prelogic:allow_submission AND l.all_settings:tgreviews_prelogic:allow_submission EQ 0 }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsGeneralSettings_Load_Cached( l.general_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "g.TGR_Product_Code"	VALUE = "{ trim( g.TGR_Product_Code ) }">
	<MvASSIGN NAME = "g.TGR_Order_ID"		VALUE = "{ int( g.TGR_Order_ID ) }">
	<MvASSIGN NAME = "g.TGR_Rating"			VALUE = "{ int( g.TGR_Rating ) }">
	<MvASSIGN NAME = "g.TGR_Name"			VALUE = "{ trim( g.TGR_Name ) }">
	<MvASSIGN NAME = "g.TGR_Email"			VALUE = "{ trim( g.TGR_Email ) }">
	<MvASSIGN NAME = "g.TGR_Location"		VALUE = "{ trim( g.TGR_Location ) }">
	<MvASSIGN NAME = "g.TGR_Notify"			VALUE = "{ [ g.Module_Admin ].Trim_Boolean( g.TGR_Notify ) }">
	<MvASSIGN NAME = "g.TGR_Title"			VALUE = "{ trim( g.TGR_Title ) }">
	<MvASSIGN NAME = "g.TGR_Summary"		VALUE = "{ trim( g.TGR_Summary ) }">

	<MvIF EXPR = "{ g.TGR_Rating EQ 0 OR g.TGR_Rating GT 5 }">
		<MvASSIGN NAME = "g.TGR_Error"			VALUE = "Rating must be between 1-5.">
		<MvASSIGN NAME = "g.TGR_Rating_Class"	VALUE = "error has-error">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_name }">
		<MvIF EXPR = "{ ISNULL g.TGR_Name }">
			<MvASSIGN NAME = "g.TGR_Error"		VALUE = "Name is required.">
			<MvASSIGN NAME = "g.TGR_Name_Class"	VALUE = "error has-error">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ ( l.general_settings:fld_email ) OR ( NOT ISNULL g.TGR_Email ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Admin ].Validate_Email( g.TGR_Email ) }">
			<MvASSIGN NAME = "g.TGR_Error"			VALUE = "{ g.Validation_Message }">
			<MvASSIGN NAME = "g.TGR_Email_Class"	VALUE = "error has-error">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_loc }">
		<MvIF EXPR = "{ ISNULL g.TGR_Name }">
			<MvASSIGN NAME = "g.TGR_Error"			VALUE = "Location is required.">
			<MvASSIGN NAME = "g.TGR_Location_Class"	VALUE = "error has-error">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_summry }">
		<MvIF EXPR = "{ ISNULL g.TGR_Summary }">
			<MvASSIGN NAME = "g.TGR_Error"			VALUE = "Summary is required.">
			<MvASSIGN NAME = "g.TGR_Summary_Class"	VALUE = "error has-error">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:fld_title }">
		<MvIF EXPR = "{ ISNULL g.TGR_Title }">
			<MvASSIGN NAME = "g.TGR_Error"			VALUE = "Title is required.">
			<MvASSIGN NAME = "g.TGR_Title_Class"	VALUE = "error has-error">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Runtime_Product_Load_Code_Cached( g.TGR_Product_Code, l.product ) }">
		<MvASSIGN NAME = "g.TGR_Error" VALUE = "Invalid Product">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ ISNULL l.all_settings:tgreviews_prelogic:allow_same_reviewer OR l.all_settings:tgreviews_prelogic:allow_same_reviewer EQ 0 }">
		<MvIF EXPR = "{ NOT ISNULL g.TGR_Email }">
			<MvIF EXPR = "{ Review_Check_EmailXProduct( g.TGR_Email, l.product:id ) }">
				<MvASSIGN NAME = "g.TGR_Error" VALUE = "You have reviewed this product already.">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>

		<MvIF EXPR = "{ g.Basket:cust_id GT 0 }">
			<MvIF EXPR = "{ Review_Check_CustomerXProduct( g.Basket:cust_id, l.product:id ) }">
				<MvASSIGN NAME = "g.TGR_Error" VALUE = "You have reviewed this product already.">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ l.general_settings:rc_enabled }">
		<MvIF EXPR = "{ NOT reCAPTCHA( l.general_settings:rc_prv_key, miva_variable_value( 'g.g-recaptcha-response' ) ) }">
			<MvASSIGN NAME = "g.TGR_Error" VALUE = "Invalid reCAPTCHA.">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>
	</MvIF>

	<MvASSIGN NAME = "l.review:created"				VALUE = "{ s.dyn_time_t }">
	<MvASSIGN NAME = "l.review:product_id"			VALUE = "{ l.product:id }">
	<MvASSIGN NAME = "l.review:cust_id"				VALUE = "{ g.Basket:cust_id }">
	<MvASSIGN NAME = "l.review:order_id"			VALUE = "{ g.TGR_Order_ID }">
	<MvASSIGN NAME = "l.review:approved"			VALUE = 0>
	<MvASSIGN NAME = "l.review:rating"				VALUE = "{ g.TGR_Rating }">
	<MvASSIGN NAME = "l.review:name"				VALUE = "{ g.TGR_Name }">
	<MvASSIGN NAME = "l.review:email"				VALUE = "{ g.TGR_Email }">
	<MvASSIGN NAME = "l.review:location"			VALUE = "{ g.TGR_Location }">
	<MvASSIGN NAME = "l.review:notify"				VALUE = "{ g.TGR_Notify }">
	<MvASSIGN NAME = "l.review:title"				VALUE = "{ g.TGR_Title }">
	<MvASSIGN NAME = "l.review:summary"				VALUE = "{ g.TGR_Summary }">
	<MvASSIGN NAME = "l.review:additionalfields"	VALUE = "{ g.TGR_AdditionalFields }">

	<MvIF EXPR = "{ l.general_settings:auto_apprv }">
		<MvASSIGN NAME = "l.review:approved" VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT ISNULL l.all_settings:tgreviews_prelogic:approve }">
		<MvIF EXPR = "{ l.all_settings:tgreviews_prelogic:approve EQ 1 }">
			<MvASSIGN NAME = "l.review:approved" VALUE = 1>
		<MvELSEIF EXPR = "{ l.all_settings:tgreviews_prelogic:approve EQ 0 }">
			<MvASSIGN NAME = "l.review:approved" VALUE = 0>
		</MvIF>
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProducts_Load_Similar( l.review ) }">
		<MvASSIGN NAME = "g.TGR_Error" VALUE = "Review has already been submitted.">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsProduct_Insert( l.review ) }">
		<MvASSIGN NAME = "g.TGR_Error" VALUE = "{ g.Error_Code $ ': ' $ g.Error_Message }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.null" VALUE = "{ Merchant_Notification_Email_Trigger( l.review ) }">

	<MvASSIGN NAME = "l.null" VALUE = "{ Customer_Notification_Email_Trigger( l.review ) }">

	<MvASSIGN NAME = "g.TGR_Product_Code"		VALUE = "">
	<MvASSIGN NAME = "g.TGR_Order_ID"			VALUE = "">
	<MvASSIGN NAME = "g.TGR_Rating"				VALUE = "">
	<MvASSIGN NAME = "g.TGR_Name"				VALUE = "">
	<MvASSIGN NAME = "g.TGR_Email"				VALUE = "">
	<MvASSIGN NAME = "g.TGR_Location"			VALUE = "">
	<MvASSIGN NAME = "g.TGR_Notify"				VALUE = "">
	<MvASSIGN NAME = "g.TGR_Title"				VALUE = "">
	<MvASSIGN NAME = "g.TGR_Summary"			VALUE = "">
	<MvASSIGN NAME = "g.TGR_AdditionalFields"	VALUE = "">
	<MvASSIGN NAME = "g.TGR_Review_ID"			VALUE = "{ l.review:id }">
	<MvASSIGN NAME = "g.TGR_Review_Success"		VALUE = 1>

	<MvFUNCTIONRETURN VALUE = 1>
</MVFUNCTION>

<MvFUNCTION NAME = "reCAPTCHA" PARAMETERS = "secret, response" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.response }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.remoteip"	VALUE = "{ s.remote_addr }">

	<MIVA MvCALL_ERROR = "nonfatal, nodisplay">
	<MvCALL METHOD = "POST"
			ACTION = "https://www.google.com/recaptcha/api/siteverify"
			FIELDS = "l.secret, l.response, l.remoteip">
			<MvASSIGN NAME = "l.google_response" VALUE = "{ l.google_response $ s.callvalue }">
	</MvCALL>
	<MIVA MvCALL_ERROR = "fatal, display">

	<MvFUNCTIONRETURN VALUE = "{ miva_json_decode( l.google_response, l.data ) AND l.data:success }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Review_Check_Verified" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.review:cust_id GT 0 }">
		<MvIF EXPR = "{ Product_Review_Check_Verified_Customer( l.review ) }">
			<MvFUNCTIONRETURN VALUE = 1>
		</MvIF>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Product_Review_Check_Verified_Email( l.review ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Review_Check_Verified_Customer" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = ""  ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "Product_Review_Check_Verified"
				QUERY	= "{'SELECT
								o.id
							 FROM '
								$ g.Store_Table_Prefix $ 'Orders o
							 LEFT JOIN '
								$ g.Store_Table_Prefix $ 'OrderItems oi
							 ON
								oi.order_id = o.id
							 WHERE
								o.cust_id = ? AND
								oi.product_id = ?
							 LIMIT 1' }"
				FIELDS	= "l.review:cust_id, l.review:product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00141', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Product_Review_Check_Verified.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Product_Review_Check_Verified">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00142' ) }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Product_Review_Check_Verified">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Product_Review_Check_Verified_Email" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = ""  ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "Product_Review_Check_Verified"
				QUERY	= "{'SELECT
								o.id
							 FROM '
								$ g.Store_Table_Prefix $ 'Orders o
							 LEFT JOIN '
								$ g.Store_Table_Prefix $ 'OrderItems oi
							 ON
								oi.order_id = o.id
							 WHERE
								( o.bill_email = ? OR o.ship_email = ? ) AND
								oi.product_id = ?
							 LIMIT 1' }"
				FIELDS	= "l.review:email, l.review:email, l.review:product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00125', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ Product_Review_Check_Verified.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "Product_Review_Check_Verified">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00126' ) }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "Product_Review_Check_Verified">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Review_Check_EmailXProduct" PARAMETERS = "email, product_id" STANDARDOUTPUTLEVEL = ""  ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE email = ? AND product_id = ? LIMIT 1' }"
				FIELDS	= "l.email, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00127', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProducts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00128' ) }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Review_Check_CustomerXProduct" PARAMETERS = "cust_id, product_id" STANDARDOUTPUTLEVEL = ""  ERROROUTPUTLEVEL = "">
	<MvOPENVIEW	NAME	= "Merchant"
				VIEW	= "TGReviewsProducts"
				QUERY	= "{'SELECT id FROM ' $ g.Store_Table_Prefix $ 'TGReviewsProducts WHERE cust_id = ? AND product_id = ? LIMIT 1' }"
				FIELDS	= "l.cust_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00139', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ TGReviewsProducts.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00140' ) }">
	</MvIF>

	<MvCLOSEVIEW NAME = "Merchant" VIEW = "TGReviewsProducts">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Admin_Formatted_Item_Function" PARAMETERS = "item_name, function_name, parameters" STANDARDOUTPUTLEVEL = "html, text">
	<MvIF EXPR = "{ l.function_name }">
		<div style="background: #ffffff; overflow:auto;width:auto;padding:.2em 0;">
		<pre style="margin: 0; line-height: 125%;"><span style="color: #446fbd">&lt;mvt:item</span> <span style="color: #6d8600">name=</span><span style="color: #535353">&quot;<MvEVAL EXPR = "{ l.item_name }">&quot;</span> <span style="color: #6d8600">param=</span><span style="color: #535353">&quot;<span style="color: #446fbd"><MvEVAL EXPR = "{ l.function_name }"></span>(<MvEVAL EXPR = "{ l.parameters }">)&quot;</span> <span style="color: #446fbd">/&gt;</span></pre>
		</div>
	<MvELSE>
		<div style="background: #ffffff; overflow:auto;width:auto;padding:.2em 0;">
		<pre style="margin: 0; line-height: 125%;"><span style="color: #446fbd">&lt;mvt:item</span> <span style="color: #6d8600">name=</span><span style="color: #535353">&quot;<MvEVAL EXPR = "{ l.item_name }">&quot;</span> <span style="color: #446fbd">/&gt;</span></pre>
		</div>
	</MvIF>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Clientside_Output_File" PARAMETERS = "module var, filename" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ l.filename EQ 'functions.js' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html"><MvINCLUDE INTERPRET = "off" FILE = "js/functions.js"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.filename EQ 'productlookupcolumn.js' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html"><MvINCLUDE INTERPRET = "off" FILE = "js/productlookupcolumn.js"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.filename EQ 'productreviewlist.js' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html"><MvINCLUDE INTERPRET = "off" FILE = "js/productreviewlist.js"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSEIF EXPR = "{ l.filename EQ 'additionalfieldlist.js' }">
		<MIVA STANDARDOUTPUTLEVEL = "text, html"><MvINCLUDE INTERPRET = "off" FILE = "js/additionalfieldlist.js"><MIVA STANDARDOUTPUTLEVEL = "">
	<MvELSE>
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvCOMMENT>
|
|	Email Triggers
|
</MvCOMMENT>

<MvFUNCTION NAME = "Merchant_Notification_Email_Trigger" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load_Cached( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.notification_settings:mn_enable }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.email:to" 		VALUE = "{ l.notification_settings:mn_to }">
	<MvASSIGN NAME = "l.email:from"		VALUE = "{ l.notification_settings:mn_from }">
	<MvASSIGN NAME = "l.email:cc"		VALUE = "{ l.notification_settings:mn_cc }">
	<MvASSIGN NAME = "l.email:bcc"		VALUE = "{ l.notification_settings:mn_bcc }">
	<MvASSIGN NAME = "l.email:subject"	VALUE = "{ l.notification_settings:mn_subject }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID_Cached( l.review:product_id, l.review:product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.review:additionalfields_count" VALUE = "{ TGReviewsAdditionalValue_Load_All_Review_Cached( l.review:id, l.review:additionalfields ) }">

	<MvCOMMENT>
	|
	|	Backwards Compatibility
	|
	</MvCOMMENT>

	<MvASSIGN NAME = "l.review:additional_fields" VALUE = "{ l.review:additionalfields }">

	<MvFUNCTIONRETURN VALUE = "{ Trigger_Email( l.review, l.null, 'TGR_Merchant_Email', l.email ) }">
</MVFUNCTION>

<MvFUNCTION NAME = "Customer_Notification_Email_Trigger" PARAMETERS = "review var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT l.review:approved	OR
					NOT l.review:notify		OR
					l.review:notified		OR
					ISNULL l.review:email }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load_Cached( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.email:to" 		VALUE = "{ l.review:email }">
	<MvASSIGN NAME = "l.email:from"		VALUE = "{ l.notification_settings:cn_from }">
	<MvASSIGN NAME = "l.email:cc"		VALUE = "{ l.notification_settings:cn_cc }">
	<MvASSIGN NAME = "l.email:bcc"		VALUE = "{ l.notification_settings:cn_bcc }">
	<MvASSIGN NAME = "l.email:subject"	VALUE = "{ l.notification_settings:cn_subject }">

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Product_Load_ID_Cached( l.review:product_id, l.review:product ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT Trigger_Email( l.review, l.null, 'TGR_Customer_Email', l.email ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.review:notified" VALUE = "{ s.dyn_time_t }">

	<MvIF EXPR = "{ NOT TGReviewsProduct_Update_LowLevel( l.review ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MVFUNCTION>

<MvFUNCTION NAME = "MailAfter_Email_Trigger" PARAMETERS = "order var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Load_ID( l.order:id, l.order ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load_Cached( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.email:to" 		VALUE = "{ l.order:bill_email }">
	<MvASSIGN NAME = "l.email:from"		VALUE = "{ l.notification_settings:ma_from }">
	<MvASSIGN NAME = "l.email:cc"		VALUE = "{ l.notification_settings:ma_cc }">
	<MvASSIGN NAME = "l.email:bcc"		VALUE = "{ l.notification_settings:ma_bcc }">
	<MvASSIGN NAME = "l.email:subject"	VALUE = "{ l.notification_settings:ma_subject }">

	<MvIF EXPR = "{ NOT MailAfter_Email_Trigger_LowLevel( l.order, l.email ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ TGReviewsOrder_Delete( l.order:id ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "MailAfter_Email_Trigger_LowLevel" PARAMETERS = "order var, email var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].OrderItemList_Load_Order( l.order:id, l.order_items ) }">
		<MvASSIGN NAME = "l.null" VALUE = "{ TGReviewsOrder_Delete( l.order:id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00129', 'Reviews: Mail After - Removed Order \''$ l.order:id $ '\' (no items in order).' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.order:item_count" VALUE = 0>

	<MvFOREACH ITERATOR = "l.item" ARRAY = "l.order_items">
		<MvIF EXPR = "{ Review_Check_EmailXProduct( l.order:bill_email, l.item:product_id ) }">
			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.order:cust_id GT 0 }">
			<MvIF EXPR = "{ Review_Check_CustomerXProduct( l.order:cust_id, l.item:product_id ) }">
				<MvFOREACHCONTINUE>
			</MvIF>
		</MvIF>


		<MvASSIGN NAME = "l.item:options_count"	VALUE = "{ [ g.Module_Library_DB ].OrderOptionList_Load_Line( l.item:line_id, l.item:options ) }">
		<MvASSIGN NAME = "l.order:item_count"	VALUE = "{ miva_array_insert_var( l.order:items, l.item, -1 ) }">
	</MvFOREACH>

	<MvIF EXPR = "{ l.order:item_count EQ 0 }">
		<MvASSIGN NAME = "l.null" VALUE = "{ TGReviewsOrder_Delete( l.order:id ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00130', 'Reviews: Mail After - Removed Order \'' $ l.order:id $ '\' (no products to review).' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ Trigger_Email( l.null, l.order, 'TGR_MailAfter_Email', l.email ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "Trigger_Email" PARAMETERS = "review var, order var, page_code, email var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ ISNULL l.email:from }">			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00131', 'Empty From address' ) }">
	<MvELSEIF EXPR = "{ ISNULL l.email:to }">		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00132', 'Empty To address' ) }">
	<MvELSEIF EXPR = "{ ISNULL l.email:subject }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00133', 'Empty Subject' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Feature_TUI_DB ].Page_Load_Code( l.page_code, l.page ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.page:settings:review"	VALUE = "{ l.review }">
	<MvASSIGN NAME = "l.page:settings:order"	VALUE = "{ l.order }">

	<MvCAPTURE VARIABLE = "l.message"><MvASSIGN NAME = "l.result" VALUE = "{ [ g.Module_Feature_TUI_MGR ].TemplateManager_Render_Page_LowLevel( l.page:code, l.page ) }"></MvCAPTURE>

	<MvIF EXPR = "{ ISNULL l.message }">
		<MvIF EXPR = "{ l.order:id }">
			<MvASSIGN NAME = "l.null" VALUE = "{ TGReviewsOrder_Delete( l.order:id ) }">
			<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00135', 'Reviews: Mail After - Removed Order \'' $ l.order:id $ '\' (no email message).' ) }">
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvASSIGN NAME = "l.email:from"		VALUE = "{ trim( l.email:from ) }">
	<MvASSIGN NAME = "l.email:to"		VALUE = "{ trim( l.email:to ) }">
	<MvASSIGN NAME = "l.email:cc"		VALUE = "{ trim( l.email:cc ) }">
	<MvASSIGN NAME = "l.email:bcc" 		VALUE = "{ trim( l.email:bcc ) }">
	<MvASSIGN NAME = "l.email:subject"	VALUE = "{ trim( l.email:subject ) }">
	<MvASSIGN NAME = "l.mime_type"		VALUE = "{ 'text/html; charset=' $ g.Store:charset }">
	<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Library_Utilities ].StartMimeEmail( l.mime_email ) }">
	<MvASSIGN NAME = "l.null"			VALUE = "{ [ g.Module_Library_Utilities ].GenerateBodyMIME( l.mime_email, l.mime_type, '', '', l.message ) }">

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].v9_SendMimeEmail( l.mime_email, l.email ) }">
</MVFUNCTION>

<MvFUNCTION NAME = "MailAfter_Email_Trigger_All" PARAMETERS = "task var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT TGReviewsNotificationSettings_Load_Cached( l.notification_settings ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvIF EXPR = "{ NOT l.notification_settings:ma_enable }">
		<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'I', 'Reviews: Mail After not enabled' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvIF EXPR = "{ NOT TGReviewsOrder_Load_All( l.order_ids ) }">
		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Error_Is_EOF() }">
			<MvFUNCTIONRETURN VALUE = 0>
		</MvIF>

		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ MailAfter_Email_Trigger_All_LowLevel( l.task, l.order_ids, l.notification_settings ) }">
</MvFUNCTION>

<MvFUNCTION NAME = "MailAfter_Email_Trigger_All_LowLevel" PARAMETERS = "task var, order_ids var, notification_settings var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.timestamp_check" VALUE = "{ s.dyn_time_t - ( 86400 * l.notification_settings:ma_days ) }">

	<MvFOREACH ITERATOR = "l.order_id" ARRAY = "l.order_ids">
		<MvASSIGN NAME = "l.order" VALUE = "">

		<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].Order_Load_ID( l.order_id, l.order ) }">
			<MvIF EXPR = "{ NOT TGReviewsOrder_Delete( l.order_id ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ ( l.order:status EQ 600 ) OR ( l.order:status EQ 300 ) }">
			<MvIF EXPR = "{ NOT TGReviewsOrder_Delete( l.order:id ) }">
				<MvFUNCTIONRETURN VALUE = 0>
			</MvIF>

			<MvFOREACHCONTINUE>
		</MvIF>

		<MvIF EXPR = "{ l.notification_settings:ma_after EQ 'order' }">
			<MvIF EXPR = "{ l.order:orderdate LE l.timestamp_check }">
				<MvIF EXPR = "{ MailAfter_Email_Trigger( l.order ) }">
					<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'I', 'Reviews: Mail After Email triggered for Order \'' $ l.order:id $ '\'' ) }">
				<MvELSE>
					<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'E', g.Error_Code $ ': ' $ g.Error_Message ) }">
				</MvIF>
			</MvIF>
		<MvELSEIF EXPR = "{ [ g.Module_Library_DB ].OrderShipmentList_Load_Order( l.order:id, l.order:shipments ) }">
			<MvIF EXPR = "{ NOT miva_array_search( l.order:shipments, 1, l.shipment, 'l.shipment:ship_date EQ 0 OR l.shipment:ship_date GT l.timestamp_check OR l.shipment:status NE 200' ) }">
				<MvIF EXPR = "{ MailAfter_Email_Trigger( l.order ) }">
					<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'I', 'Reviews: Mail After Email triggered for Order \'' $ l.order:id $ '\'' ) }">
				<MvELSE>
					<MvASSIGN NAME = "l.null" VALUE = "{ [ g.Module_Feature_SCH_DB ].ScheduledTaskLog_Insert( l.task:id, 'E', g.Error_Code $ ': ' $ g.Error_Message ) }">
				</MvIF>
			</MvIF>
		</MvIF>
	</MvFOREACH>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Load_Product_MostRecent_Order_Customer" PARAMETERS = "product_id, cust_id, order var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvOPENVIEW NAME	= "Merchant"
				VIEW 	= "OrderItems"
				QUERY 	= "{ 'SELECT
								orders.cust_id		AS cust_id,
								orders.orderdate	AS orderdate,
								oi.order_id			AS order_id,
								oi.line_id			AS line_id,
								oi.group_id			AS group_id,
								oi.status			AS status,
								oi.product_id		AS product_id,
								oi.code				AS code,
								oi.name				AS name,
								oi.sku				AS sku,
								oi.retail			AS retail,
								oi.base_price		AS base_price,
								oi.price			AS price,
								oi.weight			AS weight,
								oi.taxable			AS taxable,
								oi.upsold			AS upsold,
								oi.quantity			AS quantity
							FROM ' $ 
								g.Store_Table_Prefix $ 'OrderItems oi 
								LEFT OUTER JOIN ' $ g.Store_Table_Prefix $ 'Orders orders ON orders.id = oi.order_id
							WHERE
								orders.cust_id	= ? AND
								oi.product_id 	= ?
							ORDER BY
								orders.orderdate DESC
							LIMIT 1' }"
				FIELDS = "l.cust_id, l.product_id">
	<MvIF EXPR = "{ g.MvOPENVIEW_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'MOD-SYS-TGR-00152', g.MvOPENVIEW_Error ) }">
	</MvIF>

	<MvIF EXPR = "{ OrderItems.d.EOF }">
		<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_DB ].Error_Load_EOF( 'MOD-SYS-TGR-00153' ) }">
	</MvIF>

	<MvASSIGN NAME = "l.order:cust_id"		VALUE = "{ OrderItems.d.cust_id }">
	<MvASSIGN NAME = "l.order:orderdate"	VALUE = "{ OrderItems.d.orderdate }">
	<MvASSIGN NAME = "l.order:order_id"		VALUE = "{ OrderItems.d.order_id }">
	<MvASSIGN NAME = "l.order:line_id"		VALUE = "{ OrderItems.d.line_id }">
	<MvASSIGN NAME = "l.order:group_id"		VALUE = "{ OrderItems.d.group_id }">
	<MvASSIGN NAME = "l.order:status"		VALUE = "{ OrderItems.d.status }">
	<MvASSIGN NAME = "l.order:product_id"	VALUE = "{ OrderItems.d.product_id }">
	<MvASSIGN NAME = "l.order:code"			VALUE = "{ OrderItems.d.code }">
	<MvASSIGN NAME = "l.order:name"			VALUE = "{ OrderItems.d.name }">
	<MvASSIGN NAME = "l.order:sku"			VALUE = "{ OrderItems.d.sku }">
	<MvASSIGN NAME = "l.order:retail"		VALUE = "{ OrderItems.d.retail }">
	<MvASSIGN NAME = "l.order:base_price"	VALUE = "{ OrderItems.d.base_price }">
	<MvASSIGN NAME = "l.order:price"		VALUE = "{ OrderItems.d.price }">
	<MvASSIGN NAME = "l.order:weight"		VALUE = "{ OrderItems.d.weight }">
	<MvASSIGN NAME = "l.order:taxable"		VALUE = "{ OrderItems.d.taxable }">
	<MvASSIGN NAME = "l.order:upsold"		VALUE = "{ OrderItems.d.upsold }">
	<MvASSIGN NAME = "l.order:quantity"		VALUE = "{ OrderItems.d.quantity }">
		
	<MvCLOSEVIEW NAME = "Merchant" VIEW = "OrderItems">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvINCLUDE FILE = "import_include.mv">
